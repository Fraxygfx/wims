!if $wims_read_parm=$empty
 !exit
!endif
!if $wims_read_parm=help
 !if $lang=cn
  把拖拉填充或点击填充的栏放进一个大的图形里.
 用于OEF练习的陈述. 例如:
  <pre>
 \special{imagefill \imagedir/myphoto.jpg,450x350,40x40
 reply 1,120x250
 reply 3,300x50
 reply 4,10x15
 }
 </pre>
这个例子把一个大的图形(<tt>\imagedir/myphoto.jpg</tt>)放在陈述里,
大小是 450x350, 有 3 个 40x40 的拖拉-放置栏.
3个栏的正确答案分别是 1,3,4(必须是点击填充或拖拉填充类型的),
它们在大图中的位置分别是 120x250, 300x50, 10x15.
 <p> 大图会自动伸展以填满指定的区域.
 !exit
!endif
 Put dragfill or clickfill fields into a big image. To be used in the
 statement of an OEF exercise. Example:
 <pre>
\special{imagefill \imagedir/myphoto.jpg,450x350,40x40
reply 1,120x250
reply 3,300x50
reply 4,10x15
}
</pre>
 This example puts a big image (<tt>\imagedir/myphoto.jpg</tt>) in the
 statement, of size
 450x350, with 3 embedded drag-and-drop fields of size 40x40. The 3 fields
 are respectively answers 1,3 and 4 (which must be of clickfill or dragfill
 type), and at respective positions 120x250, 300x50, 10x15 within the big
 image.
 <p> The big image will be stretched to fit exactly the specified size.
 !exit
!endif
!set parm=!replace internal $	$ by $ ; $ in $wims_read_parm
!set parm1=!row 1 of $parm
!distribute items $parm1 into imgurl,bigsize,smallsize
!set bigsize=!translate x, to $  $ in $bigsize
!distribute words $bigsize into bigsizex,bigsizey
!set ssize=!translate x, to $  $ in $smallsize
!distribute words $ssize into ssizex,ssizey
!set parm2=!row 2 to -1 of $parm
!set parm2=!nonempty rows $parm2
!set icnt=!rowcnt $parm2

!if $printing=yes or $cmd=hint
 <img src=$imgurl width=$bigsizex height=$bigsizey align=top>
 !exit
!endif

!set fill_reposition=yes
!reset flist hlist
!default fillcnt=0
!set oldfillcnt=$[$fillcnt+1]
!for t=1 to $icnt
 !set l=!row $t of $parm2
 !set l=!translate x to , in $l
 !distribute item $l into n_,x_,y_
 !set n_=!text select 0123456789()+-*/ in $n_
 !set n_=$[$n_]
 !bound n_ between integer 1 and 100 default $
 !if $n_!=$empty and r$n_ notitemof $embedded and\
    $(replytype$n_) iswordof clickfill dragfill
  !default wims_fill_type=$(replytype$n_)
  !set embedded=!append item r$n_ to $embedded
  !set flist=!append item $n_ to $flist
  !set i=$n_
  !set inputsize=$smallsize 1
  !read anstype/fill.inc noshow
  !set hlist=$hlist<input type=hidden name=reply$n_ value="">
  !set fill_namelist=!append item $oldfillcnt to $fill_namelist
  !increase fillcnt
  !set serial_$fillcnt=$i
  !set reposition_$fillcnt=$x_,$y_
 !endif
!next t

<a name=fill$oldfillcnt id=fill$oldfillcnt><img src=gifs/transparent.gif
width=$bigsizex height=$bigsizey align=top></a>
!set thisjs=var f_bigimage$oldfillcnt=new DynLayer();\
f_bigimage$oldfillcnt.setAnchor({topA:"fill$oldfillcnt",leftA:"fill$oldfillcnt",\
stretchH:$bigsizex,stretchV:$bigsizey});\
dynapi.document.addChild(f_bigimage$oldfillcnt);\
f_bigimage$oldfillcnt.setHTML("<img src=$imgurl width=$bigsizex height=$bigsizey>");

!if $q_form=yes
 $hlist
 !set fillafter_js=$fillafter_js\
$thisjs
!else
 !distribute item 40C040,FF4040 into colorr_good,colorr_bad
 !set colorr_ana=C8C8E0
 !distribute item 20,15 into Sx,Sy
 !set cc1="<table border=0 cellpadding=0 cellspacing=0 width=$ssizex height=$ssizey><tr><td align=center>"
 !set cc2="</td></tr></table>"
 !set wims_ins_alt=none
 !read js/dynapi3.phtml
 <script language=javascript>
 dynapi.onLoad(function() {
  $thisjs
 !for c=$oldfillcnt to $fillcnt
  !set i=$(serial_$c)
  !set embcnt=!positionof item r$i in $embedded
  !default reply_$i=$(reply$i)
  !set p_=$(reply_$i)
  !set tt=!word 1 of $(replygood$i)
  !if $tt!=?analyze
    !set cl=$(colorr_$(diareply$i))
  !else
    !set cl=$colorr_ana
    !set embedded_analyze=!append item $i to $embedded_analyze
!!    !set embedded_bracket=$embedded_bracket [$embcnt]
  !endif
  !set replyname$i=[$embcnt]
  !distribute item $(reposition_$c) into rx,ry
  var f_l$c=new DynLayer();
  var f_t$c=new DynLayer();
  f_l$c.setAnchor({topA:'fill$oldfillcnt',leftA:'fill$oldfillcnt',
    left:$rx,top:$ry,stretchH:$ssizex,stretchV:$ssizey});
  dynapi.document.addChild(f_l$c);
  f_l$c.setBgColor("#$cl");
  !set ss=!replace internal " by \" in $(reply$i)
  f_l$c.setHTML($cc1+"$ss"+$cc2);
  !if $tt!=?analyze
   f_t$c.setAnchor({topA:'fill$oldfillcnt',leftA:'fill$oldfillcnt',
    left:$rx+$ssizex-4,top:$ry,stretchH:$Sx,stretchV:$Sy});
   dynapi.document.addChild(f_t$c);
   f_t$c.setHTML("<small><small>[$embcnt]</small></small>");
   f_t$c.setZIndex(100);
  !endif
 !next c
 });
 </script>
!endif

