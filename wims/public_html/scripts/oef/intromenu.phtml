!nocache
!! useful when one comes from modtool
!set modu_lang=$lang
!read oef/$modu_lang/names
!if $lang_exists!=yes
   !let modu_lang=en
   !read oef/$modu_lang/names
!endif
!set wims_backslash_insmath=yes
!set proc=oef
!if robot isin $session
 !changeto content.html
!endif

!set test_data=!replace internal data/ by in $module_data
!set test_data=!defof title in datamodule/$test_data/INDEX
!if $test_data= and $module_data!=
   <p class="name_datamodule"><i>
   $name_datamodule
   </i></p>
!endif
<hr style="width:35%" />

!if robot isin $session
 !changeto content.html
!endif

!if $special_parm3!=$empty
 !let tmp=!replace internal ; by $ $ in $special_parm3
 !for cle in exo,qnum,qcmlevel,scoredelay,intro_qcmpresent,intro_presentsol,intro_check,confparm1,confparm2,confparm3,confparm4,confparm5,confparm6,confparm7,confparm8,intro_expert
  !let t=!getopt $(cle) in $tmp
  !default $(cle)=$t
 !next cle
 !if $scoredelay=0
  !reset scoredelay
 !endif
 !if $exo=0
  !reset exo
 !endif
 !if yes isin $intro_expert
  !let special_parm2=!append word expert to $special_parm4
 !endif
!endif

!if $wims_class!=$empty and $wims_user=supervisor and $special_parm3=$empty
 !distribute line $wims_oefdefault_qnum\
$wims_oefdefault_qcmlevel\
$wims_oefdefault_scoredelay\
$wims_oefdefault_qcmpresent\
$wims_oefdefault_presentsol\
$wims_oefdefault_check into qnum,qcmlevel,scoredelay,intro_qcmpresent,intro_presentsol,intro_check
 !let special_parm2=$special_parm
!endif
!reset special_parm3

!if ////classes/ isin ////$module
 !set class_header=wimshome/log/classes/$wims_class/
!endif

!read $(class_header)Exindex
!set exocnt=!itemcnt $exolist
!set qcnt=$[min($exocnt,8)]
!if $wims_user=supervisor and #classes/ isin #$module
  !set wims_menu_items=!append line add_exo,1,module=adm/createxo\
  to $wims_menu_items
    !if $exocnt<1
     !exit
    !endif
!else
   !if $exocnt<1
    $name_noexercise
    !exit
   !endif
!endif

!if $lang notsametext $module_language and $(titlelist_$lang) notsametext 
 !set titlelist=$(titlelist_$lang)
!endif

!if $exocnt=1
 $name_oneexercise, <span class="bold">
 !item 1 of $titlelist
 </span>.
!endif

!form new
<input type="hidden" name="module" value="$module" />
<div id="oef_intro_params">
<h2 class="legend oef_name_menu">$name_menu</h2>
!if $exocnt>1
 <div class="field">
  !set special_parm=!trim $special_parm
  !if select iswordof $special_parm2 and $special_parm!=$empty
   <span>$name_chooseexo$name_exercisescontaining</span> &laquo;<em style="color:blue">$special_parm</em>&nbsp;&raquo;&nbsp;:
  !else
   <label for="exo">$name_chooseexo:</label>
  !endif
  !read oef/select.phtml
 </div>
!endif
<div class="field">
 <label for="qnum">$name_oneserie
 !default qnum=1
 !formselect qnum from 1 to 9
 $name_exercises.</label>
</div>
<div class="field">
 <label for="qcmlevel">$name_levelseverity:</label>
 !default qcmlevel=3
 !formselect qcmlevel from 1 to 9
</div>
<div class="field">
 <label for="scoredelay">$name_chrono:</label>
 <input size="7" id="scoredelay" name="scoredelay" value="$scoredelay" /> $name_secondes.
</div>
!!!FIXME should test if there is an introhook
<table class="field introhook">
!read introhook.phtml
</table>
!if expert iswordof $special_parm2
   <fieldset class="oef_expert">
    <legend>$name_expertmenu</legend>
    <div class="field">
     <label for="intro_qcmpresent">$name_qcmanswer: </label>
     !default intro_qcmpresent=4
     !formselect intro_qcmpresent list 2,3,4,5,6,7,8 
    </div>
    <div class="field">
     <label for="intro_presentsol">$name_solutiongiven? </label>
     !default intro_presentsol=1
     !formselect intro_presentsol list 0,1,2 prompt $name_choice
    </div>

    !default intro_check=1,2,3,4
    <div class="field">
     <label>
      !formcheckbox intro_check list 1 prompt $(name_choice_check[1])
     </label>
    </div>
    <div class="field">
     <label>
      !formcheckbox intro_check list 2 prompt $(name_choice_check[2])
     </label>
    </div>
    <div class="field">
     <label>
      !formcheckbox  intro_check list 3 prompt $(name_choice_check[3])
     </label>
    </div>
    <div class="field">
     <label>
      !formcheckbox intro_check list 4 prompt $(name_choice_check[4])
     </label>
    </div>
    !let tmp=!replace word expert by $ in $special_parm2
    !set tmp3=!replace internal intro_expert=yes by in $special_parm3
    <input type="hidden" name="intro_expert" value="yes" />

   !set wims_ref_class=wims_button
   <p class="oef_intro_buttons">
    !href module=$module&special_parm=$special_parm&special_parm2=$tmp&special_parm3=$tmp3&special_parm4=$special_parm4 $name_simplifiedmenu
   </p>
   </fieldset>
!else
   !let tmp=$special_parm2 expert
   !set wims_ref_class=wims_button
   <p class="oef_intro_buttons">
    !href module=$module&special_parm=$special_parm&special_parm2=$tmp&special_parm3=$special_parm3&special_parm4=$special_parm4 $name_expertmenu
   </p>
!endif
</div>

<div class="oef_intro_buttons">
	<input type="submit" value="$wims_name_work" />&nbsp;
	<input type="submit" name="print" value="$name_print" />
	<input type="hidden" name="special_parm2" value="$special_parm2" />
	<input type="hidden" name="special_parm4" value="$special_parm4" />
</div>

!formend

!if $exocnt>10
 !form intro
 <input type="hidden" name="special_parm2" value="select $special_parm2" />
 <input type="hidden" name="special_parm3" value="$special_parm3" />
 <input type="hidden" name="special_parm4" value="$special_parm4" />
 <input type="hidden" name="module" value="$module" />
 $name_select
 <input size="12" maxlength="12" name="special_parm" value="$special_parm" />
 <input type="submit" value="$wims_name_send" />

 !formend
!endif

$name_remarkintro1

!if * isin $titlelist
 <p>
  $name_remarkintro2
 </p>
!endif

<div class="wims_smallremark">
$name_remarkintro3
</div>


!! very tricky ...
!reset authorlist
!set dictionary=$module_dir/$(class_header)Exauthors
!set translator_unknown=unknown
!set exolist_cnt=!itemcnt $exolist
!for ex = 1 to  $exolist_cnt
  !set tmp=!exec translator $(exolist[$ex])
  !set tmp=!replace internal , by $ $ in $tmp
  !set tmp=!singlespace $tmp
  !set authorlist=!append item $tmp to $authorlist
!next
!set authorlist=!replace internal $translator_unknown by in $authorlist
!set authorlist=!replace internal ; by , in $authorlist
!if $authorlist!= and $authorlist notsametext $exolist
  !set authorlist=!listuniq $authorlist
  !set tmp=!items2words $module_author
  !set tmp=!replace internal ; by ,$  in $tmp
  !set tmp=!singlespace $tmp
  !set authorlist=!singlespace $authorlist
  !!set authorlist1=!listcomplement $tmp in $authorlist
  !set authorlist1=$authorlist
  !set authorlist_cnt=!itemcnt $authorlist1
  !set authorlist=!replace internal , by ,$ $ in $authorlist
  !if $authorlist_cnt > 0
   <div class="wims_contribute">$name_Contribute $authorlist.</div>
  !endif
!endif

