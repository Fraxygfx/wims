!nocache
!if $oef_test=yes
 !set renewlink=cmd=next&retest=again
 !!$header_js
 !goto tester
!else
 !set init_parm=!translate + to $ $ in $module_init_parm
 !set init_parm=!replace internal $\
$ by ; in $init_parm
 !set renewlink=cmd=renew&worksheet=$worksheet&$init_parm
!endif

!if $printing=yes
 !changeto oef/print.phtml
!endif

!if $title!=$empty
 !set module_title=$title
 !set module_author=$author
 !set module_address=$email
!endif

!if 0$delay2>0 and $status!=$empty
 !set now_=$wims_nowseconds
 !read js/chrono.phtml $[max(0,$delay1-($now_-$starttime))]
!endif
!set wims_html_header=!append line $header_js to $wims_html_header
!set wims_html_header=$oefcss\
$wims_html_header

!header1
<h1 class="oeftitle">$title</h1>

!if $module_checkdata!=yes
 !read oef/$module_language/nodata.phtml
 !goto end
!endif

!if $title=$empty
 This module is a groupment of exercises. You have apparently requested it
 without precising which valid exercise you want to do. It is either because
 you clicked on a wrong link, or because of a software bug.
 <p><center>
 !href module=$module Try to restart your manipulation
 , or
 !href $ abandon
 . </center>
 !goto end
!endif

!if 0$delay1>0 and $status!=$empty
 <p><form target="#" name=chrono><center>
 <table border=2 bgcolor="$wims_ref_bgcolor"><tr><td>
 &nbsp;<b><font color=red>Attention!</font></b>
 !if $qnum>1
  This series of exercises is limited in time.
 !else
  This exercise is limited in time.
 !endif
 <input size=4 name=clock readonly onFocus="this.blur();">
 </table>
 </center></form> <p>
!endif

:tester
!read oef/present.phtml
<center><hr width="50%"></center>

!if $cmd=hint
 !read oef/hint.phtml
 !goto end
!endif

!if $status=waiting
 !if $error!=$empty
  !read oef/msg.phtml
  <center><hr width="50%"></center>
 !endif
 !read oef/form.phtml
 <p>
 !if $hint!=$empty and $givehint>0
  !href cmd=hint Hint
.
 !endif
 !if $help!=$empty
  !href cmd=help Help
.
 !endif
 !if $qnum<=1
  !href $renewlink Renew the exercise
.
 !else
  This is exercise number $[$reply_cnt+1] in a series of $qnum exercises.
  !href $renewlink Abandon the series
.
 !endif
!else
 !if $error!=$empty
  !read oef/msg.phtml
  <center><hr width="50%"></center>
 !endif
 !read oef/answer.phtml
 <p>
 !if $status!=$empty
  This is exercise number $reply_cnt in a series of $qnum exercises. <p>
  !href cmd=next Continue the series with the next exercise
, or
  !href $renewlink abandon the series and ask for a new one
. (You must go through to the end of a series if you want a score.)
 !else
  !if $qnum>1
   You have finished a series of $qnum exercises, with a score of
   $module_score over 10.
   !ifval $module_score>9.8
    Congratulations!
   !endif
   !if $toolate!=$empty and $score>$module_score
    (You have been punished for exceeding the time limit.)
   !endif
   <p>
   !href $renewlink Once again
.
   !if $wims_sheet=$empty
    !href exo_Intro
.
   !endif
  !else
   You have obtained a score of $module_score over 10.
   !ifval $module_score>9.8
    Congratulations!
   !endif
   !if $toolate!=$empty and $score>$module_score
    (You have been punished for exceeding the time limit.)
   !endif
   <p>
   !href $renewlink Once again
.
   !if $wims_sheet=$empty and $oef_test!=yes
    !href exo_Intro
.
   !endif
  !endif
 !endif
!endif

!if $replydefaulted=yes
 <p> <small>Development mode:
 !if $oef_default=yes
  good replies inserted into reply fields as default.
 !else
  !href cmd=config&oef_default=yes insert good replies
  into reply fields.
 !endif
 </small>
!endif

!read ./endhook.phtml
!if $wims_user=supervisor and exam notin $session and \
	..classes/ notin ..$module and ..adm/ notin ..$module and \
	..devel/ notin ..$module and \
	GNU isin $module_copyright and \
	_check notin $session and \
	$class_importation!=no
 <p>
 !href module=adm/createxo&existing=$thisexo&catmodule=$module Import
 this exercise into your class (where you can modify it).
!endif
:end

!tail

