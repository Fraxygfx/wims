!set proc=oef
<p><center><hr width="35%"></center> <p>

!if robot isin $session
 !changeto content.html
!endif

!if ////classes/ isin ////$module
 !set class_header=wimshome/log/classes/$wims_class/
!endif

!read $(class_header)Exindex
!set exocnt=!itemcnt $exolist
!set qcnt=$[min($exocnt,8)]
!if $exocnt<1
 !if $wims_user=supervisor and #classes/ isin #$module
  !href module=adm/createxo Clica aquí
  per crear els teus exercicis.
 !else
  Ho sento, en aquest moment aquest mòdul no disposa de cap exercici. Torna més tard!
 !endif
 !exit
!endif
!if $exocnt=1
 Aquest mòdul conté actualment un sol exercici, <b>
 !item 1 of $titlelist
 </b>.
 !set exoo=!item 1 of $exolist
 !href cmd=new&module=$module&exo=$exoo Tria'l.
 !exit
!endif

!form new
<input type=hidden name=module value="$module">
<center><table border=0><tr>
!set special_parm=!trim $special_parm
!if select iswordof $special_parm2 and $special_parm!=$empty
 <td align=center>Exercicis continguts<br>
 <em><font color=blue>$special_parm</font></em>&nbsp;:<td>
!else
 <td align=center>Tria els exercicis:<td>
!endif
!read oef/select.phtml
<tr><td align=center>Una sèrie tindrà<td>
!set qnum=1
!formselect qnum from 1 to 9
exercicis.
<tr><td align=center>Nivell de dificultat <td>
!set qcmlevel=3
!formselect qcmlevel from 1 to 9
<tr><td align=center>Temps límit <td>
<input size=7 name=scoredelay value="$scoredelay"> segons.
!read introhook.phtml
!if expert iswordof $special_parm2
   <tr><td align=center>
   Número d'elements mostrats en respostes múltiples: <td>
   !default intro_qcmpresent=4
   !formselect intro_qcmpresent list 2,3,4,5,6,7,8 
   <tr><td align=center>Dóna la solució (si és posible)? <td>
   !default intro_presentsol=1
   !formselect intro_presentsol list 0,1,2 prompt mai,si la nota no és màxima,sempre
   <tr><td align=right>
   !default intro_check=1,2,3,4
   Resposta múltiple</td><td>
   !formcheckbox intro_check list 1 prompt Sempre conté
   la resposta correcta.
   <tr><td rowspan=3>&nbsp;</td><td align=left>
   !formcheckbox intro_check list 2 prompt Penalitzar per respostes incorrectes en respostes múltiples.
   <tr><td align=left>
   !formcheckbox  intro_check list 3 prompt Mostrar resposta correcta.
   <tr><td align=left>
   !formcheckbox intro_check list 4 prompt Permetre consell (quan n'hi hagi).
   !let tmp=!replace word expert by $ in $special_parm2
   <tr><td colspan=2 align=center>
   !href module=$module&special_parm=$special_parm&special_parm2=$tmp Menú simplificat
   <input type=hidden name=intro_expert value=yes>
!else
   <tr><td colspan=2 align=center>
   !let tmp=$special_parm2 expert
   !href module=$module&special_parm=$special_parm&special_parm2=$tmp Menú expert
!endif
</table></center> <p><center>
<input type=submit value="Anar a treballar">&nbsp;
<input type=submit name=print value="Versió imprimible">
</center>
</form>

!if $exocnt>10
 !form intro
 <input type=hidden name=special_parm2 value=select>
 <input type=hidden name=module value="$module">
 Selecciona títols que continguin la paraula
 <input size=12 maxlength=12 name=special_parm value="$oldparm">
 <input type=submit value=OK>
 </form>
!endif

<b>Nota</b>. Els exercicis s'agafaran aleatòriament entre els que triïs
(o entre tots els exercicis disponibles si no en tries cap).

!if * isin $titlelist
 <p>Exercicis marcats amb un * són en general més difícils que els altres.
!endif

<p><small>Pots posar dos números al temps límit, un límit petit seguit 
per un altre de gran. En aquest cas, al primer límit comença a diminuir-se la nota, que
serà 0 si el segon límit se supera.
</small>
