
!if $wims_read_parm=nocompare
 !exit
!endif

rr=$(reply$i)
gg=!rows2lines $(replygood$i)
!distribute lines $gg into left,right
left=!nonempty items $left
leftcnt=!itemcnt $left
bgbad=#f04040
bggood=#40c040
badcnt=0
tt=<table border=0 cellspacing=2 cellpadding=2>
!for j=1 to $leftcnt
 g_=!item $j of $right
 r_=!item $j of $rr
 l_=!item $j of $left
 tt=$tt\
<tr><td bgcolor="#c0c0c0" align=center>$l_\
<td><img src="gifs/arrows/corrlr.gif" alt=arrow>
 !if $g_ notsametext $r_
  !advance badcnt
  bg=$bgbad
 !else
  bg=$bggood
 !endif
 tt=$tt\
<td bgcolor="$bg" align=center>$r_
!next j
tt=$tt\
</table>

!if (split iswordof $(replyoption$i) or partialscore iswordof $(replyoption$i)) and $leftcnt>2
 !if $badcnt<1
  diareply$i=good
  !advance freegot
 !else
  !if $badcnt<$leftcnt
   diareply$i=good
   partialgood$i=yes
   freegot=$[$freegot+($leftcnt-$badcnt)/$leftcnt]
  !else
   diareply$i=bad
  !endif
 !endif
!else
 !if $badcnt<1
  diareply$i=good
  !advance freegot
 !else
  diareply$i=bad
  tt=!replace $bggood by #c0b040 in $tt
 !endif
!endif
reply_$i=$tt
reply__$i=&nbsp;

