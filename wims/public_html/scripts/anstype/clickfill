option:align=" " noorder transparent

good=!singlespace $(replygood$i)
good=!trim $good
good=!rows2lines $good
good=!line 1 of $good
good=!translate | to $\
$ in $good
good=!nonempty lines $good
replyGood$i=!line 1 of $good
replyGood$i=!items2words $(replyGood$i)
dd=$(reply$i)

!if r$i notitemof $thisstep
 !goto nocheck
!endif

!if $i notitemof $fill_checked
 fill_checked=!append item $i to $fill_checked
 dd2=
 !for d_ in $dd
  !if $d_ isitemof $fill_check
   dd2=!append item $d_ to $dd2
   !if $(replytype$i)=dragfill
    pos=!positionof item $d_ in $fill_check
    pos=!item 1 of $pos
    !if $pos!=$empty
     fill_check=!replace item number $pos by $ in $fill_check
    !endif
   !endif
  !else
   !set default_$i= !replace internal &nbsp by &nbsp; in $(default_$i)
   dd2=!append item $(default_$i) to $dd2
  !endif
 !next d_
 dd=$dd2
 !if $(replytype$i)=dragfill
  fill_check=!nonempty items $fill_check
 !endif
!endif

:nocheck
reply$i=$dd
m_reply$i=$dd
!distribute items $(oef_answer_option$i) into sizeh,sizev,sizei,sizej
!set rightcolor=#87ceeb
!set align_option=!getopt align in $(replyoption$i)
!set align_option=!tolower $align_option
!bound align_option within left,right,center default center

!!reply_$i=!items2words $dd
!set dd_cnt=!itemcnt $dd

!if $fill_css_defined!=yes
  !set reply_$i=<style type="text/css">\
    .drag_label{\
      display:table-cell;\
      vertical-align:middle;\
    }\
  </style>
  !set fill_css_defined=yes
!else
  !set reply_$i=
!endif

!for a = 1 to $dd_cnt
  reply_$i=$(reply_$i)\
  <div class="inline drag_label" style="text-align:$align_option;background-color:$rightcolor;width:$(sizeh)px;height:$(sizev)px;">\
  $(dd[$a])\
  </div>
  !if $a=$[(floor($a/$sizej))*$sizej]
     reply_$i=$(reply_$i)<br/>
  !endif
!next
!if $wims_read_parm=nocompare
 !exit
!endif

!if $good=$empty
 Test=bad $i
 !exit
!endif

diag=
diaratio=-1
t_=!linecnt $good
good=!replace internal , $ by , in $good
dd=!replace internal , $ by , in $dd
ddo=!sort items $dd
!for n=1 to $t_
 g=!line $n of $good
 g=!trim $g
 !if $dd issametext $g
  diag=yes
 !endif
 !if noorder iswordof $(replyoption$i)
   go=!sort items $g
   !if $ddo issametext $go
     diag=yes
   !endif
   !if $diag notwordof yes and (split iswordof $(replyoption$i) or partialscore iswordof $(replyoption$i))
!! nombre de reponses
       poscnt1=!itemcnt $ddo
       lint=!listintersect $ddo and $go
!! nombre de bonnes reponses
       poscnt2=!itemcnt $lint
!! nombre demandé de bonnes réponses
       poscnt3=!itemcnt $go
       !if eqweight iswordof $(replyoption$i)
         diaratio_=$[2*$poscnt2 - $poscnt1]
       !else
         diaratio_=$[3*$poscnt2 - 2*$poscnt1]
       !endif
       !if $diaratio_ > 0
        diaratio=$[max($diaratio,$[$diaratio_/max($poscnt3,1)])]
       !endif
   !endif split
 !endif noorder
!next n

!if $diag=yes
 diareply$i=good
 !advance freegot
!else
  !if $diaratio > 0 and (split iswordof $(replyoption$i) or partialscore iswordof $(replyoption$i))
    diareply$i=good
    partialgood$i=yes
    freegot=$[$freegot+ max(0,$diaratio)]
  !else
    diareply$i=bad
  !endif
!endif
