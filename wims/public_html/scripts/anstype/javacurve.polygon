!!same as segment
!set precision=!getopt precision in $(replyoption$i)
!default precision=6

!if $n<2
 :badrep
 reply$i=
 test=NaN
 !exit
!endif
!if $m<2
 Test=bad $i
 !exit
!endif

!ifval $n!=$m
 mean=1000
 !goto diag
!endif

curvecomp_xrange=0,1000
curvecomp_yrange=0,1000
curvecomp_option=discrete1 discrete2
curvecomp_1=$glist
curvecomp_2=$rlist
curvecomp_tolerance=$[2*$precision+3]

tt=!exec curvecomp
!distribute words $tt into mean1, mean2
mean=$[max($mean1,$mean2)/2]

!if NaN isin $mean or Inf isin $mean or $mean1=$empty
 !goto badrep
!endif

:diag
test=$mean
!if $mean<$precision
  diareply$i=good
  !advance freegot
!else
  !if $mean<$[2*$precision]
   precreply$i=yes
   !advance precgood
  !endif
  diareply$i=bad
!endif

:drawing
!if $(replytype$i)=javacurve
 primitive1=polygon $color1,$rlist
 primitive2=polygon $color2,$glist
 !exit
!endif

!! $(replytype$i)=jsxgraphcurve
script=!append line p[0] = brd.create('point',[$(glist[1]),$(glist[2])],{face:'+', size:'0',fixed:true,name:'',showInfobox:false,strokeColor:'$color2'}); to $script
!for j=1 to $m/2
script=!append line p[$j] = brd.create('point',[$(glist[2*$j-1]),$(glist[2*$j])],{face:'+', size:'0',fixed:true,name:'',showInfobox:false,strokeColor:'$color2'}); to $script
script=!append line brd.create('line',[p[$j-1],p[$j]],{strokeColor:'$color2', straightFirst:false, straightLast:false}); to $script
!next
script=!append line brd.create('line',[p[$m/2],p[1]],{strokeColor:'$color2', straightFirst:false, straightLast:false}); to $script

script=!append line q[0] = brd.create('point',[$(rlist[1]),$(rlist[2])],{face:'+', size:'8',fixed:true,name:'',showInfobox:false,strokeColor:'$color1'}); to $script
!for j=1 to $n/2
script=!append line q[$j] = brd.create('point',[$(rlist[2*$j-1]),$(rlist[2*$j])],{face:'+', size:'8',fixed:true,name:'',showInfobox:false,strokeColor:'$color1'}); to $script
script=!append line brd.create('line',[q[$j-1],q[$j]],{strokeColor:'$color1',straightFirst:false, straightLast:false}); to $script
!next
script=!append line brd.create('line',[q[$n/2],q[1]],{strokeColor:'$color1',straightFirst:false, straightLast:false}); to $script
