!! this process allow to generate a list (echoose) of series of exercices taken in active sheet
!!
!! to work this script need
!! variables defined :
!! asheets : list of active sheets
!! echoose : list of actual series selected
!! language variable to be defined
!! name_All, name_selectsheet
!! add to main.phtml before header
!! !let js=!record 0 of js/chooselist.js
!! !let wims_html_header=!append line $js to $wims_html_header
!! in var.def echoose and ltsheets need to be define to any
!!
!! output
!! variable echoose contain list of series selected format $sheet.$exo
!!

  $name_selectsheet
  <select name="ltsheets" onchange="submit()" value="$ltsheets">
  !let ltsheets=!listintersect $asheets and $ltsheets
  !if $ltsheets=$empty
   !let ltsheets=$asheets
  !endif
  !if $ltsheets=$asheets
   <option value="$asheets" selected>$name_All</option>
  !else
   <option value="$asheets">$name_All</option>  
  !endif
  !for k in $asheets
   !if $ltsheets=$k
    <option value="$k" selected>
   !else
    <option value="$k">   
   !endif
    !let t=!record $k of wimshome/log/classes/$wims_class/sheets/.sheets
    $k :
    !line 3 of $t
   </option>
  !next k
 </select>
  !reset wims_chooselist_checked
  !reset wims_chooselist_select

  !for i in $asheets
   !set t=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$i
   !for j=1 to $t
    !if $i.$j isitemof $echoose
     !set r=!record $j of wimshome/log/classes/$wims_class/sheets/.sheet$i
     !distribute line $r into r1,r2,r3,r4,r5,r6
     !let wims_chooselist_checked=!append line <option value="$i.$j">$i.$j : $r5</option> to $wims_chooselist_checked
    !else
     !if $i isitemof $ltsheets
      !set r=!record $j of wimshome/log/classes/$wims_class/sheets/.sheet$i
      !distribute line $r into r1,r2,r3,r4,r5,r6
      !let wims_chooselist_select=!append line <option value="$i.$j">$i.$j : $r5</option> to $wims_chooselist_select
     !endif
    !endif
   !next j
  !next i
 !set cnt_chooselist=!linecnt $wims_chooselist_checked\
 $wims_chooselist_select
 !set wims_chooselist_option=style="width:200px;" multiple="multiple" size="$[max(min($cnt_chooselist,15),2)]" 
 <center>
!read js/chooselist.phtml select_sheet1,select_sheet2,echoose
</center>


