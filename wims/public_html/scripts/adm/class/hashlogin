## INPUT; identifier, class
## OUTPUT : wims compatible unique identifier, compatible with class
## FIXME : if some ldap_field has been defined as 
!distribute items $wims_read_parm into login,cl
!set limit=2
!set limit_while=$[10^$limit]
!read adm/class/authchars
!set login=!lower $login
!set login_ext=$login
!set login=!text select $char_alnum in $login
!set login=!char 1 to $[$login_max -$limit] of $login
!set login=!char 1 to $[16-$limit] of $login
!!set cl=!replace internal / by , in $cl
!set cl=$(cl[1])
!if $cl=$empty
  !reset login
  !exit
!endif
  cnt=0
  u_exists=yes
  login_init=$login
  !while $u_exists=yes and $cnt<$limit_while
      !if $cnt>0
        login=$login_init$cnt
      !endif
      !set tt1=!lookup $login_ext in wimshome/log/classes/$cl/.teacherlist_external
      !set tt2=!lookup $login_ext in wimshome/log/classes/$cl/.userlist_external
      !if $tt1!=$empty or $tt2!=$empty
         login=$tt1$tt2
        !exit
      !endif
      !read adm/class/userinclass $cl,$login
      !set u_exists=$script_reply
      !increase cnt
  !endwhile
  !if $cnt>=$limit_while
    !reset login
  !endif
