# make some list of varfilter in a structure (class or groupement or portal)
# variable defined are :
# tv_Sclass : dir of .indextechvar file in the structure.
# tv_listallname : list of all name of techvar in structure.
# tv_listname : list of name of techvar available at the actual level of a structure.
# tv_listtechvar : list (of line) for each techvar available (with information on each techvar in each line : name,classid,number)
# tv_listlocal : list of num of local techvar (in the list tv_listtechvar)

!if $tv_Sclass!=$empty
    !exit
!endif
!reset tv_Sclass,tv_listallname,tv_listname,tv_listtechvar,tv_listlocal
!if $wims_superclass!=$empty
    tv_Sclass=$wims_superclass
    tmp_=!replace / by , in $wims_class
    n_=!itemcnt $tmp_
    parent_=$wims_superclass
    !if $n_>1
	!for k_=2 to $n_
	    parent_=!append item $(parent_[-1])/$(tmp_[$k_]) to $parent_
	!next k_
    !endif
!else
    tv_Sclass=$wims_class
    parent_=$wims_class
!endif
nbtechvar_=!recordcnt wimshome/log/classes/$tv_Sclass/.indextechvar
tv_listtechvar=$empty
tv_listlocal=$empty
!if $nbtechvar_>0
    !for k_=1 to $nbtechvar_
	tmp_=!record $k_ of wimshome/log/classes/$tv_Sclass/.indextechvar
	tv_listallname=!append item $(tmp_[1]) to $tv_listallname
	!if $(tmp_[2]) isitemof $parent_
		tv_listtechvar=!append line $tmp_ to $tv_listtechvar
		tv_listname=!append item $(tmp_[1])$ to $tv_listname
		!if $(tmp_[2])=$wims_class
		    t_=!linecnt $tv_listtechvar
		    tv_listlocal=!append item $t_ to $tv_listlocal
		!endif
	!endif
    !next k_
!endif


