!set renewlink=cmd=renew&worksheet=$worksheet&$module_init_parm
!set module_title=$module_title: $title

!header

!if $title=$empty
 This module is a groupment of exercises. You have apparently requested it
 without precising which valid exercise you want to do. It is either because
 you clicked on a wrong link, or because of a software bug.
 <p><center>
 !href module=$module Try to restart your manipulation
 , or
 !href $ abandon
 . </center>
 !goto end
!endif

:tester
!read proc/present.phtml

<p><hr width="50%"> <p>

!if $cmd=hint
 !read proc/hint.phtml
 !goto end
!endif

!if $status=waiting
 !if $error!=$empty
  !read proc/msg.phtml
  <p><hr width="50%"> <p>
 !endif
 !read proc/form.phtml
 <p>
 !if $hint!=$empty and $givehint>0
  !href cmd=hint Hint
.
 !endif
 !if $qnum<=1
  !href $renewlink Renew the exercise
.
 !else
  This is exercise number $[$reply_cnt+1] in a session of $qnum exercises.
  !href $renewlink Abandon the session
.
 !endif
!else
 !if $error!=$empty
  !read proc/msg.phtml
  <p><hr width="50%"> <p>
 !endif
 !read proc/answer.phtml
 <p>
 !if $status!=$empty
  This is exercise number $reply_cnt in a session of $qnum exercises. <p>
  !href cmd=next Continue the session with the next exercise
, or
  !href $renewlink abandon the session and ask for a new one
. (You must go through to the end of a session if you want a score.)
 !else
  !if $qnum>1
   You have finished a session of $qnum exercises, with a score of
   $score over 10.
   !ifval $score>9.8
    Congratulations!
   !endif
   <p>
   !href $renewlink Another session
.
   !if $wims_sheet=$empty
    !href module=$module&cmd=intro&special_parm=$wims_intro_parm Change the level of difficulty
.
   !endif
  !else
   You have obtained a score of $score over 10.
   !ifval $score>9.8
    Congratulations!
   !endif
   <p>
   !href $renewlink Once again
.
   !if $wims_sheet=$empty and $oef_test!=yes
    !href exo_Intro
.
   !endif
  !endif
 !endif
!endif

!read endhook.phtml
:end

!tail

