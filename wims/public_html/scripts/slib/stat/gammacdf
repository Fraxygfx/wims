!if $wims_read_parm!=slib_header
!goto proc
!endif
slib_title=Cumulative distribution function of Gamma law
slib_parms=3\
,[data]\
,a\
,b

slib_author=Bernadette PERRIN-RIOU
slib_out=values of cumulative distribution function of Gamma law on data
slib_comment=data can be a list of reals in brackets; density is b*exp(-b*x)*(b*x)^(a-1)/gamma(a) for x>=0
slib_example=[0.2,0.4,7],4,5
!exit

:proc
slib_out=

!distribute  item $wims_read_parm into slib_data,slib_a, slib_b
slib_data=!declosing $slib_data
slib_n=!itemcnt $slib_data

slib_out=!exec pari slib_u=gamma($slib_a); V=vector($slib_n,x,1-incgam($slib_a ,$slib_b*[$slib_data][x],slib_u)/slib_u);\
for(slib_r =1, $slib_n, if ( [$slib_data][slib_r] <= 0, V[slib_r]=0)); V 

slib_out=!trim $slib_out

