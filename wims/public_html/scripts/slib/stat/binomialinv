!if $wims_read_parm!=slib_header
!goto proc
!endif
slib_title=Quantile of Binomial law
slib_parms=3\
,[data]\
5,n\
0.5,p

slib_author=Bernadette PERRIN-RIOU
slib_out=quantilei of binomial law of parameters n and p ((0&lt;p&lt;1)) at data
slib_comment=data can be a list of reals
slib_example=[0.2,0.4,0.6],5,0.5
!exit

:proc

!distribute  item $wims_read_parm into slib_data,slib_n,slib_p
!default slib_n= 5
!default slib_p=0.5
!if $slib_p>=1 or $slib_p<=0 
	slib_out=0
	!exit
!endif
slib_out=
slib_data=!declosing $slib_data
slib_p1=$[1-$slib_p]
slib_r=$[$slib_p/$slib_p1]
slib_a=$[$slib_p1^($slib_n)]
!!slib_bino=!values binomial($slib_n,x)*$slib_p^x*$slib_p1^($slib_n-x) for x =0 to $slib_n
slib_cnt= !itemcnt $slib_data
slib_pp=$[$slib_p/$slib_p1]
!for slib_i =1 to $slib_cnt
	slib_di=!item $slib_i of $slib_data
	slib_P=0
	slib_a=$[$slib_p1^($slib_n)]
	!for slib_k=0 to $slib_n
		!!slib_a=!item $[$slib_k+1] of $slib_bino
		slib_P=$[$slib_P+$slib_a]
		!if $slib_P>= $slib_di
			slib_out=!append item  $slib_k to $slib_out
			!break
		!endif
		slib_a=$[$slib_a*$slib_pp*($slib_n-$slib_k)/($slib_k+1)]
	!next slib_k
!next slib_i
slib_out=!trim $slib_out
