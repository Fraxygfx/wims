!! On pose les questions dans ce fichier. D'habitude, c'est par
!! des forms. Voici un exemple de conception de la forme de réponse.

!if $sstyle=1
 !if $substep=0
  !if $all_steps=0
   !form reply
   Quelle est la <em>première</em> étape erronée de cette série&nbsp;?
   !formselect badstep from 0 to $stepcnt
   (0 voudra dire que tout est juste.)
   <p><center><input type=submit value="Envoyer la réponse"></center>
  !else
   Marque <em>toutes</em> les mauvaises étapes ci-dessus, puis
   <input type=submit value="clique ici">.
  !endif
  </form>
 !else
  !if $all_steps=0
   !if $badstep=0
    Effectivement, toutes les étapes sont correctes.
   !else
    En effet, la première mauvaise étape est le numéro $badstep.
   !endif
  !endif
  Ta note&nbsp;: $module_score/10.
  !if $expstep!=$empty
   <p>Quelques explications.<ul>
   !for i in $expstep
    <p><li> Etape $i
    !if $i isitemof $badstep
     est correcte.
     !line $i of $remarks
    !else
     !set j=!positionof item $i in $bad_steps
     !set e=!item $j of $bad_reasons
     n'est pas bonne.
     !read reasons/$e.det
    !endif
   !next i   
   </ul>
  !endif
 !endif
!else
 !if $substep=0
  !form reply
  Que penses-tu de la <em>dernière</em> étape&nbsp;?
  !formselect ans list --,OK,$bad_reasons prompt Je ne sais pas,\
  	Oui : cette étape est correcte,$bad_rnames
  <input type=submit value="Envoyer">
  </form>
 !endif
 
 !if $substep=2
  Tu as raison&nbsp;:
  !if $step_reason=OK
   la dernière étape est correcte.
  !else
   la dernière étape est erronée. 
   La bonne étape sera<p><center>
   <table border=0 bgcolor=#d0c0d0 width=80%><tr><td align=center>
   !line $step of $good
   </table> </center>
  !endif
  <p>
  !if $step<$stepcnt
   !href cmd=reply&ans=next Passer à l'étape suivante.
  !else
   Cet exercice est terminé. Ta note&nbsp;: $module_score/10.
  !endif
 !endif
!endif

