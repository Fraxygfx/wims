<!-- begin of $wims_theme/user.phtml -->

!! Class participant page theme.
!nocache
!if $module!=home
 !exit
!endif
!!initialisation
!set home_theme=yes

!set class_authidp=!defof class_authidp in wimshome/log/classes/$wims_superclass/.def
!set auth_method_list=cas,ldap,php
!set auth_method=$(class_authidp[1;])
!if $(class_authidp[1;]) isitemof $auth_method_list
  !set ext_login=$wims_external_auth
!endif
!default ext_login=$wims_user

!set percents=!getscorepercent
!set class_homepage=!getdef class_homepage in wimshome/log/classes/$wims_class/.def
!set etest=!record 0 of wimshome/log/classes/$wims_class/.parmreg/$wims_user.exam
!set n_=!wordcnt $etest

!if / isin $class_homepage and _tool notin $wims_session
 !set deffile=wimshome/log/classes/$wims_class/doc/$class_homepage.def
 !set t_=!getdef titb in $deffile
 !if $t_=$empty or $n_>=4
  !goto normal
 !endif
 !read ./userhome.phtml
 !goto end
!endif

:normal
!read classlogo.phtml
!if $class_logo!=
 <div id="wimslogo$class_logoside">
  <img src="$class_logo" alt="class logo" />
 </div>
!endif

!read ./adm/title.phtml 2\
 <span class="wims_classname">$wims_classname</span> <span class="wims_class_typename">($(wims_name_$class_typename))</span>

!read adm/class/initclass $wims_superclass, auth

!!FIXME
!if $auth_method isitemof $auth_method_list
   !set wims_user_ext=!defof user_external_auth in wimshome/log/classes/$wims_superclass/.users/$wims_user
!endif
!default wims_user_ext=$wims_user

<div class="wims_classes_direct">
!if $wims_superclass=$empty or $wims_superclass=$wims_class
  $wims_institutionname
!else
  <span class="wims_classes_direct_portal">
   !href module=adm/class/classes&type=authparticipant&class=$wims_superclass&auth_user=$wims_user_ext $wims_institutionname
  </span>
  !if / isin $class_parent and $class_type=1
    !set pname=!defof class_description in wimshome/log/classes/$class_parent/.def
  / $
 <span class="wims_classes_direct_class">
  !href module=adm/class/classes&type=authparticipant&class=$class_parent&auth_user=$wims_user_ext $pname
 </span>
 !endif
!endif
</div> <!--wims_classes_direct-->

!if _tool isin $wims_session
 !read ./tools.phtml
 !goto end
!endif
!set supmail=!mailurl $wims_supervisormail $wims_name_mail
!if $n_>=4
 !distribute words $etest into h_,s_,t_,n_
 !set r_=!record 0 of wimshome/sessions/$s_/examreg.$n_
 !set now=$wims_nowseconds
 !set ws=!translate _ to $ $ in $wims_session
 !set ws=!word 1 of $ws
 !set sm=!record 0 of wimshome/log/classes/$wims_class/.E$n_
 !if $r_!=$empty and $s_!=$ws and $t_>$now and $sm!=#
  <span class="wims_warning">$wims_name_warning</span>.
  $U_examworking
  !if $h_=$httpd_REMOTE_ADDR
   !href session=$s_&module=adm/class/exam&job=student&exam=$n_ $U_examcont
  !else
   $U_examdouble
   !sh rm -f ../sessions/$ws/var.stat >/dev/null
  !endif
  !goto end
 !endif
 !if $t_<$now or $r_=$empty
  !writefile wimshome/log/classes/$wims_class/.parmreg/$wims_user.exam
 !endif
!endif
<p class="wims_greetings">$U_greet</p>

!if & isin $newmsgcnt or $newmsgcnt>0
 <p class="wims_newmsg">
 !href module=adm/forum/mboard $U_newmsg
 </p>
!endif

!if $wims_supconnectshowed=yes and $wims_supervisorconnected!=$empty
 <p class="wims_connected">$U_supconnected</p>
!endif

!read themes/_widgets/motd.phtml

!if $class_type>=2
 !read ./subclasses.phtml
 !set is_interclass=yes
!endif

!set seq_cnt=!record 0 of wimshome/log/classes/$wims_class/seq/.sequences
!set seq_cnt=!line 1 of $seq_cnt
!set seq_open=!defof sequence_open in wimshome/log/classes/$wims_class/seq/.def

!if $seq_open=yes
  !if $seq_cnt>0
    !read themes/_widgets/sequence.phtml
  !endif
  !goto endbar
!endif

!read themes/_widgets/user_vardoc.phtml
!read themes/_widgets/user_vardocp.phtml
!read themes/_widgets/user_varsheet.phtml
!read themes/_widgets/user_varexam.phtml
!read themes/_widgets/user_varvote.phtml

!ifval $dcnt+$dpcnt+$n+$m+$v>1
 <div id="widget_user_list">
 !if $jquery_defined=yes
    <script type="text/javascript">
      /*<![CDATA[*/
      jQuery(function($$) {
        // Get in sessionStorage if a tab was previously selected, and activate it.
        var selectedTabId = sessionStorage.getItem("user_list_selectedTab");
        selectedTabId = selectedTabId === null ? 0 : selectedTabId; //default tab = 0
        $$("#widget_user_list").tabs({
          active: selectedTabId,
          activate : function( event, ui ) {
              selectedTabId = $$("#widget_user_list").tabs("option", "active");
              sessionStorage.setItem("user_list_selectedTab", selectedTabId);
          }
        });
      });
      /*]]>*/
    </script>
 !endif
<ul id="wims_user_summary">
 !if $[$dcnt+$dpcnt]>0
  <li><a href="#wims_user_doc">$wims_name_Docs</a></li>
 !endif
 !if $n>0
  <li><a href="#wims_user_sheet">$wims_name_Sheets</a></li>
 !endif
 !if $m>0
  <li><a href="#wims_user_exam">$wims_name_Examens</a></li>
 !endif
 !if $v>0
  <li><a href="#wims_user_vote">$wims_name_Votes</a></li>
 !endif
</ul>
!endif
!!define variable $dcnt and $ dpcnt
!read themes/_widgets/userdoc.phtml
!!define variable $n
!read themes/_widgets/usersheet.phtml
!!define variable $m
!read themes/_widgets/userexam.phtml
!!define variable $v
!read themes/_widgets/uservote.phtml
</div>
!ifval $dcnt+$dpcnt+$n+$m+$v=0
 !if $is_interclass!=yes
   $U_nosheet
 !endif
!endif

:endbar
</div><!--wimsbody-->
</div><!--wimspagebox-->

!!must have read user*.phtml,  with *=sheet, exam must be read before
!distribute items div,div into div0,div1

!read themes/_procs/usermenumodubox.proc
!set wims_homeref_n0=!linecnt $wims_rm_0
!!read themes/_widgets/usermenumodubox_accordion.phtml
!read themes/_widgets/menumodubox.phtml

!read themes/_widgets/topback.phtml

!distribute items div,span into div0,div1
!!read themes/_widgets/usermenubox.phtml
!reset div0 div1
 <br class="clearall" />
</div><!--end of wimsbodybox-->
:end
</body>
</html>
<!-- end of $wims_theme/user.phtml -->

