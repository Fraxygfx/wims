<!-- begin of $wims_theme/user.phtml -->

!! Class participant page theme.
!nocache
!if $module!=home
 !exit
!endif
!!initialisation
!set home_theme=yes

!if $(class_actcolor) != $empty
<style type="text/css">
  body.main_body{background-color:red;}

  .wims_sheet_item, .wims_exam_item, .wims_doc_item, .wims_vote_item, .wims_exo_item{
   color:$(class_actcolor[1]);
  }

  .wims_sheet_item {background-color:$(class_actcolor[2]);}
  .wims_exam_item  {background-color:$(class_actcolor[3]);}
  .wims_doc_item   {background-color:$(class_actcolor[4]);}
  .wims_vote_item  {background-color:$(class_actcolor[5]);}
  .wims_exo_item   {background-color:$(class_actcolor[6]);}
</style>
!endif

!set percents=!getscorepercent
!set class_homepage=!getdef class_homepage in wimshome/log/classes/$wims_class/.def
!set etest=!record 0 of wimshome/log/classes/$wims_class/.parmreg/$wims_user.exam
!set n_=!wordcnt $etest

!if / isin $class_homepage and _tool notin $wims_session
 !set deffile=wimshome/log/classes/$wims_class/doc/$class_homepage.def
 !set t_=!getdef titb in $deffile
 !if $t_=$empty or $n_>=4
  !goto normal
 !endif
 !read ./userhome.phtml
 !goto end
!endif

:normal
!read classlogo.phtml
!if $class_logo!=$empty
 <div id="classlogo" class="float_$class_logoside">
  <img src="$class_logo" alt="class logo" />
 </div>
!endif

!read ./adm/title.phtml 2\
 <span class="wims_classname">$wims_classname</span> <span class="wims_class_typename">($(wims_name_$class_typename))</span>

!read adm/class/initclass $wims_superclass, auth

!!FIXME
!if $auth_method isitemof $auth_method_list
   !set wims_user_ext=!defof user_external_auth in wimshome/log/classes/$wims_superclass/.users/$wims_user
!endif
!default wims_user_ext=$wims_user

<div class="wims_classes_direct">
!if $wims_superclass=$empty or $wims_superclass=$wims_class
  $wims_institutionname
!else
  <span class="wims_classes_direct_portal">
   !href module=adm/class/classes&type=authparticipant&class=$wims_superclass&auth_user=$wims_user_ext $wims_institutionname
  </span>
  !if / isin $class_parent and $class_type=1
    !set pname=!defof class_description in wimshome/log/classes/$class_parent/.def
  / $
   <span class="wims_classes_direct_class">
  !href module=adm/class/classes&type=authparticipant&class=$class_parent&auth_user=$wims_user_ext $pname
 </span>
 !endif
!endif
</div> <!--wims_classes_direct-->

<div class="wims_content">

!if _tool isin $wims_session
 !read ./tools.phtml
 !goto end
!endif

!if $n_>=4
 !distribute words $etest into h_,s_,t_,n_
 !set r_=!record 0 of wimshome/sessions/$s_/examreg.$n_
 !set now=$wims_nowseconds
 !set ws=!translate _ to $ $ in $wims_session
 !set ws=!word 1 of $ws
 !set sm=!record 0 of wimshome/log/classes/$wims_class/.E$n_
 !if $r_!=$empty and $s_!=$ws and $t_>$now and $sm!=#
  <span class="wims_warning">$wims_name_warning</span>.
  $U_examworking
  !if $h_=$httpd_REMOTE_ADDR
   !href session=$s_&module=adm/class/exam&job=student&exam=$n_ $U_examcont
  !else
   $U_examdouble
   !sh rm -f ../sessions/$ws/var.stat >/dev/null
  !endif
  !goto end
 !endif
 !if $t_<$now or $r_=$empty
  !writefile wimshome/log/classes/$wims_class/.parmreg/$wims_user.exam
 !endif
!endif
<p class="wims_greetings">$U_greet</p>

!if & isin $newmsgcnt or $newmsgcnt>0
 <p class="wims_newmsg">
 !href module=adm/forum/mboard $U_newmsg
 </p>
!endif

!if $wims_supconnectshowed=yes and $wims_supervisorconnected!=$empty
 <p class="wims_connected">$U_supconnected</p>
!endif

!read themes/_widgets/motd.phtml

!if $class_type>=2
 !read ./subclasses.phtml
 !set is_interclass=yes
!endif

!set seq_cnt=!record 0 of wimshome/log/classes/$wims_class/seq/.sequences
!set seq_cnt=!line 1 of $seq_cnt
!set seq_open=!defof sequence_open in wimshome/log/classes/$wims_class/seq/.def

!if $seq_open=yes and $seq_cnt>0
  !read themes/_widgets/sequence.phtml
  !goto endbar
!endif
!!define variable $dcnt and dpcnt
!read themes/_widgets/userdoc.phtml
!!define variable $n
!read themes/_widgets/usersheet.phtml
!!define variable $m
!read themes/_widgets/userexam.phtml
!!define variable $v
!read themes/_widgets/uservote.phtml

!ifval $dcnt+$dpcnt+$n+$m+$v=0
 !if $is_interclass!=yes
  <div class="wims_msg info">$U_nosheet</div>
 !endif
!endif

:endbar
</div><!--wims_content-->
</div><!--wimsbody-->
</div><!--wimspagebox-->

 <br class="clearall" />

!read themes/_procs/usermenumodubox.proc
!set wims_homeref_n0=!linecnt $wims_rm_0

!!must have read user*.phtml,  with *=sheet, exam must be read before
!distribute items ul,li into div0,div1
!read themes/_widgets/menumodubox.phtml
!read themes/_widgets/usermenubox.phtml
!reset div0 div1

</div><!--end of wimsbodybox-->
:end
</body>
</html>
<!-- end of $wims_theme/user.phtml -->

