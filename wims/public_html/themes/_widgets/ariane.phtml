!! variable wims_ariane_home =no don't use $wims_home_items
!! wims_ariane_self don't generate automatically : home / module

!if $module = home
  !set path = ./adm/
!else
  !set path = $empty
!endif
<!-- _widgets/ariane.phtml -->
!set wims_ariane_cnt=!linecnt $wims_ariane
<nav aria-label="You are here:">
  <ul class="breadcrumbs">
!! the test is when student are doing an exercise in a sheet!
!! tricky
  !if $wims_ariane_home=$empty
   !set 1_=!line 1 of $wims_home_items
   <li class="breadcrumb_root $(1_[1])">
     !read $(path)oneitem.phtml $1_
   </li>
  !endif
!! the variable wims_ariane_self must be defined in the var.proc of
!! the module if one does not an ariane thread automatically created
!! should have more parameters, for the moment, just no.
!if $wims_ariane_self!=$empty
  !for i_=1 to $wims_ariane_cnt
   !set 1_=!line $i_ of $wims_ariane
   !set 2_=!word 1 of $1_
   <li class="menuitem $2_">
     !read $(path)oneitem.phtml $1_
   </li>
  !next
  !if $wims_ariane_cnt>1
    !set 3_=!item 2 of $1_
    !if $3_!=8
      <li>$3_*<span class="show-for-sr">(Current)</span></li>
    !endif
   !endif
  </ul>
 </nav>
 !exit
!endif

!if _ notin $session and $module!=home and adm isin $module_category
  !default ariane_title=$(module_title_$lang)
  !default ariane_title=$module_title
!! clickable or not ? it depends and the link module=$module
!! is not always pertinent and sometimes repetition with Back
!! the other problem is that some modules
!! rename module_title according to the job.

!! the test is not always OK, how to find if we are not in the homepage
!! of the adm module
  !if $cmd!=$empty and $cmd!=new and adm/light notin $module
    <li>
     !read $(path)oneitem.phtml "$ariane_title",1,module=$module
    </li>
    !if $wims_ariane_cnt>0
      !for i_=1 to $wims_ariane_cnt
        !set 1_=!line $i_ of $wims_ariane
        !set 2_=!word 1 of $1_
        !set 3_=!item 2 of $1_
        <li class="menuitem $2_">
        !if $3_=8
         <span class="show-for-sr">Current: </span>
        !endif
          !read $(path)oneitem.phtml $1_
        </li>
      !next
    !endif
    !if $3_!=8
      !if $wims_title_title!=$empty
        <li><span class="show-for-sr">Current:</span>
        $wims_title_title
        </li>
      !else
        <li>*<span class="show-for-sr">(Current)</span></li>
     !endif
    !endif
  !else
    <li><span class="show-for-sr">Current: </span>
    !read $(path)oneitem.phtml "$ariane_title",8
    </li>
  !endif
!endif
  </ul>
</nav>
<!-- _widgets/ariane.phtml -->
!!reset wims_ariane_home wims_ariane_self
