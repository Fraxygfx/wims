today=!char 1 to 8 of $wims_now
thisyear=!char 1 to 4 of $today
!if $class_expiration=$empty
 !defread wimshome/log/classes/$wims_class/.def
!endif
!if $class_expiration=$empty
 error=bad_class
 !exit
!endif

!readdef $votedir/$vote.data
!default testointro=!record 1 of $votedir/$vote.data
!default mod_numsess=$numsess
numsess=$mod_numsess
!for i=1 to $numsess
   !for var in testoextra,nstud
    !default up_$var$i=$($var$i)
    !set $var$i=$(up_$var$i)
   !next var
 !next i
!for var in testointro, namevar
    !default up_$var=$($var)
    !set $var=$(up_$var)
!next var

!if $gstep=2
!! need to check carefully namexevar vs. up_namevar (at some step we seem to
!! be loosing the correction)(?? should this go outside the if $gstep=2)
 !bound up_namevar within $listlocalname
 !if $up_namevar=$empty and $namevar=$empty
   error=bad_variable
   gstep=1
   !exit
 !endif
 !default namevar=$up_namevar
 !if $namevar=$empty
  error=no_variable
  gstep=1
  !exit
 !endif
 !for dato in namevar
  !setdef $dato=$($dato) \
in  $votedir/$vote.data
 !next dato
!endif

!if $gstep>=2
!! get list of value for the technical variable :
 tmp=!positionof item $namevar in $tv_listname
 tmp=!line $tmp of $tv_listtechvar
 tmp=!record $(tmp[3]) of wimshome/log/classes/$wims_class/.techvar
 listval=!line 2 of $tmp
 numsess=!itemcnt $listval
!endif

!if $gstep=3
!!## need to check $testointro contains no vote primitives
 !for i in uservar,menu,list,radio,checkbox,textarea
  !set testointro=!replace internal \$i by INVALID in $testointro
 !next i
 !writefile $votedir/$vote.data

!!## with writefile we are loosing these variables at gstep=3
!for dato in numsess,namevar
  !appendfile $votedir/$vote.data $dato=$($dato)
!next dato

 donfile=$empty
 !for i=1 to $numsess
!!## need to check no "," in testoextra
  !set testoextra$i=!replace internal , by . in $(testoextra$i)
  nstud$i=$[max(0,$(nstud$i))]
  !if NaN isin $(nstud$i)
    nstud$i=0
  !endif
  !for dato in nstud$i,testoextra$i
   donfile=!append line $dato=$($dato) to $donfile
  !next dato
!next i
 !appendfile $votedir/$vote.data $donfile\
:$testointro

 reg_src=<div>\
$testointro\
</div>\
\list{

 reg_src1=$reg_src
 reg_src2=\uservar{$namevar,$empty,
 !for i=1 to $numsess
  reg_src1=$reg_src1\
$(nstud$i): $(testoextra$i),
  reg_src2=$reg_src2\
$(listval[$i]),
 !next i
 reg_src=$reg_src1\
$name_cancellation\
}\
$reg_src2\
$empty\
}

!endif

