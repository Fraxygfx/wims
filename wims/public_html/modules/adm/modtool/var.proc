!read adm/language names.phtml

!readdef wimshome/log/wims.conf

publish_site=$submitpublish_site
!!default publish_site=$mu_publish_site

!! read the list of publication centers
publishsites=!sh cd $wims_home/public_html/bases/sys/publishcenter/;\
grep publishcenter=yes * | cut -d":" -f1
publishsites=!lines2items $publishsites
!bound publish_site within $publishsites default $(publishsites[1])
publishsitesname=$empty
!for name in $publishsites
 tmp=!defof publishname in wimshome/public_html/bases/sys/publishcenter/$name
 tmp=!replace internal , by &#44; in $tmp
 publishcentersname=!append item $tmp to $publishcentersname
!next name
!!read wimshome/public_html/bases/sys/publishdata

!!default publish_site=$(publishsites[1])
!default mu_publish_site=$publish_site

!! read the specification of $publish_site
!read wimshome/public_html/bases/sys/publishcenter/$publish_site

wims_nw=job mod existing serial_number auth_login auth_passwd \
	original2 originalheader empty devdir fname target
wims_nr=wims_sesrandom

#############################################################
jobs=modname create flist index edfile showfile delfile delconfirm \
	backup restore copy move del publish prop diff binfile
maxlist=100
sellist=10
alnum=0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ
searchsymbol=.*+-_~\[]|()<>
searchchars=$alnum$searchsymbol
langlist=!words2items $wims_site_languages
!read lang/langname.$modu_lang
deflist=title, description, language, category, domain, \
	level, keywords, require, scoring, copyright,\
	author, address, version, wims_version, vardef, \
	translator, translator_address, data, maintainer, maintainer_address
wims_form_method=post
!read tabletheme

!if $cmd=help or robot isin $session
 !exit
!endif

jobreq=!word 1 of $jobreq
!if $jobreq!=$empty and $jobreq iswordof $jobs
 job=$jobreq
!endif
jobreq=

!if $wims_class!=$empty
 !read adm/class/userdef classes,$wims_class,$wims_user
!endif
!if $wims_developer=$empty and $wims_class!=$empty and $wims_user!=$empty
 !if $wims_user=supervisor
  !if $wims_sup_secure>=0
   wims_developer=!getdef wims_developer in $userdef
  !endif
 !else
  wims_developer=!getdef wims_developer in $userdef
 !endif
 !distribute item $wims_developer into wims_developer,devpass
 !if $devpass=$empty
  !reset wims_developer
 !endif
!endif

!if $wims_ismanager>=2 and $change=yes
 wims_developer=
!endif

!if $wims_developer!=$empty and $auth_login=$empty
 n=!recordcnt wimshome/log/.developers
 !for i=1 to $n
  r=!record $i of wimshome/log/.developers
  !distribute lines $r into ll,pp,nn,ee
  pp=!passcrypt $pp
  !if $devpass!=$empty
   devpass=!passcrypt $devpass
  !endif
  !if $ll=$wims_developer and ($devpass=$empty or $devpass=$pp)
   auth_login=$ll
   auth_passwd=$pp
   auth_name=$nn
   auth_email=$ee
   login=$wims_developer
   sesdir=!translate internal _ to $ $ in $wims_sesdir
   sesdir=!word 1 of $sesdir
   !setdef wims_developer=$login in wimshome/$sesdir/var.stat
   !if $original!=$empty and $job=$empty
    job=copy
   !endif
   !default job=modname
   !break
  !endif
 !next i
 !if $auth_login=$empty
  wims_developer=
  job=auth
 !endif
!endif
!default job=auth

!if $auth_login=$empty and $wims_ismanager<2
 job=auth
!endif
!if $auth_login!=$empty
 devdir=modules/devel/$auth_login
!endif

!read $job.proc

!if $(name_$job)!=$empty
 module_title=$module_title - $(name_$job)
!endif

!if ~ isin $mod
 m=!translate internal ~ to , in $mod
 m1=!item 1 of $m
 !reset pubtit
 !if $m1!=test and $m1 isitemof $zonelist
  m=!translate internal ~ to / in $mod
  pubtit=!module title $m
 !endif
!endif

 

