!if $job=lightpopup
 !changeto jobs/lightpopup.proc
!endif

wims_prefix=class user tmp n sharing sharable ident
!if $job=help
 wims_module_log=help
 !exit
!endif

!read defs

job=!lower $job
!bound job within $allowed_actions_jobs,$allowed_file_jobs,$allowed_jobs_without_auth default $
option=!items2words $option
option=!words2items $option
code=!word 1 of $code


!if $job notin $allowed_jobs_without_auth or $ident != $empty
	# le script autchars permet de definir les listes de caracteres alphanumeriques et de ponctuation
	# il definit egalement la fourchette de longueur autorise des login/mdp
	!read adm/class/authchars
	# Le script var.check verifie l'authentification de l'utilisateur
	!read var.check	
!endif

!if $error=$empty
 !read jobs/$job.proc
!endif

#Lorsque l'on sort de la procedure "job", il ne doit tjs pas y avoir d'erreur.
!if $error!=$empty
 !changeto error.proc $error
!endif

!if $code=$empty
  !if $ident_type != xmlformat
	ok=OK
  !else
  	ok=<?xml version="1.0" encoding="$encoding"?>\
<!DOCTYPE methodResponse PUBLIC "XML9-RPC" "http://ticewims.unice.fr/xml-rpc/xml-rpc.dtd">\
<methodResponse xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="http://ticewims.unice.fr/xml-rpc/xml-rpc.xsd">\
<params><param><value><string>OK</string></value></param>
  !endif
!else
  !if $ident_type != xmlformat
	ok=OK $code$ident_type
  !else
  	ok=<?xml version="1.0" encoding="$encoding"?>\
<!DOCTYPE methodResponse PUBLIC "XML9-RPC" "http://ticewims.unice.fr/xml-rpc/xml-rpc.dtd">\
<methodResponse xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="http://ticewims.unice.fr/xml-rpc/xml-rpc.xsd">\
	<params><param><value><struct><member>\
		<name>code</name>\
		<value><string>$code</string></value>\
	</member>\

  !endif
!endif

!if $ident_type != xmlformat
	!shortout $header\

!else
	!shortout $header_xml\

!endif



!if $job isitemof $allowed_actions_jobs
 !shortout $ok\

 !read types/$ident_type
!endif

!if $job isitemof $allowed_file_jobs
 !sh cat $wims_home/$fname
!endif

!if $job notwordof authuser
 wims_mode=popup
!endif

wims_module_log=$job $ident -> $qclass $quser