!read scripts/check.class
!if $error!=$empty
 !exit
!endif

# dans la variable $supp, on ajoute des informations supplementaires aux infos de base presentes dans le .def de la classe
supp=connections,creation
!readdef $classdeffile
!readdef $classexofile

userlist=!sh cd $wims_home/log/classes/$qclass/.users; ls | sort
class_userlist=!singlespace $userlist
class_usercount=!wordcnt $class_userlist
class_userlist= !words2items $class_userlist

# Pour compter les exos, on utilise le fichier Extitles de la classe
class_exotitlelist=!record 0 of wimshome/log/classes/$qclass/Extitles
class_exotitlelist=!lines2items $class_exotitlelist
class_exocount=!itemcnt $class_exotitlelist

##course list,  program list
!if $class_type>=2
  super_defs=programs,classes,icourses,levels,courses
  supp=$supp,$super_defs
  
  !for typ in $super_defs
   class_$typ=!record 0 of wimshome/log/classes/$qclass/$typ
   class_$typ=!lines2rows $(class_$typ)
   class_$typ=$(class_$typ[;1])
  !next
!endif

!if $class_type=2
#Cas du groupement de classes
  class_subclasses=!record 0 of wimshome/log/classes/$qclass/.subclasses
  supp=$supp,subclasses
!endif

class_sheetcount=!recordcnt wimshome/log/classes/$qclass/sheets/.sheets
class_examcount=!recordcnt wimshome/log/classes/$qclass/exams/.exams


# homepage = parametre obsolete  ?  
supp=$supp, userlist, usercount, exotitlelist, exocount, examcount, sheetcount


#l'option "all_even_hidden" est un raccourci pour obtenir la liste complete des infos possibles
!if all_even_hidden isin $option
 option=$classdefs,$supp,hidden_sheets
!endif

option=!listintersect $option and $classdefs,$supp,hidden_sheets
!if $option=$empty
 option=$classdefs,$supp
!endif

# l'option 'hidden_sheets' permet d'obtenir, en plus des infos de la classe, des infos sur les feuilles de celle-ci.
!if hidden_sheets isin $option
 list_type=sheets
 !read scripts/listing.proc
!endif