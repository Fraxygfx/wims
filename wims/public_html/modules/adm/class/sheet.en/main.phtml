!reset confirm
!!<html><head>
!!$wims_html_header
!!</head><body $wims_htmlbody>
!!!headmenu
!read adm/css.phtml
!header1

!read names.phtml

!if $job iswordof student
 !changeto $job.phtml
!endif

!if $cmd=help
 !read help/$special_parm.phtml
 !goto end
!endif

<center>$classname
<p>
<h1>Preparation of sheet $sheet
</h1></center> <p><hr width="40%"> <p>

!if $error!=$empty
 <p>
 !read msg.phtml
 <p>
 !href module=home Back to home page
. <p>
 !goto end
!endif

!if $job=getsource
 !read getsource.phtml
 !goto end
!endif

!if $activetest>0
 !form reply
 <input type=hidden name=job value=text>
 This sheet is
 !if $activetest=1
  active.
  !href cmd=reply&job=expire Expire it NOW
  or
  !href cmd=reply&job=deactivate deactivate it
.
 !else
  !if $activetest=2
   expired but still visible by participants.
   !href cmd=reply&job=hide Hide it
  !else
   expired and hidden from participants.
   !href cmd=reply&job=expire Make it reappear
  !endif
  .
  !href cmd=reply&job=reactivate Reactivate it
  or
  !href cmd=reply&job=deactivate deactivate it
  (so that you can modify). <p>
. <p>
 !endif
 <p><center><table border=2>
 <tr><td align=right>Title:
 <td><input size=40 name=title value="$title">
 <td rowspan=3><input type=submit value=Modify>
 <tr><td align=right>Description:
 <td><textarea name=desc rows=2 cols=40>$desc</textarea>
 <tr><td align=right>Presentation page:
 <td><input size=30 name=prpage value="$prpage"><small>
 !href target=wims_mhelp cmd=help&special_parm=sheetpage help
 </small>
 <tr><td align=right>Expiration date:<td>
 !distribute item $[$expday],$[$expmon] into expday,expmon
 !formselect expday from 1 to 31
 !formselect expmon from 1 to 12 prompt $months
 !formselect expyear from $thisyear to $thisyear+1
 </table></form></center> <p>
 !goto end1
!endif

!form reply
<input type=hidden name=job value=text>
Title (limited to $title_limit characters):<br>
<input size=65 name=title value="$title"> <p>
And a text of explanation (limited to $desc_limit characters; html
tags and links are allowed):<br>
<textarea rows=6 cols=65 name=desc>$desc</textarea> <p>
Or a presentation page:
<input size=30 name=prpage value="$prpage"><small>
!href target=wims_mhelp cmd=help&special_parm=sheetpage help
</small> <p>
<center><input type=submit value="Register the texts"></center>

<p>Expiration date of the sheet:
!distribute item $[$expday],$[$expmon] into expday,expmon
!formselect expday from 1 to 31
!formselect expmon from 1 to 12 prompt $months
!formselect expyear from $thisyear to $thisyear+1
&nbsp; <input type=submit value="Register">

!ifval $activetest=-1
 <p>This sheet does not exist yet.
 <input type=submit value="Click to create it"> (but fill in the title first).
 </form>
 !goto end
!else
 </form>
 !if $exocnt>0
  <p>This sheet is not yet activated.
  !href cmd=reply&job=prep_activate Click here
  to activate it.
  (Students only see active sheets; on the other hand, you can no longuer
  modify the sheet once it is activated.)
 !endif
!endif

:end1
<p>
!if $exocnt=0
 This sheet has no content yet.
!else
 <a name=list></a>
 Content of the sheet: <small>[
 !href module=adm/sheet&sh=$sheet&job=print Printable version
 ]</small>
 <ol>
 !for i=1 to $exocnt
  <li>
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sheet
  !distribute lines $ex into di,pa,re,we,ti,de,dep
  !set dep=!trim $dep
  !href cmd=new&module=$di&worksheet=$sheet&$pa $ti
, $re points of weight $we.
  !if $activetest=0
   &nbsp;(
   !if $i>1
    !href cmd=reply&job=moveup&moveup=$i#list Move&nbsp;up
    &nbsp;
   !endif
   !href cmd=reply&job=prep_modify&modif=$i Modify
   &nbsp;
   !href cmd=reply&job=delete&delete=$i#list Delete
   !if $dep!=$empty
    &nbsp;
    !href cmd=reply&job=prep_modify&modif=$i Dependancies
   !endif
   )
  !endif
 !next i
 </ol>
!endif

!if $activetest=0
 <p>To add a work (exercise, course, tool, etc.) to a sheet, go to
 !href $ the home page
 of the class, take the corresponding item in the list of applications,
 and choose its level of difficulty. Then use the link at the end of the page
 of this exercise to add it to the sheet.
 <p>
 You can
 !href cmd=reply&job=prep_putsource insert a source file
 !if $erasable=yes
  into this sheet, or even
  !href cmd=reply&job=prep_erase erase this sheet
 !endif
.
!endif

!if $activetest>=0
 <p>
 !href module=adm/sheet&sh=$sheet Participants' view of the sheet.
 &nbsp;
 !href cmd=reply&job=getsource Source of the sheet.
 <p>
 !form reply
 <input type=hidden name=allow value=yes>
 The registration of scores is
 !formselect allowtype list all, none, select prompt open to all, closed to all, open to the following hosts (and/or times):
 <br><input size=60 name=IPlist value="$IPlist"> <br>
 <input type=submit value="Register the change">
 !set allowshare=0
 !if $sharers!=$empty
  ( for all sharing classes:
  !formselect allowshare list 0,1 prompt no,yes
  )
 !else
  <input type=hidden name=allowshare value=0>
 !endif
 </form>
 <small><b>Hint.</b> In the selective open mode, you can make time
 restrictions for score registrations, by adding words 
 <font color=blue><b>
 <tt>&gt;yyyymmdd.hh:mm</tt></b></font>
 (starting time) and/or
 <font color=blue><b>
 <tt>&lt;yyyymmdd.hh:mm</tt></b></font>
 (ending time). These must be in the
 SERVER's local time, and must be separated from other words by white spaces.
 </small>
!endif
:end
!tail

!distribute item ,, into job,source,title,desc,prpage

