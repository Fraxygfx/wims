!!##!! language independant
!read adm/title.phtml 1\
$wims_classname - $wims_institutionname\
$livret_title

!if $usercnt==0
 $name_noyetparticipant
 !exit
!endif
!if $livret_nbcomp==0
 $name_no_competence
 !exit
!endif
!default note=0
<p>
$table_header
 $table_hdtr
   <th rowspan=2>$wims_name_lastname, $wims_name_firstname</th>
   !for k=1 to $livret_nbcomp
    <th valign=center colspan=$(nbpalier_$k)>
     !href cmd=renew&job=addcomp&comp=$k $name_competence $k
     <a onmouseover="Tip('$(title_$k)',$tooltip_parm)"> ? </a>
    </th>
   !next k
 </tr>
 $table_hdtr
  !for k=1 to $livret_nbcomp
   !for j=1 to $(nbpalier_$k)
    !let desc=!record $j of wimshome/log/classes/$wims_class/livret/.comp$k
    !let desc=!line 1 of $desc
    <th valign=center><a onmouseover="Tip('$desc',$tooltip_parm)">$name_n_palier $j</a></th>
   !next j
  !next k
 </tr>    
 !for i=1 to $usercnt
  !set uu=!record $i of wimshome/log/classes/$wims_class/.userlist
  !distribute items $uu into lastname,firstname,uu
  $table_tr
   <td>
    !href cmd=reply&job=user&user=$uu $lastname,$firstname
   </td>
   !for k=1 to $livret_nbcomp
    !for j=1 to $(nbpalier_$k)
     !let t=!charcnt $(l_$(uu)_$(k)_$(j))
     !if $t>1
      !let note=$[rint($(l_$(uu)_$(k)_$(j)))]
     !else
      !let note=&nbsp;
     !endif
     <td align="center" style="background-color: $(livret_color[$note+1])">$note</td>
    !next j
   !next k
  </tr>
 !next i
$table_end

