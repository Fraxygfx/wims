
!read search.phtml
!if $overflow=yes
 !exit
!endif

!if $clcnt+$sccnt+$directcnt<1
 !if $keyword=$empty
  Il n'y a pas de classe en langue française sur ce site. Cherchez-vous une
  classe d'une
  !href module=home&list=0 autre langue
?
 !else
  Aucune classe correspondant à votre recherche.
 !endif
 !exit
!endif

!if $wims_user!=$empty
 !read direct.phtml
 !if $wims_user!=supervisor and $superclass=$empty
  !exit
 !endif
!else
Choisissez la ligne correspondant à la classe ou l'établissement 
qui vous concerne. 
 <p>
Si vous n'êtes pas encore inscrit, cliquer sur <tt>S'inscrire</tt> 
(si le statut est <tt>protégé</tt>, vous avez besoin d'un mot de passe 
que vous obtiendrez auprès de l'enseignant ou de l'administrateur).

 <p>Sinon, cliquez <tt>Entrer</tt> pour y entrer. 
!endif

!if $sccnt>0
 <p>$table_header
 <caption>Etablissements et groupements de classes</caption>
 $table_hdtr
 <th>Nom<th colspan=2>Elève</th><th>Administrateur</th><th>Statut</th>
 !for i=1 to $sccnt
  !set l=!line $i of $sclasses
  !distribute items $l into code,expire,inst,cl,la,ty,lv,pswd
  !if $superclass=$empty
   !set show=$inst
  !else
   !set show=$cl
  !endif
  !set supv=!item 9 of $l
  !if $supv=$empty
   !reset class_password
   !defread wimshome/log/classes/$code/.def
   !set supv=$class_supervisor
   !set pswd=$class_password
  !endif
  
  $table_tr<td align=center>
  !!!href module=$module&type=authparticipant&class=$code 
  $show
  </td> <td align=center>
  !href module=$module&type=authparticipant&class=$code Entrer
  </td> <td align=center>
  !href module=adm/class/reguser&class=$code&subclass=yes S'inscrire
  </td> <td align=center>
  $supv
  </td> <td align=center>
  !if $pswd=$empty
   ouvert
  !else
   protégé
  !endif
 !next i
 $table_end <p>
!endif

!if $clcnt>0
 <p>$table_header
 !if $superclass=$empty
  <caption>Classes indépendantes</caption>
  $table_hdtr
  <th>Nom de classe</th><th colspan=2>Elève</th>
  <th>Enseignant</th><th>Niveau</th><th>Statut</th>
 !else
   !if $splevel>1
    !set inst=!defof class_description in wimshome/log/classes/$superclass2/.def
   !else
    !set inst=!defof class_institution in wimshome/log/classes/$superclass/.def
   !endif
   <caption>$Name_levels dans $inst
   !if $splevel>1
    !href cmd=reply&type=$type&superclass2=$sparent Up
   !endif
   </caption>
   $table_hdtr
   <th>Nom de classe</th><th colspan=2>Elève</th>
   <th>Enseignant</th><th>Niveau</th><th>Statut</th>
   !if $superclass!=$empty and $stype=4
    <th>+</th>
   !endif
 !endif
 !for i=1 to $clcnt
  !set l=!line $i of $classes
  !distribute items $l into code,expire,inst,cl,la,ty,lv,pswd
  !set supv=!item 9 of $l
  !if $supv=$empty
   !reset class_password
   !defread wimshome/log/classes/$code/.def
   !set lv=$class_level
   !set supv=$class_supervisor
   !set pswd=$class_password
  !endif
  !distribute char $lv into l_,y_
  
  !if $superclass=$empty
   !set show=$inst <br>$cl
  !else
   !set show=$cl
  !endif
  $table_tr<td align=center>
    $show
  </td><td align=center> 
  !href module=$module&type=authparticipant&class=$code Entrer
  </td> <td align=center> 
  !if $splevel=$empty or $splevel=2
   !href module=adm/class/reguser&class=$code S'inscrire
  !else
   <font color=grey>S'inscrire</font>
  !endif
  </td><td align=center>
  $supv
  </td><td align=center>$(n_$l_)<br>année $y_</td>
  <td align=center>
  !if $pswd=$empty
   ouvert
  !else
   protégé
  !endif
  </td>
   !if $superclass!=$empty and $stype=4
    <td>
    !if $ty!=1
     !href cmd=reply&type=$type&superclass2=$code $sousclasses
    !else
    -
    !endif
   !endif
 !next i
 $table_end <p>
!endif


