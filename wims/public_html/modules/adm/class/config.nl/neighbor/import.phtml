
!distribute item message of the day,\
	oefening,werkblad,overhoring,document,leerling\
	into name_motd,name_exo,name_sheet,name_exam,name_doc,name_user
!set code1=Niets
!set code2=$wims_name_yes maar wel verschillend 
!set code3=$wims_name_yes , actief en verschillend 

De virtuele klas
!href cmd=reply&job2=def $nei_description
 van
<em><font color=blue>$nei_institution</font></em>
!if $newcnt1+$newcnt2<1
heeft niets dat U kunt delen/lenen.
 !href cmd=reply&job2=def $wims_name_back2
.
 !exit
!endif
heeft het volgende -leenbare- materiaal.
<small>
!href cmd=help&special_parm=import $wims_name_help
</small>

!if $newcnt1>0
 <p>$table_header
 <caption>$name_teaching</caption>
 $table_hdtr<th>$wims_name_Type<th>$wims_name_title<th><small>$wims_name_name</small>
 <th><small>Inhoud</small></th>
 <th>De zelfde naam in Uw klas</th><th>$wims_name_action</th>
 !for i=1 to $newcnt1
  !set l=!line $i of $newrec
  !set ll=!words2items $l
  $table_tr
  <td align=center>$(name_$(rtype$i))
  <td align=center>$(rname$i)
  <td align=center><small>$(rName$i)</small>
  <td align=center>$(rcnt$i)
  <td align=center>$(code$(rcode$i))
  <td align=center>
  !if $(rstat$i)=0 or $(rcnt$i)=0
   !if $(rcnt$i)=0
    Empty
   !else
    <small>werk in uitvoering </small>
   !endif
  !else
   !if $(rcode$i)!=2 or $(rtype$i) notwordof motd exo
    !href cmd=reply&job2=import2&import=$ll&impmethod=add $wims_name_import
   !endif
   !if $(rcode$i)=2
    &nbsp;
    !href cmd=reply&job2=import2&import=$ll&impmethod=replace&replace=$(rmine$i) Vervang
   !endif
  !endif
 !next i
 $table_end
!endif

!if $newcnt2>0
 <p>$table_header
 <caption>$wims_name_student
 !if $newcnt2>2
 [
  !href cmd=reply&job2=import2&import=user,-all- $name_everybodyimport
 ]
 !endif
 </caption>
 $table_hdtr<th>login<th>$wims_name_lastname<th>$wims_name_firstname<th>$wims_name_action
 !for i=1 to $newcnt2
  $table_tr<td>$(ulogin$i)
  <td>$(uname2$i)<td>$(uname1$i)<td align=center>
  !if $(ucode$i)=1
   !href cmd=reply&job2=import2&import=user,$(ulogin$i) $wims_name_import
  !else
   !if $(ucode$i)=2
   Deze loginnaam bestaat reeds in deze klas
   !else
    Verwijderde gebruiker bestaat in deze klas
   !endif
  !endif
  </td>
 !next i
 $table_end
!endif

