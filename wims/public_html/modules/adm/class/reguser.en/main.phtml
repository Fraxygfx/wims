!!<html><head>
!!$wims_html_header
!!</head><body $wims_htmlbody>
!!!headmenu
!read adm/css.phtml
!header1

!!if $cmd=help
 !!read help.phtml
 !!goto end
!!endif

!read names.phtml
<h1 class="wims_title">WIMS at $httpd_HTTP_HOST
<br>
Registration of participation to $name_classess
</h1> <p>

!if $error!=$empty
 <p>
 !read msg.phtml
 <p>
 !goto end1
!endif

!if $classcnt=0
 There is no class that you can register to. Sorry, try again later! !goto end
!endif

!if $class=$empty
 Please choose the class to which you want to participate
 (click on its name). <p>
 $table_header
 !if $ctype=0
  $table_hdtr
  <th>Name<th>supervisor<th>level<th>language</th>
  <th>available<br>places<th>status
 !else
  <caption>$institution_name</caption>
  $table_hdtr
  <th>Name<th>supervisor<th>level
  <th>status
 !endif
 !for cl in $classlist
  !reset class_supervisor,class_email,class_level,class_lang,class_institution,class_description,class_password,class_limit
  !defread wimshome/log/classes/$cl/.def
  !distribute char $class_level into l1,l2
  !set part=!recordcnt wimshome/log/classes/$cl/.userlist
  $table_tr<td align=center>
  !if $ctype=0
   !href module=$module&cmd=reply&class=$cl $class_institution<br>$class_description
  !else
   !href module=$module&cmd=reply&class=$cl $class_description
  !endif
  <td align=center>
  !mailurl $class_email $class_supervisor
  </td>
  !if $class_level=$empty
   !set pname=!defof class_description in wimshome/log/classes/$class_parent/.def
    <td align=center>$pname</td>
  !else
  <td align=center>$(level$l1)<br>year $l2</td>
  !endif
  !if $ctype=0
   <td align=center>$(lang_$class_lang)</td>
   <td align=right>
   !if $class_limit-$part>$site_dispo
    $site_dispo
   !else
    $[$class_limit-$part]
   !endif
  !endif
  !if $class_password=$empty
   <td align=center>open</td>
  !else
   <td align=center>protected</td>
  !endif
 !next cl
 $table_end <p>
 <b>Remark.</b> You may register yourself freely into any open classes. On the
 contrary, protected classes need a password to register (the password should
 be obtained from the supervisor of the class).
 !goto end1
!endif class=empty

!set classname=<b><font color=green>$description</font></b>\
 of <b><font color=green>$institution</font></b>

!if $password!=$empty and $passverify=$empty
  $name_classessss
  !href module=adm/class/classes&type=authparticipant&class=$class <b>$description</b>
 of <b><font color=green>$institution</font></b>
 !if $cltype iswordof 4 2
  is protected by a password and direct inscription is reserved to 
  <b>teachers</b>.
  !if $classcnt>0 
  If you are a student, you should directly register to a 
  !href cmd=new&module=$module&class=$class&subclass=yes subordinate class
  !endif
 !else
 is protected by a password.
  !endif
 <p> You have to type this
 password before you can register yourself
  !if $cltype iswordof 2 4
  as a <b>teacher</b> in it.
 !else
  as a participant to this class.
 !endif
 !form reply
 <input type=hidden name=step value=1>
 <center> <p><label for="classpass">Password:</label>
 <input type=password name=classpass id="classpass">
 <p>
 <input type=submit value="Send this password">
 </center></form> <p>
 !if $cltype iswordof 2 4
  <b>ATTENTION</b>. Direct registration to an institution portal is reserved
  to teachers. Students should directly register to a
  !href cmd=new&module=$module&class=$class&subclass=yes subordinate class
.
  <p>
  <b>Remark</b>. If the administrator ($supervisor) of the portal
 !else
  <b>Remark</b>. If the supervisor ($supervisor) of this class
 !endif
 allows you the access, you should be able to obtain the password from him/her.
(
 !mailurl $smail Write to the supervisor
)
 !goto end1
!endif

!if $step=$empty or $step<2
 !if $superclass!=$empty and $wims_class!=$superclass and $wims_user=supervisor
  !form reply
  <input type=hidden name=step value=1>
  <label for="search">To bring a participant already registered in $name_classes,
  give a word in his/her name or login:</label>
  <input size=20 name=search id="search" value="$search">
  <input type=submit value=OK>
  </form> <p>
  !default foundcnt=0
  !if $foundcnt>0
   $table_header
   <caption>Click on a name to bring to the class.</caption>
   $table_hdtr<th>Name</th><th>login</th></tr>
   !for i=1 to $foundcnt
    !set l=!line $i of $found
    !distribute item $l into l_,f_,L_
    $table_tr<td>
    !href cmd=reply&step=2&login=$L_ $l_, $f_
    </td>
    <td>$L_</td></tr>
   !next i
   $table_end
   !goto end1
  !endif
  !if $search!=$empty and $foundcnt=0
   (No participant account found with <tt>$search</tt> in name or login.) <p>
  !endif
  Please fill-in the following informations about the new participant to
  $classname.
  !set name_Your=The
  !set name_you=the participant
 !else
  In order to register yourself as a participant to the class $classname,
  please fill-in the following informations.
  !set name_Your=Your
  !set name_you=you
 !endif
  !form reply
 <input type=hidden name=step value=2>
 <ul>
 <li><label for="lastn">$name_Your family name</label> <input size=20 name=lastn id="lastn" value="$lastn">
  and <label for="firstn">first name</label> <input size=20 name=firstn id="firstn" value="$firstn">
  <p>
 <li><label for="login">Choose a login name:</label>
  <input size=16 name=login id="login" value="$login"> <br>
  This will be the name which $name_you will use to log into the $classname later.
  Please choose a word containing between 4 and 16 characters
  (digits or letters without accent).
  <p>
 <li><label for="pass">And a password:</label>
  <input type=password size=16 name=pass id="pass"> <br>
  the password must contain between 4 and 16 characters (digits or letters
  without accent).
  <p>
 <li><label for="email">$name_Your email address (optional):</label><br>
  <input size=60 name=email id="email" value="$email">
  <p>
 !if $wims_user=supervisor and $class=$wims_class and $cltype iswordof 2 4
  <li>Type of the account:
  !default utype=0
  !formradio utype list 0,1 prompt student, teacher
 !endif
 <center><input type=submit value="Register"></center>
</ul>
</form>
 !goto end1
!endif

!if $step=2
 Hello, $firstn $lastn!<p>
 You are registering yourself as a participant to the $name_classesss $classname,
 with the login name &nbsp;
 ``<tt><b><font color=green>$login</font></b></tt>''. <p>
 !if $email=$empty
  You didn't give a valid email address. <p>
 !else
  Your email address: <tt>$email</tt> <p>
 !endif
 <label for="pass2">In order to verify the exactness of your password, please retype it again:</label>
 !form reply
 <input type=hidden name=step value=3>
 <p><center><input type=password size=16 name=pass2 id="pass2">
 <p><input type=submit value="Send the password"></center>
 </form> <p>
 !goto end1
!endif

!if $step=3
 !if $wims_user=supervisor
  The participant $firstn $lastn is added to the $name_classe.
  !if $wims_supertype=4
   !readproc adm/gateway/transfer $login test
   !if $transfer_test>0
    <p>
    This participant may have activities in another class. 
    !href module=adm/class/userlist&job=transfer&getuser=$login Transfer the activities
    into this class.
    <p>
   !endif
  !endif
  !href module=adm/class/userlist List of participants
.&nbsp;
  !href module=$module&step=1 Add another participant
.
  !goto end
 !endif
 Hello, $firstn $lastn!<p>
 Welcome to the class $classname.
 You are now a participant of this class/course, with the login name
 ``<b><font color=green>$login</font></b>''. <p>
 You may now
 !if $wims_class!=$empty
  !href module=adm/class/classes&type=authparticipant&class=$class&auth_user=$login \
	go to work in it
.
 !else
  <a href="http://$httpd_HTTP_HOST$httpd_SCRIPT_NAME?lang=$lang&+module=adm/class/classes&+type=authparticipant&+class=$class&auth_user=$login">
  go to work in the class</a>. (Also think of putting this link in your
  bookmark.)
 !endif
 !goto end
!endif

:end1
!if $wims_user=supervisor
 !goto end
!endif
<p>
!if $cltype iswordof 2 4
 !set wims_menu_tail=!append line portallist,1,module=adm/class/classes&type=supervisor\
to $wims_menu_tail
 !!href module=adm/class/classes&type=supervisor List of classes
!!.
!else
 !set wims_menu_tail=!append line classlist,1,module=adm/class/classes&type=participant\
to $wims_menu_tail
 !!href module=adm/class/classes&type=participant List of classes
!!.
!endif
:end
!tail

!reset step, subclass

