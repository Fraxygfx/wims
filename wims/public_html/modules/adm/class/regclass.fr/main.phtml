
!read adm/css.phtml
!header1

!!if $cmd=help
 !!read help.phtml
 !!goto end
!!endif

!read names.phtml
!read adm/title.phtml 3\
WIMS à $httpd_HTTP_HOST\
$title

!if $toomany=yes
 !read toomany.phtml
 !goto end
!endif

!if $error!=$empty
 <p>
 !read msg.phtml
 !if $restart!=no
  <p>
  !href module=$module&cmd=reply&step=0 $name_again
 !endif
 <p>
 !goto end
!endif

!if $step=$empty or $step=pre
 $name_choose
 <p>
 !href cmd=reply&step=0&cltype=0 $wims_name_create_class
 !set wims_menu_items=!append line create_class,1,cmd=reply&step=0&cltype=0\
 to $wims_menu_items
 <p>
 !if 2 isitemof $alltypes
  !href cmd=reply&step=0&cltype=2 $wims_name_create_superclass
  !set wims_menu_items=!append line create_superclass,1,cmd=reply&step=0&cltype=2\
 to $wims_menu_items
 !else
  <font color=blue><strike>$wims_name_create_superclass</strike></font> ($noright).
 !endif
 <p>
 !if 4 isitemof $alltypes
  !href cmd=reply&step=0&cltype=4 $wims_name_create_portal
  ($name_expert)
  !set wims_menu_items=!append line create_portal,1,cmd=reply&step=0&cltype=4\
 to $wims_menu_items
 !else
  <font color=blue><strike>$wims_name_create_portal</strike></font> ($noright).
 !endif
 <p>
 !read help.phtml.$lang 1
 !goto end
!endif

!if $step=0
 !read help.phtml.$lang 2
 !form reply
 <input type=hidden name=step value=1>
 <ul>
 <li><label for="institution">$name_Name_portal ($name_atmost $instlim $name_characters) :</label>
 !if $Cltype isin 13
  !default institution=$wims_institutionname
 !endif
  <input size=$instlim name=institution value="$institution" id="institution">
 </li>
 <li><label for="description">$wims_name_name $name_classesss ($name_atmost $desclim $name_characters) :</label>
  <input size=$desclim name=description value="$description" id="description">
 </li>
 
 !if $Cltype notin 24
 <li><label for="level">$wims_name_level $name_classesss </label>
  <select name=level id="level">
   !for k=1 to $nblevel
    !if $(levelid[$k])=$level
     <option selected value=$(levelid[$k])>$(leveldesc[$k])</option>
    !else
     <option value=$(levelid[$k])>$(leveldesc[$k])</option>    
    !endif   
   !next k
  </select>
 </li>
 !endif

 <li><label for="supervisor">$wims_name_firstname  $wims_name_lastname ($wims_name_supervisor)</label>
  <input size=$suplim name=supervisor value="$supervisor" id="supervisor">
 </li>

 <li><label for="email">$wims_name_email</label>
  <input size=$emaillim name=email value="$email" id="email">
  !if $regpolicy=email
   <br>(L'opération de création $name_classesss ne peut aboutir que si vous fournissez
   une adresse électronique correcte et accessible en ce moment même.)
  !endif
  <br>
 </li>

 <li>Choisissez le <label for="passsup">mot de passe $name_sup</label> :
  <input size=16 type=password name=passsup id="passsup"> <small>(entre 4 et 16 caractères, chiffres ou lettres sans accent). Ce mot de passe vous permettra d'accéder à votre $name_classe en tant
  que responsable. Vous devez être le seul à le connaître.</small> <br>
 </li>

 <li>Choisissez <label for="password">le mot de passe $name_classesss :</label>
  <input size=16 type=password name=password id="password"> <small>(entre 4 et 16 caractères, chiffres ou lettres sans accent). Le mot de passe $name_classesss sera exigé lors de l'inscription
  !if $Cltype<2
 des participants de la classe ; vous pouvez donc le diffuser auprès
  de vos étudiants/élèves. 
  !else
des enseignants $name_classesss ;
 vous ne devez pas le diffuser auprès
  de vos étudiants/élèves. 
 !endif
 </small><br>
 </li>
 
 <li><label for="exp_day">$wims_name_expiration</label>
 <select name=exp_day id="exp_day">
  !for i=1 to 31
   <option value=$i
   !ifval $i=$exp_day
    selected
   !endif
   >$i
  !next i
  </select> <select name=exp_month id="exp_month">
  !for i=1 to 12
   <option value=$i
   !ifval $i=$exp_month
    selected
   !endif
   >
   !item $i of $months
  !next i
  </select> <select name=exp_year id="exp_year">
  !for i=$thisyear to $thisyear+1
   <option value=$i
   !ifval $i=$exp_year
    selected
   !endif
   >$i
  !next i
  </select> <small> C'est la date à laquelle votre classe sera automatiquement archivée.</small><br>
 </li>

 <li><label for="limit">Limite du nombre de participants de votre $name_classe :</label>
  <input size=3 name=limit value="$limit" id="limit">
  <small>(< $wims_class_user_limit.) </small><br>
 </li>
 
 <li><label for="secure">Accès sécurisé $name_sup</label>
  !default secure=all
  <input size=30 name=secure value="$secure" id="secure"> <br>
  !reset secure
  <small>Noms des postes à partir desquels vous effectuerez les opérations
  sensibles sur votre $name_classe. 
  !if $Cltype<3
    Facultatif.
  !endif 
  </small> <br>
 </li>
 </ul>
 <center><input type=submit value="$name_continue"></center>
 </form>

 !goto end
!endif

!if $step=1
 Bonjour, $supervisor! <p>
 Vous êtes en train de créer 
 !if $Cltype<2
 la
!else
 le
 !endif
 $name_classe $classname sur le site WIMS à l'adresse
 $httpd_HTTP_HOST,
 !if $Cltype iswordof 0 1
  !let tmp=!positionof item $ilevel in $levelid
  de niveau ``<b><font color=green>$(leveldesc[$tmp])</font></b>'',
 !endif
 qui prendra fin le $exp_day/$exp_month/$exp_year
 . Le nombre maximum de participants est de $ilimit ; 
 l'inscription est
 !if $pword!=$empty
  protégée par le mot de passe $name_classesss.
 !else
  ouvert à tout le monde sur l'internet (en êtes-vous sûr? Sachez que le
  gestionnaire du site peut les refuser et les détruire).
 !endif
 <p>
 Si vous décelez une erreur dans les renseignements ci-dessus, veuillez
 !href module=$module&cmd=reply&step=0 la corriger.
 <p>Sinon, afin de vérifier l'exactitude de vos mots de passe,
 veuillez les retaper encore une fois. <p>
 !form reply
 <input type=hidden name=step value=2>
<label for="passsup"> $wims_name_Password ($name_sup) :</label>
 <input type=password name=passsup id=passsup> <p>
 !if $pword!=$empty
 <label for="password">$wims_name_Password ($name_classesss)</label>
  <input type=password name=password id=password> <p>
 !endif
 <center><input type=submit value="$name_continue"></center>
 </form>
 !goto end1
!endif

!if $step=2
 !read help.phtml.$lang step2
 !form reply
 <input type=hidden name=step value=3>
 <center><label for="typecode">Code de votre $name_classe :</label> <input size=12 name=typecode id="typecode">
 <p><input type=submit value="$name_continue"></center>
 </form>
 <b>Attention</b> Ce code ne sert qu'une seule fois. Il ne peut pas
 être utilisé pour vous connecter à votre $name_classe&nbsp;!
 <p>
 !goto end1
!endif

!if $step=3
 Bonjour, $supervisor!<p>
 Votre $name_classe $classname est maintenant en place. Félicitations
 et amusez-vous bien&nbsp;!<p>
 Le nombre de participants est limité à $ilimit participants (y compris vous) 
 !if $Cltype iswordof 0 1
  !let tmp=!positionof item $ilevel in $levelid
  au niveau <font color=green>$(leveldesc[$tmp])</font>.
 !endif
 <p>
 !read links.phtml
 !goto end
!endif

:end1
!!<p>
!!!href module=$module&cmd=reply&step=0 $name_comeback

:end
!if $step>=0 and $cmd=reply
 !set wims_menu_items=!append line back2,1,module=$module\
 to $wims_menu_items
!endif

!tail

!set step=

