<center>$classname<p>

!if $wims_user=supervisor
  !href cmd=resume Torna all'elenco degli iscritti
!endif
<h3>Dettagli del lavoro svolto da 
 !href cmd=reply&job=userprop&getuser=$getuser $firstname $lastname
<br><font size="-1">login&nbsp;: <tt>$getuser</tt>
!if $user_email!=$empty
 &nbsp;e-mail: <a href="mailto:$user_email">$user_email</a>
!endif
</font></h3> 
  !if $user_photourl!=
   <img src="$user_photourl" width=100 style="position:absolute;top:auto;right:5%;" alt="$firstname $lastname">
  !endif
!if $eremain>0
 <p>
 Questo studente ha una sessione d'esame in corso:
 !href cmd=reply&job=userexam&getuser=$getuser $esession
!else
 !if $wims_user=supervisor and $wims_supertype=4
  !readproc adm/gateway/transfer $getuser test
  !if $transfer_test>0
   !href cmd=reply&job=transfer&getuser=$getuser Importa la sua attività da altro corso
!endif
 !endif
!endif
</center> <p>

!if $manual>0 and $gcnt>0 and $teacher!=$empty
 $table_header
 <caption><small>
 min/media/max del corso:
 $min_glob/$mean_glob/$max_glob</small></caption>
 $table_hdtr<th>Nome<th>Percentuale<th>Media
 $table_hdtr<td>Voti assegnati dal docente
     <td align=center>$manual%<td align=center>$mav
 $table_hdtr<td>Voti registrati dal server
     <td align=center>$[100-$manual]%<td align=center>$per
 $table_hdtr<td><b>Media globale</b> (su $scoremax)
     <td align=center>100%<th>$globalav
 $table_end<p>
 $table_header$table_hdtr<th colspan=3>Voti assegnati dal docente
 !if $wims_user=supervisor
  <br>
  !href lang=$lang&module=adm/class/grades&method=user&un=$getuser Modifica
 !endif
 </th>
 $table_hdtr<th>titolo<th>peso<th>voto</th>
 !set v_=0
 !for i=1 to $gcnt
  !set t_=!item $i+2 of $titles
  $table_tr<td>$t_<td align=center>$(w$i)<td align=center>$(g_$i)</td>
 !next i
 $table_hdtr<th colspan=2>Media<td align=center>$mav</td>
 $table_end <p>
!else
 <center>
 Media globale: <b>$per</b> su $scoremax. 
 !if $min_glob<$scoremax
  <br><small>min/media/max del corso:
  $min_glob/$mean_glob/$max_glob</small>
 !endif
 </center> <p>
!endif

!readdef wimshome/log/classes/$wims_class/.def
!if $eactivecnt>0
 $table_header$table_hdtr<th colspan=4>Voti d'esame
 !if $wims_user=supervisor or $class_examshow!=no
  <small>
  !href cmd=reply&job=examcheck&checkuser=$getuser [dettagli]
  </small>
 !endif
 $table_hdtr<th>Numero<th>Titolo<th>Voto/$scoremax
 !for i in $activexams
  !set l=!record $i of wimshome/log/classes/$wims_class/exams/.exams
  !set t=!line 4 of $l
  
  $table_tr<td align=right>$i
  <td align=center>$t
  <td align=center>$[rint(10*$(es_$i)*$scoremax)/100]
 !next i
 $table_end <p>
!endif

!for i=1 to $activecnt
 !set l_=!line $i of $activesheets
 !distribute items $l_ into n_,e_
 !set L_=!line $n_ of $defaultformula
 !distribute word $L_ into A_,B_
 !set t_=!item 3 to -1 of $l_
 !set t_=!char 1 to $title_limit of $t_
 !set v_=!getscorerequire sheet=$n_ user=$getuser
 !set w_=!wordcnt $v_
 !set S_=0
 !for k=1 to $w_
  !set a_=!word $k of $v_
  !set S_=$[$S_+$a_]
 !next k
 !if $S_<=0
  !goto next
 !endif
 $table_header$table_hdtr<th colspan=7>Test numero $i&nbsp;: $t_
 <small>peso $A_, severit&agrave; $B_<br>
 !set A_=!line $n_ of $percents
 !distribute words $A_ into p1,p2
 !if $p2!=$empty
  !distribute item $[$p1/100],$[$p2/10] into x_,y_
  !set p_=$[rint(100*$scoremax*$(f_$i))/100]
  Completato il $p1%, votazione media $p2/10, equivalente a <b>$p_</b> su $scoremax.
  !if $(min_$i)<$scoremax
   <br>min/media/max del corso: $(min_$i)/$(mean_$i)/$(max_$i)
  !endif
 !endif
 </small>
 $table_hdtr<th>Numero<th>titolo<th>punti<br>richiesti<th>peso
     <th>punti<br>ottenuti<th>%<th>votazione<br>media
 !for j=1 to $e_
  $table_tr<td align=right>$j
  <td>$(title_$(n_)_$j)
  <td align=right>
  !set v_=$(req_$(n_)_$j)
  $v_
  <td align=center>$(weight_$(n_)_$j)
  <td align=right>
  !set d_=!getscore user=$getuser sheet=$n_ work=$j
  !default d_=0
  $d_
  <td align=right>
  !if $v_>0
   $[round(100*$d_/$v_)]%
  !else
   ---
  !endif
  <td align=right>
  !set d_=!getscoremean user=$getuser sheet=$n_ work=$j
  !default d_=0
  $d_
 !next j
 $table_end <p>
 :next
!next i
<center>
!if $exologs!=$empty
 <p>
 !href module=adm/class/exolog&cmd=new&job=list&checkuser=$getuser Elenco degli esercizi registrati
!endif
<p>
!if $wims_user=supervisor
!! !href cmd=reply&job=getraw&getraw=$getuser Dati non elaborati
!! .
!! !href cmd=resume Elenco iscritti
!! .
!! <p>
!! !href cmd=reply&job=delprep&delprep=$getuser Escludi questo studente
!!, o
!! !href module=adm/class/passwd&job=part&part=$getuser modificane la password
.
!set wims_menu_items=!append line rawdata,1,cmd=reply&job=getraw&getraw=$getuser\
 delprep,1,cmd=reply&job=delprep&delprep=$getuser\
passwd,1,module=adm/class/passwd.fr&job=part&part=$getuser\
part_list,1,cmd=resume\
to $wims_menu_items
!else
 !href module=home Home page del corso
!endif
</center> <p>

