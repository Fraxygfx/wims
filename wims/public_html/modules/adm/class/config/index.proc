common=userlist,oef,doc

!if $class_type=4
 !bound job2 within structure,teacherlist,$common default $
!else
 !if $class_type=2
  !bound job2 within teacherlist,$common default $
 !else
  !bound job2 within $common default $
 !endif
!endif

!if $job2=$empty
 job=list
 !changeto $job.proc
!endif

!if $job2=structure
 !read adm/gateway/mkindex
 lcnt=!linecnt $ldata
 ccnt=!linecnt $cdata
 pcnt=!linecnt $pdata
 icnt=!linecnt $idata
 result=$lcnt levels, $ccnt classes, $pcnt programs, $icnt interlevel courses found.
 !exit
!endif

!if $job2=teacherlist
 !read adm/gateway/mkteacherlist
 result=!record 0 of wimshome/log/classes/$wims_class/.teacherlist
 result=!linecnt $result
 result=$result teachers found in $wims_class.
 !exit
!endif

!if $job2=userlist
 !read adm/class/mkuserlist
 result=!recordcnt wimshome/log/classes/$wims_class/.userlist
 result=$result participants found in $wims_class.
 !if $wims_superclass!=$wims_class
  !exit
 !else
  !if $class_type=4
   !for j_ in classes,courses,icourses
    list=!record 0 of wimshome/log/classes/$wims_superclass/$j_
    list=!column 1 of $list
    !for cl_ in $list
     !read adm/class/mkuserlist $wims_superclass/$cl_
     Result=!recordcnt wimshome/log/classes/$wims_superclass/$cl_/.userlist
     result=$result \
$Result participants found in $cl_.
    !next
   !next
   !exit
  !else
   classcnt=!recordcnt wimshome/log/classes/$wims_superclass/.subclasses
   !for c_=1 to $classcnt
    cl_=!record $c_ of wimshome/log/classes/$wims_superclass/.subclasses
    cl_=!item 1 of $cl_
    !read adm/class/mkuserlist $cl_
     Result=!recordcnt wimshome/log/classes/$cl_/.userlist
     result=$result \
$Result participants found in $cl_.
   !next
  !endif
 !endif
 !exit
!endif

!if $job2=oef
 result=!sh cd $wims_home/log/classes/$wims_class\
  src2def oef
 !exit
!endif

!if $job2=doc
 Docdir=log/classes/$wims_class/doc
 docdir=wimshome/$Docdir
 !read adm/docindex.proc
 result=!recordcnt $docdir/.index
 result=$result documents found.
 !exit
!endif

