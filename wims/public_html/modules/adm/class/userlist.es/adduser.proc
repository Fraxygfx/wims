
uu=$wims_read_parm
l=!text select $char_login in $uu
n=!charcnt $l
!if $l!=$uu or $n<$login_min or $n>$login_max
 !exit
!endif

!reset user_lastname,user_firstname,user_password,user_email,user_comments,\
	user_exists
!defread wimshome/log/classes/$wims_class/.users/$uu

changed=0
!for n in lastname,firstname,email,regnum,comments
 !if $n isitemof $replacelist,lastname,firstname,email
  !if $(up_$n)!=$(user_$n)
   l_=!singlespace $(up_$n)
   l_=!replace , by &#44; in $l_
   l_=!replace < by &lt; in $l_
   l_=!translate internal "#*!$$ to $.....$ in $l_
   !if $n!=comments
    l_=!char 1 to 40 of $l_
   !else
    l_=!char 1 to 300 of $l_
   !endif  
   up_$n=$l_
   changed=1
  !endif
 !else
  up_$n=$(user_$n)
 !endif
!next n

!if $up_password!=$empty and $up_password!=$user_password
 changed=1
!endif
!if $changed!=1
 !exit
!endif

p=!text select $char_passwd in $up_password
n=!charcnt $p
!if $p!=$up_password or $n<$passwd_min or $n>$passwd_max
 !if $user_password!=$empty and $up_password=$empty
  up_password=$user_password
 !else
  bad=!append item $uu to $bad
  !exit
 !endif
!endif
change=!append item $uu to $change

up_exists=yes
up_password=!passcrypt $up_password
!for field in lastname,firstname,password,email,regnum,comments,exists
 !if $(user_$field) != $(up_$field)
  !setdef !set user_$field=$(up_$field) in wimshome/log/classes/$wims_class/.users/$uu
 !endif
!next field

wims_class_log=User info $uu

