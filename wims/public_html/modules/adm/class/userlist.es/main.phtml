!read adm/css.phtml
!header1

!read names.phtml

!if $cmd=help
 !read help.phtml
 !goto end
!endif

!if $job!=$empty and $job!=sheets
 !read $job.phtml
 !goto end
!endif

!read adm/title.phtml 1\
$wims_classname - $wims_institutionname\
$wims_name_part_list

!if $error!=$empty
 <p>
 !read msg.phtml
 <p>
 !href module=home $wims_name_class_home
. <p>
 !goto end
!endif

!if $usercnt==0
 $name_noyetparticipant
 [
 !href module=adm/class/reguser&step=1 $wims_name_addstudent
 ]
 
 [
 !href cmd=reply&job=csv $wims_name_csv
 ]
 
!else
 <center>
 !set deleted=!sh cd $wims_home/log/classes/$wims_class/.users; ls .[0-9a-zA-Z]* 2>/dev/null
 !set deleted=!trim $deleted
 [
 !href module=adm/class/reguser&step=1 $wims_name_addstudent
 ] [
 !href cmd=reply&job=csv $wims_name_csv
 ]
 !if $deleted!=$empty
  &nbsp;[
  !href cmd=reply&job=recover $name_partdeleted
  ]
 !endif
 </center>
 <p>$table_header
 !if $job_affi=notes
  <caption>$name_formula2</caption>
 !else
  !let tmp=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheets
  !let tmp=!values v for v=1 to $tmp
  !let showsheet=!listintersect $showsheet and $tmp
  !if $job_affi=qualite
   <caption>$name_qualitygot</caption>
  !else
   <caption>$name_percentagedone</caption>
  !endif
 !endif
 !if $manual>0 and $job_affi=notes
  $table_hdtr<th rowspan=2>$wims_name_lastname, $wims_name_firstname</th>
   <th colspan=3>$name_Average</th>
  !for j in $showsheet
   !if E isin $j
    !set k=!char 2 to -1 of $j
    <th rowspan=2><small>$name_Exam $k</small></th>
   !else
    <th rowspan=2>
    <small>
     !href cmd=reply&job=showsheet&numshowsheet=$j $name_Sheet $j
    </small></th>
   !endif
  !next j
  $table_hdtr<th><small>$(name_manual_notes[1])<th><small>$(name_manual_notes[2])<th><small>$(name_manual_notes[3])
 !else
  $table_hdtr<th>$wims_name_lastname, $wims_name_firstname</th>
  !if $job_affi=notes
   <th>$name_Average</th> 
  !endif
  !for j in $showsheet
   !if E isin $j
    !set k=!char 2 to -1 of $j
    <th><small>$name_Exam $k</small></th>
   !else
    <th><small>
     !href cmd=reply&job=showsheet&numshowsheet=$j $name_Sheet $j
    </small></th>
   !endif
  !next j
 !endif
 !distribute item 0,0,0 into mean_tea,mean_auto,mean_glob
 !distribute item 0,0,0 into cnt_tea,cnt_auto,cnt_glob
 !for i=1 to $usercnt
  !set uu=!record $i of wimshome/log/classes/$wims_class/.userlist
  !distribute items $uu into lastname,firstname,uu
  !set UU=!hex $uu
  $table_tr<td>
  !href cmd=reply&job=getuser&getuser=$uu $lastname, $firstname
  !read userscore
  !if $manual>0 and $job_affi=notes
   !set glob=$[rint($manual*$(manual_$UU)+(100-$manual)*($per))/100]
   <td align=center>$glob</td>
   <td align=center>$(manual_$UU)</td>
   <td align=center>$per</td>
   !distribute item $[$mean_glob+$glob],$[$mean_tea+$(manual_$UU)] into mean_glob,mean_tea
   !if $glob>0
    !advance cnt_glob
    !if $(manual_$UU)>0
     !advance cnt_tea
    !endif
    !if $per>0
     !advance cnt_auto
    !endif
   !endif
  !else
   !if $job_affi=notes
    <td align=center>$per</td>
    !if $per>0
     !advance cnt_auto
     !advance cnt_glob
    !endif
   !endif
  !endif
  !set mean_auto=$[$mean_auto+$per]
  !for j in $showsheet
   !if E isin $j
    !set k=!char 2 to -1 of $j
    !set p_=$[rint(10*$scoremax*$(es_$k))/100]
    !if NaN isin $p_
     !set p_=0
    !endif
    <td align=center>$p_</td>
   !else
    !set p_=!line $j of $percents
    !distribute words $p_ into p1,p2
    !if $job_affi=notes
     !if $p2!=$empty
      !distribute item $[$p1/100],$[$p2/10] into x_,y_
      !set p_=$[rint(100*$scoremax*$(f_$j))/100]
     !else
      !set p_=0
     !endif
     <td align=center>$p_</td>
    !else
     !if qualite iswordof $job_affi
      !set p_=$p2
      <td align=center>$p_</td>
     !else
      !set p_=$p1
      <td align=center>$p_ %</td>
     !endif
     !if $p2=$empty
      !set $p_=0
     !endif
    !endif
   !endif
   !set per_$j=$[$(per_$j)+$p_]
   !if $p_>0
    !advance cnt_$j
   !endif
  !next j
 !next i
 !distribute item $[rint(100*$mean_auto/$usercnt)/100],\
	$[rint(100*$mean_tea/$usercnt)/100],\
	$[rint(100*$mean_glob/$usercnt)/100] into m_auto,m_tea,m_glob
 $table_hdtr<td align=center><b>$name_classAverage</b></td>
 !if $job_affi=notes
 !if $manual>0
  <td align=center>$m_glob</td>
  <td align=center>$m_tea</td>
  <td align=center>$m_auto</td>
 !else
  <td align=center>$m_auto</td>
 !endif
 !endif
 !for j in $showsheet
  <td align=center>$[rint(100*$(per_$j)/max(1,$usercnt))/100]</td>
 !next j
 !if $cnt_glob>0
  !distribute item $[rint(100*$mean_auto/max(1,$cnt_auto))/100],\
	$[rint(100*$mean_tea/max(1,$cnt_tea))/100],\
	$[rint(100*$mean_glob/max(1,$cnt_glob))/100] into m_auto,m_tea,m_glob
  $table_hdtr<td align=center><b>$name_posAverage</b></td>
  !if $manual>0
   <td align=center>$m_glob</td>
   <td align=center>$m_tea</td>
   <td align=center>$m_auto</td>
  !else
   <td align=center>$m_auto</td>
  !endif
  !for j in $showsheet
   <td align=center>$[rint(100*$(per_$j)/max(1,$(cnt_$j)))/100]</td>
  !next j
 !endif
 $table_end
<p>$name_click</p>
!if $activecnt>1
  <p>
  !form reply
  <input type=hidden name=job value=sheets>
  !set wims_formselect_switch=multiple size=$[min($activecnt,6)]
  <table border=0>
  <tr><td>$name_pickup:
  </td><td>&nbsp;&nbsp;
  !reset tmp
  !set seq_open=!defof sequence_open in wimshome/log/classes/$wims_class/seq/.def
  !for i=1 to $activecnt
   !set sh=!line $i of $activesheets
   !set se=!item 1 of $sh
   !if $seq_open!=yes
    !set ti=!item 3 to -1 of $sh
    !set ti=!replace internal , by &#44; in $ti
    !set tmp=!append item $se: $ti to $tmp
   !else
    !set ti=!item 4 to -1 of $sh
    !set ti=!replace internal , by &#44; in $ti
    !set seq=!item 3 of $sh
    !set tmp=!append item $seq/$se: $ti to $tmp
   !endif
  !next i
  !set ash=$activesh
  !for i=1 to $eactivecnt
   !set tt=!item $i of $activexams
   !set l=!record $tt of wimshome/log/classes/$wims_class/exams/.exams
   !set l=!line 4 of $l
   !set l=!replace internal , by &#44; in $l
   !set tmp=!append item $name_Exam $tt: $l to $tmp
   !set ash=!append item E$tt to $ash
  !next i
  !formselect showsheet list $ash prompt $tmp
  </td>
  <td><input type=submit value="$wims_name_Show"></td>
  </tr></table>
  </form>
 !endif
<p>
   $name_post
   !if $job_affi=notes
    !href cmd=reply&job_affi=qualite $(name_affi[1])
    ,
    !href cmd=reply&job_affi=points $(name_affi[2])
   !else
    !if $job_affi=qualite
     !href cmd=reply&job_affi=notes $(name_affi[3])
,
     !href cmd=reply&job_affi=points $(name_affi[2])
    !else
     !href cmd=reply&job_affi=notes $(name_affi[3])
,
     !href cmd=reply&job_affi=qualite $(name_affi[1])
    !endif
   !endif
. </p>

!endif (usercnt=0?)

!set wims_menu_items=!append line sendmail,1,module=adm/class/sendmail\
photoboard,1,module=adm/class/photoboard\
addstudent,1,module=adm/class/reguser&step=1\
csv,1,cmd=reply&job=csv\
 ,0,\
formula,1,cmd=reply&job=formula&formula=prep\
grades,1,module=adm/class/grades\
to $wims_menu_items
!if ($wims_supertype<4 and $wims_typename=class) or ($wims_supertype=4 and $wims_typename iswordof course program)
 !set wims_menu_items=!append line livret,1,module=adm/class/livret\
  to $wims_menu_items
!endif

!if $usercnt!=0
 !set wims_menu_items=!append line  ,0,\
 class_stat,1,module=adm/class/stat\
activity,1,module=adm/class/activity\
to $wims_menu_items
!endif

!if $manual>0
<p>$name_manual_explanation</p>
<p><center>
  $(name_manual_notes[1]) = $[$manual/100]* $(name_manual_notes[2]) + $[1-$manual/100]* $(name_manual_notes[3])
 </center> <p>
!endif

:end
!tail

!reset job,showsheet,getuser,getraw,delprep,deluser,formula,abandon,reg,\
	passcheck
