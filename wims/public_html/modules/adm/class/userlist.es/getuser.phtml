<center>$classname<p>

<h3>Detalles del trabajo de
 !href cmd=reply&job=userprop&getuser=$getuser $lastname, $firstname
<br><font size="-1">Identificador de usuario: <tt>$getuser</tt>
!if $user_email!=$empty
 &nbsp;Dirección de correo electrónico: <a href="mailto:$user_email">$user_email</a>
!endif
</font></h3>
  !if $user_photourl!=
   <img src="$user_photourl" width=100 style="position:absolute;top:auto;right:5%;" alt="$firstname $lastname">
  !endif
!if $eremain>0
 <p>
 Este participante tiene una sesión de examen activa:
 !href cmd=reply&job=userexam&getuser=$getuser $esession
!else
 !if $wims_user=supervisor and $wims_supertype=4
  !readproc adm/gateway/transfer $getuser prueba
  !if $transfer_test>0
   !href cmd=reply&job=transfer&getuser=$getuser Transferir sus actividades desde otras clases
  !endif
 !endif
!endif
</center> <p>

!if $manual>0 and $gcnt>0 and $teacher!=$empty
 $table_header
 <caption><small>
 mín/media/máx de la clase:
 $min_glob/$mean_glob/$max_glob</small></caption>
 $table_hdtr<th>Nombre<th>Porcentaje<th>Calidad
 $table_hdtr<td>Notas asignadas por el profesor
     <td align=center>$manual%<td align=center>$mav
 $table_hdtr<td>Notas registradas por el servidor
     <td align=center>$[100-$manual]%<td align=center>$per
 $table_hdtr<td><b>Media global</b> (sobre $scoremax)
     <td align=center>100%<th>$globalav
 $table_end <p>
 $table_header$table_hdtr<th colspan=3>Notas asignadas por el profesor</th>
 $table_hdtr<th>título<th>peso<th>nota</th>
 !set v_=0
 !for i=1 to $gcnt
  !set t_=!item $i+2 of $titles
  $table_tr<td>$t_<td align=center>$(w$i)<td align=center>$(g_$i)</td>
 !next i
 $table_hdtr<th colspan=2>Media<td align=center>$mav</td>
 $table_end <p>
!else
 <center>
 Media global: <b>$per</b> sobre $scoremax.
 !if $min_glob<$scoremax
  <br><small>mín/media/máx de la clase:
  $min_glob/$mean_glob/$max_glob</small>
 !endif
 </center> <p>
!endif

!readdef wimshome/log/classes/$wims_class/.def
!if $eactivecnt>0
 $table_header$table_hdtr<th colspan=4>Notas de exámenes
 !if $wims_user=supervisor or $class_examshow!=no
  </b><small>
  !href cmd=reply&job=examcheck&checkuser=$getuser [detalles]
  </small>
 !endif
 $table_hdtr<th>Número<th>Título<th>Nota/$scoremax
 !for i in $activexams
  !set l=!record $i of wimshome/log/classes/$wims_class/exams/.exams
  !set t=!line 4 of $l
  
  $table_tr<td align=right>$i
  <td align=center>$t
  <td align=center>$[rint(10*$(es_$i)*$scoremax)/100]
 !next i
 $table_end <p>
!endif

!for i=1 to $activecnt
 !set l_=!line $i of $activesheets
 !distribute items $l_ into n_,e_
 !set L_=!line $n_ of $defaultformula
 !distribute word $L_ into A_,B_
 !set t_=!item 3 to -1 of $l_
 !set t_=!char 1 to $title_limit of $t_
 !set v_=!getscorerequire sheet=$n_ user=$getuser
 !set w_=!wordcnt $v_
 !set S_=0
 !for k=1 to $w_
  !set a_=!word $k of $v_
  !set S_=$[$S_+$a_]
 !next k
 !if $S_<=0
  !goto next
 !endif
 $table_header$table_hdtr<th colspan=7>Hoja $i: $t_
 </b><small>peso $A_, severidad $B_<br>
 !set A_=!line $n_ of $percents
 !distribute words $A_ into p1,p2
 !if $p2!=$empty
  !distribute item $[$p1/100],$[$p2/10] into x_,y_
  !set p_=$[rint(100*$scoremax*$(f_$i))/100]
  $p1% terminado, calidad $p2/10, equiv. <b>$p_</b> sobre $scoremax.
  !if $(min_$i)<$scoremax
   <br>mín/media/máx de la clase: $(min_$i)/$(mean_$i)/$(max_$i)
  !endif
 !endif
 </small>
 $table_hdtr<th>Nº<th>título<th>peso<br>requerido<th>peso
     <th>puntos<br>obtenidos<th>%<th>calidad
 !for j=1 to $e_
  $table_tr<td align=right>$j
  <td>$(title_$(n_)_$j)
  <td align=right>
  !set v_=$(req_$(n_)_$j)
  $v_
  <td align=center>$(weight_$(n_)_$j)
  <td align=right>
  !set d_=!getscore user=$getuser sheet=$n_ work=$j
  !default d_=0
  $d_
  <td align=right>
  !if $v_>0
   $[round(100*$d_/$v_)]%
  !else
   ---
  !endif
  <td align=right>
  !set d_=!getscoremean user=$getuser sheet=$n_ work=$j
  !default d_=0
  $d_
 !next j
 $table_end <p>
 :next
!next i
<center>
!if $exologs!=$empty
 <p>
 !href module=adm/class/exolog&cmd=new&job=list&checkuser=$getuser Lista de los ejercicios grabados
!endif
<p>
!if $wims_user=supervisor
 !!!href cmd=reply&job=getraw&getraw=$getuser Datos en bruto
!!.
!! !href cmd=resume Lista de todos los participantes
!!.
!! <p>
!! !href cmd=reply&job=delprep&delprep=$getuser Dar de baja a este participante
!!, o
!! !href module=adm/class/passwd.fr&job=part&part=$getuser cambiar su contraseña
!!.
!set wims_menu_items=!append line rawdata,1,cmd=reply&job=getraw&getraw=$getuser\
 delprep,1,cmd=reply&job=delprep&delprep=$getuser\
passwd,1,module=adm/class/passwd.fr&job=part&part=$getuser\
part_list,1,cmd=resume\
to $wims_menu_items
!else
 !href module=home Página principal de la clase
!endif
</center> <p>

