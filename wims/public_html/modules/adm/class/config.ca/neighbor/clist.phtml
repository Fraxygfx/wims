
$table_hdtr<th rowspan=2>Nom de classe
<th colspan=2>Déclaration<br>voisinage</b><small>
!href cmd=help&special_parm=neibordecl aide
</small>
<th rowspan=2>Partage de ressource</b><small>
!href cmd=help&special_parm=share aide
</small>
<th rowspan=2>Enseignant
<th rowspan=2>Langue
$table_hdtr<td align=center><small>par vous</small>
<td align=center><small>par l'autre</small>
!for i in $clst
 !set D_=!defof class_institution\
class_description\
class_lang\
class_supervisor in wimshome/log/classes/$i/.def
 !distribute lines $D_ into i_,d_,l_,s_
 !set t_=0
 !if $i isitemof $n_sup
  !advance t_
 !endif
 !if $i isitemof $n_part
  !set t_=$[$t_+2]
 !endif
 $table_tr<td align=center>
 !href cmd=reply&job2=def&nei=$i&nlevel=$t_ $i_<br>$d_
 <td align=center>$(n_n$t_)
 !set t_=0
 !set D_=!defof n_sup\
n_part\
sharable_all\
sharing_all in wimshome/log/classes/$i/neighbors
 !distribute line $D_ into sup,part,nsharable,nsharing
 !if $wims_class isitemof $sup or all isitemof $sup
  !advance t_
 !endif
 !if $wims_class isitemof $part or all isitemof $part
  !set t_=$[$t_+2]
 !endif
 <td align=center>$(n_n$t_)
 !if $i notitemof $sharable_all,$sharing_all and\
	$wims_class notitemof $nsharable,$nsharing
  <td align=center>---
 !else
  <td>
  !if $i isitemof $sharing_all or $wims_class isitemof $nsharing
   !if $i isitemof $sharing_all and $wims_class isitemof $nsharing
    Partages mutuels actifs.
   !else
    !if $i isitemof $sharing_all
     Vous partagez ses ressources.
    !else
     Partage vos ressources.
    !endif
   !endif
  !else
   !if $i isitemof $sharable_all and $wims_class isitemof $nsharable
    Permissions mutuelles de partage.
   !else
    !if $i isitemof $sharable_all
     A le droit de partager vos ressources.
    !else
     Vous permet de partager ses ressources.
    !endif
   !endif
  !endif
 !endif
 <td align=center><small>$s_</small>
 <td align=center>$l_
!next i

