!if _filter_ isitemof $listuser
  filter=yes
  listuser=!listcomplement _filter_ in $listuser
  !if $varfilter!=$empty
    !reset varfilter_
    !set varfilter_cnt=!linecnt $varfilter
    !for v=1 to $varfilter_cnt
      var_line=!singlespace $(varfilter[$v;])
      var_line =!replace $ $ by , in $var_line
      varfilter_=!append line $var_line to $varfilter_
    !next
  !endif
!endif

!if $user!=$empty
 !read adm/class/userdef classes,$wims_class,$user
 !defread $userdef
 !read adm/class/userisinclass $wims_class,$user
 !if $script_reply!=yes
  error=bad_user
  !reset job
  !exit
 !endif
 !reset listiduser,listnameuser
 !for k=1 to $nbuser
  l=!record $k of wimshome/log/classes/$wims_class/.userlist
  !distribute item $l into f,l,id
  !if $id!=$user
   listiduser=!append item $id to $listiduser
   n=$f $l
   listnameuser=!append item $n to $listnameuser
  !endif
  !if $filter=yes
    !reset var_filter_test
    !read adm/class/testfilter $id\
$varfilter_
   !if $var_filter_test=1
     listuser=!append item $id to $listuser
   !endif
  !endif
 !next k
 listuser=!listintersect $listuser and $listiduser
!endif

!if $user!=$empty
 file=motd/$user
!else
 file=.motd
!endif

!if $action=register
 !writefile wimshome/log/classes/$wims_class/$file $motd
 !if $user isin $file
  !writefile wimshome/log/classes/$wims_class/motd/.$user
 !endif
 !if $listuser!=$empty and $user!=$empty
  !for id in $listuser
   !writefile wimshome/log/classes/$wims_class/motd/$id $motd
   !writefile wimshome/log/classes/$wims_class/motd/.$id
  !next id
 !endif
 !reset job,user,action,listuser
!endif

motd=!record 0 of wimshome/log/classes/$wims_class/$file
