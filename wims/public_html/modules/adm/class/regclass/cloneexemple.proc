sjob=$job
cltype=0
!if $step=$empty
    !!test de la classe d'exemple à copier
    tmp=!defof class_defined\
class_isexemple in wimshome/log/classes/$sourcecls/.def
    !distribute line $tmp into tmp1,tmp2
    !if yes!=$tmp1
	error=classdontexist
	!exit
    !endif
    !if yes!=$tmp2
	error=notexempleclass
	!exit
    !endif
    ssourcecls=$sourcecls
!endif

source_title=!defof class_institution,class_description in wimshome/log/classes/$ssourcecls/.def

!if $step=0
    !if $methodecp!=0 and $methodecp!=1
	error=badcpmethod
	!exit
    !endif
    smethodecp=$methodecp
!endif

!read proc/newclass.proc

!! ajout des ressources de la classe d'exemple
!if $step=3
!! copy of ressources of class
    !sh cp -r $wims_home/log/classes/$ssourcecls/sheets $wims_home/log/classes/$code/;\
cp -r $wims_home/log/classes/$ssourcecls/doc $wims_home/log/classes//$code/;\
cp -r $wims_home/log/classes/$ssourcecls/livret $wims_home/log/classes/$code/;\
cp -r $wims_home/log/classes/$ssourcecls/seq $wims_home/log/classes/$code/;
    date=!defof class_expiration in wimshome/log/classes/$code/.def
    !if $smethodecp=0
	!sh cp -r $wims_home/log/classes/$ssourcecls/exams $wims_home/log/classes/$code/;\
cp $wims_home/log/classes/$ssourcecls/.E* $wims_home/log/classes/$code/;
	nb=!recordcnt wimshome/log/classes/$code/exams/.exams
	!if $nb>0
	    save=$empty
	    !for k from 1 to $nb
		t=!record $k of wimshome/log/classes/$code/exams/.exams
		t=!replace line number 2 in $t by $date
		save=!append line :$t to $save
	    !next k
	    !writefile $wims_home/log/classes/$code/exams/.exams $save
	!endif
    !else
	newstatut=0
    !endif
    nb=!recordcnt wimshome/log/classes/$code/sheets/.sheets
    !if $nb>0
	save=$empty
	!for k from 1 to $nb
	    t=!record $k of wimshome/log/classes/$code/sheets/.sheets
	    t=!replace line number 2 in $t by $date
	    !if $smethodecp=1
		t=!replace line number 1 in $t by 0
	    !endif
	    save=!append line :$t to $save
	!next k
	!writefile wimshome/log/classes/$code/sheets/.sheets $save
	!! suppression des entrées exam dans les sequences.
	seq=!record 0 of wimshome/log/classes/$code/seq/.sequences
	nb=!line 1 of $seq
	!if $nb!=$empty and $nb>0
	    newseq=$nb
	    !for k from 1 to $nb
		old=!line $k+1 of $seq
		nbit=!itemcnt $old
		!if $nbit>0
		    newlist=$empty
		    !for j from 1 to $nbit
			!if exam notin $(old[$j])
			    newlist=!append item $(old[$j]) to $newlist
			!endif
		    !next j
		!endif
		newseq=!append line $newlist to $newseq
		tmp=!record 1 of wimshome/log/classes/$code/seq/.sequence$k
		add=!item 2 to -1 of $newlist
		tmp=!replace line number 3 in $tmp by $add
		!writefile wimshome/log/classes/$code/seq/.sequence$k :$tmp
	    !next k
	    !writefile wimshome/log/classes/$code/seq/.sequences $newseq
	!endif
    !endif
!endif