
!read adm/css.phtml
!read tabletheme
!header1

<!--!read adm/title.phtml 3\-->
<div class="titre_recherche">
	<h1 class="wims_title">
		<!--WIMS $httpd_HTTP_HOST\-->
		$title
	</h1>
</div>


!if $toomany=yes
 !read lang/toomany.phtml.$modu_lang
 !goto end
!endif

!if $error!=$empty
 <p>
 !read lang/msg.phtml.$modu_lang
 </p>
 !if $restart!=no
  <p>
  !href module=$module&cmd=reply&step=0 $name_again
  </p>
 !endif
 !goto end
!endif

!if $step=$empty or $step=pre

!!read local.phtml
<div class="contenu">
 $name_choose 
 <p>
 !href cmd=reply&step=0&cltype=0 $wims_name_create_class
 </p>
 !set wims_menu_items=!append line create_class,1,cmd=reply&step=0&cltype=0\
 to $wims_menu_items
 <p>
 !if 2 isitemof $alltypes
  !href cmd=reply&step=0&cltype=2 $wims_name_create_superclass
  !set wims_menu_items=!append line create_superclass,1,cmd=reply&step=0&cltype=2\
 to $wims_menu_items
 !else
  <font color="blue"><strike>$wims_name_create_superclass</strike></font> ($noright).
 !endif
  </p>
   <p>
 !if 4 isitemof $alltypes
  !href cmd=reply&step=0&cltype=4 $wims_name_create_portal
  ($name_expert)
  !set wims_menu_items=!append line create_portal,1,cmd=reply&step=0&cltype=4\
 to $wims_menu_items
 !else
  <font color="blue"><strike>$wims_name_create_portal</strike></font> ($noright).
 !endif
 </p>
  
 <span class="wims_warning">$wims_name_help</span>:
 !read lang/help.phtml.$modu_lang 1
 </div>
 !goto end
  
!endif

!if $step=0
 !read lang/help.phtml.$modu_lang 2

 !form reply
 <input type="hidden" name="step" value="1"/>
<div class="wimsform">
 $table_header
 $table_tr
  <td><label for="institution">$name_Name_portal</label></td>
  <td>
  !if $Cltype isin 13
   !let institution=$wims_institutionname
   $institution
  !else
   <input size="$instlim" name="institution" value="$institution" id="institution" maxlength="$instlim"/>
  !endif
  </td>
  <td>($name_atmost $instlim $name_characters)</td>
 </tr>
 $table_tr<td><label for="description">$wims_name_name $name_classesss</label></td><td>
  <input size="$desclim" name="description" value="$description" id="description" maxlength="$desclim"/>
  </td><td>
  ($name_atmost $desclim $name_characters)
 </td></tr>
 !if $Cltype notin 24
 $table_tr<td><label for="level">$wims_name_level $name_classesss</label></td><td>
  <select name="level" id="level">
   !for k=1 to $nblevel
    !if $(levelid[$k])=$level
     <option selected value="$(levelid[$k])">$(leveldesc[$k])</option>
    !else
     <option value="$(levelid[$k])">$(leveldesc[$k])</option>
    !endif
   !next k
  </select>
 </td><td></td></tr>
 !endif
!set suplim2 =$[rint($suplim/3)]
!set emaillim2= $[rint(2*$emaillim/3)]
 $table_tr<td>$wims_name_Supervisor</td><td>
 !if $Cltype=1
  !let firstname=$wims_firstname
  !let lastname=$wims_lastname
  $wims_firstname $wims_lastname
 !else
  <table class="wimsnoborder"><tr><td><label for="firstname">$wims_name_firstname</label></td><td>
  <input size="$suplim2" name="firstname" value="$firstname" id="firstname"/>
   </td></tr><tr><td><label for="lastname">$wims_name_lastname</label></td><td>
   <input size="$suplim2" name="lastname" value="$lastname" id="lastname" maxlength="$suplim2"/>
  </td></tr></table>
 !endif
 </td><td></td></tr>
 $table_tr<td><label for="email">$wims_name_email</label></td><td>
  !if $Cltype=1 and $email!=$empty
   !default email=$email
   $email
  !else
   <input size="$emaillim2" name="email" value="$email" id="email" maxlength="$emaillim2"/>
  !endif
 </td><td>
 !if $regpolicy=email
   <br/>($name_regpolicy)
  !endif
 </td></tr>
 !if $Cltype!=1
  $table_tr<td><label for="passsup">$wims_name_Password ($name_sup)</label></td><td>
   <input size="16" type="password" name="passsup" id="passsup" maxlength="16"/>
   </td><td>
   <small>
   ($name_warningpass).
   $name_help1</small>
  </td></tr>
 !endif
 
 $table_tr<td><label for="password">$wims_name_Password ($name_classesss)</label></td><td>
  <input size="16" type="password" name="password" id="password" maxlength="16"/>
  </td><td>
  <small>($name_warningpass).
  !if $Cltype<2
  $name_help2
  !else
  $name_help3
 !endif
 </small>
 </td></tr>
 
 $table_tr<td><label for="exp_day">$wims_name_expiration</label></td><td>
 <select name="exp_day" id="exp_day">
  !for i=1 to 31
   <option value="$i"
   !ifval $i=$exp_day
    selected
   !endif
   >$i
   </option>
  !next i
  </select> <select name="exp_month" id="exp_month">
  !for i=1 to 12
   <option value="$i"
   !ifval $i=$exp_month
    selected
   !endif
   >
   !item $i of $months
   </option>
  !next i
  </select> <select name="exp_year" id="exp_year">
  !for i=$thisyear to $thisyear+1
   <option value="$i"
   !ifval $i=$exp_year
    selected
   !endif
   >$i
   </option>
  !next i
  </select> 
  </td><td><small>$name_help_date</small>
 </td></tr>

 $table_tr<td><label for="limit">$name_help_limit</label></td><td>
  !let num=!charcnt $wims_class_user_limit
  <input size="3" name="limit" value="$limit" id="limit" maxlength="$num"/>
 </td><td>
 <small>(< $wims_class_user_limit.) </small>
 </td></tr>
 
 $table_tr<td><label for="secure">$name_secure ($name_sup)</label></td><td>
  !!!default secure=all
  <input size="30" name="secure" value="$secure" id="secure"/>
  !reset secure
 </td><td>
 <small>$name_host
  !!!if $Cltype<3
  !! $name_optional.
  !!!endif 
  </small>
 </td></tr>
 !if $cltype isin 024
$table_tr<td><label for="cas_auth">$name_cas ($name_optional)</label></td><td>
  <input size="30" name="cas_auth" value="$cas_auth" id="cas_auth"/>
  </td><td>
 <small>$name_cas_help</small>
 </td></tr>
 !endif
$table_tr<td colspan="3" align="center">
 <input type="submit" value="$name_continue"/>
 </td></tr>
 $table_end
</div>
 </form>

 !goto end
!endif

!if $step=1
!read lang/help.phtml.$modu_lang step1

 !form reply
 <input type="hidden" name="step" value="2"/>
<div class="wimsform">
 !if $Cltype!=1
  <label for="passsup"> $wims_name_Password ($name_sup)</label> 
  <input type="password" name="passsup" id="passsup" maxlength="16"/>
 !endif
 !if $pword!=$empty
 <label for="password">$wims_name_Password ($name_classesss)</label>
  <input type="password" name="password" id="password" maxlength="16"/> <p>
 !endif
 <p class="wimscenter"><input type="submit" value="$name_continue"/></p>
 </div>
 </form>
 !goto end1
!endif

!if $step=2
 !read lang/help.phtml.$modu_lang step2
 !form reply
<div class="wimsform">
 <input type="hidden" name="step" value="3"/>
 <p class="wimscenter"><label for="typecode">$name_code</label> <input size="12" name="typecode" id="typecode"/>
 <p><input type="submit" value="$name_continue"/></p>
 </div>
 </form>
 <span class="wims_warning">$wims_name_warning</span>: $name_warning_code
 !goto end1
!endif

!if $step=3
 !read lang/help.phtml.$modu_lang step3
 !if $Cltype iswordof 0 1
  !let tmp=!positionof item $ilevel in $levelid
  ($wims_name_level <font color="green">$(leveldesc[$tmp])</font>).
 !endif
 !read adm/class/initclass $code,auth
 !if $(class_authidp[1;])=cas 
   !set link_=$host_auth/login?service=$wims_ref_name?
 !else
   !set link_=http://$httpd_HTTP_HOST$httpd_SCRIPT_NAME?
 !endif
 <p>
 !read adm/class/links.phtml $code
 !goto end
!endif

:end1

:end
!if $step>=0 and $cmd=reply
 !set wims_menu_items=!append line back2,1,module=$module\
 to $wims_menu_items
!endif

!tail

!set step=


