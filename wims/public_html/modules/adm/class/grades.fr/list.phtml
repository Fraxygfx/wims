!if $gcnt<=0
 Il n'y a aucune note attribuée manuellement dans votre classe.
 !href cmd=reply&method=column&cc=1 En ajouter une colonne
.
 !exit
!endif

$table_header
$table_hdtr<th>Nom<th><small>Moyenne</small>
!for g=1 to $gcnt
 <th><small>
 !set t_=!item $g of $titles
 !if $ucnt<=$maxuser
  !href cmd=reply&method=column&cc=$g $t_
 !else
  $t_
 !endif
 </small>
!next g
$table_hdtr<th>Poids<td align=center>--</th>
!for g=1 to $gcnt
 !set w_=!item $g of $weights
 !default w_=1
 <td align=center>$w_
!next g
!for u=1 to $ucnt
 !set n_=!item $u of $ulist
 !set f_=!item $u of $flist
 !set l_=!item $u of $llist
 $table_tr<td align=center>
 !href cmd=reply&method=user&un=$n_ $l_, $f_
 !set t_=!positionof item $n_ in $uulist
 !set v_=!record $t_+1 of wimshome/log/classes/$wims_class/.grades
 !set a_=0
 !for g=1 to $gcnt
  !set G$g=!item $g+2 of $v_
  !default G$g=0
  !set w_=!item $g of $weights
  !set a_=$[$a_+($w_)*$(G$g)]
 !next g
 !set a_=$[rint(100*$a_/$wtot)/100]
 <td align=center>$a_
 !for g=1 to $gcnt
  <td align=center>$(G$g)
 !next g
!next u
$table_end

<p>Cliquez sur le nom d'un colonne ou un participant pour
!if $gcnt<$maxcol
 modifier les notes, ou
 !href cmd=reply&method=column&cc=$[$gcnt+1] ajouter une autre colonne
.
!else
 modifier les notes.
!endif
!if $gcnt>0
<p>
Vous pouvez aussi 
!href cmd=reply&method=raw modifier le source
 des notes directement, ou
 !href module=adm/class/userlist&cmd=new&job=csv communiquer les notes
 avec votre tableur.
!endif

