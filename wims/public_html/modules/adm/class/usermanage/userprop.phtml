!read adm/title.phtml 1\
$classname\
$wims_name_account_property

!if $error!=$empty
 <div class="wims_msg alert">
 !read lang/msg.phtml.$moduclass_lang
 </div>
!endif

!read adm/class/initclass $wims_class
!set inplen=40

!! ----- permet de transferer les scores d'un user provenant d'une classe en partage dans un groupement ou entre des cours d'un même portail
!if $wims_user=supervisor and $wims_supertype iswordof 2 4
  !readproc adm/gateway/transfer $getuser test
  !if $transfer_test>0
   !href module=adm/class/usermanage&job=transfer&getuser=$getuser $name_transfer
  !endif
!endif

!form reply
<input type="hidden" name="job" value="userprop" />
<input type="hidden" name="getuser" value="$getuser" />
<fieldset class="property_fields halfwidth blockcenter">
 <div class="field box">
   $wims_name_Login
   !reset css_connected
!if $getuser isitemof $wims_connectedlogin and $wims_user=supervisor
     !set css_connected=class="tt wims_connected"
     !set wims_ref_title=connected
   !else
     !set css_connected=class="tt"
!endif
    <span $css_connected title="connected">$getuser</span>
 </div>
!if $type_class iswordof 2 4 or ($class_Supervisor=$wims_realuser and $wims_class!=$wims_superclass and $wims_realuser!=$getuser)
  !if $user_participate!=$empty and $type_class iswordof 2 4
    <div class="field box">
     $name_inscript
     <span class="tt">$user_participate</span>
    </div>
  !endif
  !if $user_supervisable=yes
    <div class="field box">
    !if $type_class iswordof 2 4
      !if $wims_user=supervisor
      <label for="up_supervise">$name_gestion</label>
      <input size="$inplen" name="up_supervise" id="up_supervise" value="$user_supervise" />
      <div class="formHelp"><span class="tt">$wims_class/1, ... </span></div>
      !else
      $name_gestion
      <input readonly="readonly" size="$inplen" name="up_supervise" id="up_supervise" value="$user_supervise" />
      !endif
    !else
      !if $wims_class isitemof $user_supervise
        !set superv=yes
      !else
        !set superv=no
      !endif
      <label for="superv">$name_gestion</label>
!set wims_ref_class=wims_formradio
     !formradio superv list yes,no prompt $wims_name_yes, $wims_name_no
     !set up_firstname=$user_firstname
     !set up_lastname=$user_lastname
    !endif
    </div>
  !endif
!endif
!if $user_supervisable=yes and $getuser!=$wims_realuser and $type_class notwordof 2 4
 !goto end
 !exit
!endif
!if $class_auth=$empty or $wims_user=supervisor
  <div class="field box">
    <label>$wims_name_Password</label>
    !href module=adm/class/passwd&job=part&part=$getuser $name_change
 </div>
!endif

<div class="field box">
  <label for="up_lastname">$wims_name_lastname</label>
  <input size="$inplen" name="up_lastname" id="up_lastname" value="$user_lastname" />
</div>
<div class="field box">
  <label for="up_firstname">$wims_name_firstname</label>
  <input size="$inplen" name="up_firstname" id="up_firstname" value="$user_firstname" />
</div>
<div class="field box">
  <label for="up_email">$wims_name_email</label>
  <input size="$inplen" name="up_email" id="up_email" value="$user_email" />
</div>
<div class="field box">
  <label for="up_regnum">$name_regnum</label>
  <input size="$inplen" name="up_regnum" id="up_regnum" value="$user_regnum" />
</div>
<div class="field box">
  <label for="up_photourl">$name_photourl</label>
  <input size="$inplen" name="up_photourl" id="up_photourl" value="$user_photourl" />
 </div>
!if $wims_user=supervisor
 <div class="field box">
    <label for="up_comments">$name_comments</label>
    !readproc adm/class/mkcomment val
    <input size="$inplen" name="up_comments" id="up_comments" value="$scriptout" />
  </div>
  <div class="field box">
    <label for="up_vars">$name_vars</label>
    !readproc adm/vfilter/listvarfilter.proc
    !readproc adm/vfilter/uservarfilter.proc $userdef
    !let nb=!linecnt $user_vars
    !set wims_ref_class=wims_button
    !href module=adm/class/techvar&+job=userprop&+user=$getuser $wims_name_change
    !if $nb>0
      $table_header
      !for k=1 to $nb
      $table_tr
        <td>$(user_vars[$k;1])</td><td>$(user_vars[$k;2])</td>
      </tr>
    !next k
    $table_end
    !endif
!!    <textarea class="nowrap" cols="$[$inplen-1]" rows="4" name="up_vars" id="up_vars">$user_vars</textarea>
  </div>
  <div class="field box">
    <label for="up_external_auth">$name_external_auth</label>
    <input size="$inplen" name="up_external_auth" id="up_external_auth" value="$user_external_auth" />
    !set wims_ref_class=wims_button_help float_right
!href cmd=help&special_parm=authentification $wims_name_help
 </div>
!else
 <div class="field box">
   <label for="passcheck">$name_classpassword</label>
   <input size="14" type="password" name="passcheck" id="passcheck" />
   <div class="formHelp wims_alertmsg">
       $name_userprop_warning: $name_enterpassword
   </div>
 </div>
!endif
:end

<div class="wimscenter wimsform">
 <input type="submit" name="reg" value="$wims_name_tosave" />

 <input type="submit" name="abandon" value="$wims_name_giveup" class="wims_secondary_button" />
</div>
</fieldset>
!formend
!if $wims_user=supervisor
 !if ($wims_supertype=4 and $wims_typename!=course) or ($wims_supertype=2 and $wims_typename=group)
  !! no score
 !else
  !set wims_menu_items=!append line itemsep,0,$wims_name_Evaluation\
scores,1,module=adm/class/userscore&job=getuser&getuser=$getuser\
livret1,1,module=adm/class/livret&job=user&user=$getuser\
activity1,1,module=adm/class/activity&job=user&user=$getuser\
to $wims_menu_items
 !endif
 !set wims_menu_items=!append line \
   itemsep,0,$wims_name_usermanage\
  delstudent1,1,cmd=reply&job=delprep&delprep=$getuser\
to $wims_menu_items
 !read getparm $module_init_parm job
 !if $get_job!=teacher
    !read adm/class/getnextuser $wims_class,$getuser,userprop,getuser
    !set wims_menu_items=!append line addstudent,1,module=adm/class/reguser&step=1\
    itemsep,0,\
to $wims_menu_items
    !if $wims_theme=default
!!ARIANE
      !set wims_menu_items=!append line part_list,1,cmd=resume\
to $wims_menu_items
    !endif
 !else
   !set wims_menu_items=!append line itemsep,0,\
teacherlist,1,module=adm/class/usermanage&job=teacher\
to $wims_menu_items
   !if $wims_user=supervisor and $wims_realuser=supervisor
     !set wims_menu_items=!append line addteacher,1, module=adm/class/reguser&step=1&utype=1\
to $wims_menu_items
   !endif
   !set wims_menu_items=!append line itemsep,0,$wims_name_Evaluation to $wims_menu_items
  !endif
!else
  !if $wims_supertype=4 and $wims_typename!=course
   !set wims_menu_items=!append line myscore,1,module=adm/class/userscore&job=getuser&getuser=$getuser\
to $wims_menu_items
  !endif
  !if $wims_theme=default
!!ARIANE
    !set wims_menu_items=!append line class_home,1,module=home\
to $wims_menu_items
  !endif
!endif
!reset reg,abandon
