# Variable calculations should be done in this file.
# It is read and interpreted by wims for all valid calls to the module.
!read adm/class/classlang names.phtml

max_sheet=64
title_limit=80
desc_limit=4000
etitle_limit=100
edesc_limit=400
wims_prefix=class user tmp n sharing sharable
srcname=wimshome/$wims_sesdir/home_sheet.src
wims_helper=chapter=3

!readproc tabletheme
!if $wims_user!=supervisor
 error=not_supervisor
 !exit
!endif
!if $job=chseries
!! num=!replace internal . by , in $num
!! !distribute item $num into sheet,modif
 !changeto chseries.proc
!endif
!readproc adm/search_engine/names.$lang
!readproc adm/search_engine/mklist_form.proc

today=!char 1 to 8 of $wims_now
thisyear=!char 1 to 4 of $today

!if $class_expiration=$empty 
 !defread wimshome/log/classes/$wims_class/.def
 !if $wims_superclass!=$empty
  class_expiration=!defof class_expiration in wimshome/log/classes/$wims_superclass/.def
 !endif
!endif

!if $class_expiration=$empty
 error=bad_class
 !exit
!endif

sharers=
sharing1=!defof sharable_sheet in wimshome/log/classes/$wims_class/neighbors
!for c in $sharing1
 d=!defof sharing_sheet in wimshome/log/classes/$c/neighbors
 !if $d issametext $wims_class
  sharers=!append item $c to $sharers
 !endif
!next c
expmon=!char 2,3 of $[abs(floor($expmon))+100]
expday=!char 2,3 of $[abs(floor($expday))+100]
expyear=!char -4 to -1 of $[abs(floor($expyear))+100000]
!if $expday>=1 and $expday<=31 and $expmon>=1 and $expmon<=12 \
	and $expyear>=$thisyear and $expyear<=$thisyear+1 \
	and $expyear$expmon$expday>$today \
	and $expyear$expmon$expday<=$class_expiration
	and N notin $expmon$expday$expyear
 expiration=$expyear$expmon$expday
 !else 
   !if $expyear$expmon$expday>$class_expiration
     !readproc adm/date.phtml $class_expiration
     error=toolate
   !endif
!endif

!if $sheet>$max_sheet
 error=bad_maxsheet
 !exit
!endif

!if $sheet<1
 error=bad_sheet
 !exit
!endif
# menu preparation
!if $job!=$empty and $job iswordof \
	prep_activate prep_erase prep_expire prep_putsource prep_modify
 data=!record $sheet of wimshome/log/classes/$wims_class/sheets/.sheets
 !distribute lines $data into active,expire,title,desc,,comment
 error=$job
 !exit
!endif

scnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheets
!if $job=erase and $sheet<=$scnt and $sheet>=1
 test=
 !for s=$sheet to $scnt
  l=!record $s of wimshome/log/classes/$wims_class/sheets/.sheets
  l=!line 1 of $l
  !if $l>0
   test=notclean
  !endif
 !next s
 !if $test=$empty
  update_field=0
  !read update.sheet
 !endif
 !restart module=home
!endif

!if $job=expire
 !distribute item 1,2 into update_field,update_content
 !read update.sheet
 job=
!endif

!if $job=hide
 !distribute item 1,3 into update_field,update_content
 !read update.sheet
 job=
!endif

!if $job=reactivate
 !distribute item 1,1 into update_field,update_content
 !read update.sheet
 job=
!endif

!if $job=deactivate
 !read var.proc.deactivate
!else
 confirmed=!replace word share by $ in $confirmed
 confirmed=!singlespace $confirmed
!endif

activetest=!record $sheet of wimshome/log/classes/$wims_class/sheets/.sheets
a_=!line 1 of $activetest
a_=!trim $a_
expp=!line 2 of $activetest
!default expiration=$expp
!if $expiration>$class_expiration
 expiration=$class_expiration
!endif
ecnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$sheet
!setdef wims_sheetmax=$ecnt in wimshome/sessions/$wims_session/var.stat
activetest=!replace line number 3 by $ecnt in $activetest

!if $job=putsource and $a_=0
 source=!trim $source
 !writefile $srcname $source
 putcnt=!recordcnt $srcname
 !for i=1 to $putcnt
  s_=!record $i of $srcname
  n_=!linecnt $s_
  bad_source=$i
  !if $n_>=5 and $n_<10
   !distribute lines $s_ into di,pa,re,we,ti,de
   !for k in re,we,di,pa,ti,de
    $k=!trim $($k)
    w$k=!wordcnt $($k)
   !next k
   !if NaN isin $[$re]$[$we] or $[$wre*$wwe*$wdi]!=1 or $wpa>1 or $ti=$empty or\
	.. isin $di or $re<0 or $we<0 or ($re>0 and $we=0)
    error=bad_source
    !exit
   !endif
   ti=!char 1 to $etitle_limit of $ti
   de=!char 1 to $edesc_limit of $de
   source_$i=$s_
   last=$i
  !else
   # last record may be empty
   !if $i!=$putcnt
    error=bad_source
    !exit
   !endif
  !endif
 !next i
 !for i=1 to $last
  !appendfile wimshome/log/classes/$wims_class/sheets/.sheet$sheet :$(source_$i)
 !next i
!endif

!if $activetest!=$empty
 sheet_exist=yes
 !default expiration=!line 2 of $activetest
 activetest=!line 1 of $activetest
 exocnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$sheet
!else
 sheet_exist=no
 activetest=-1
 exocnt=0
 !default expiration=$class_expiration
!endif

!if $job=getsource and $sheet_exist=yes
 !writefile $srcname <pre>
 !for i=1 to $exocnt
  l=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sheet
  !appendfile $srcname $$:$l
 !next i
 !appendfile $srcname </pre>
 !exit
!endif

!if $allow=yes
 !bound allowtype within all,none,select default all
 IPlist=!translate ,;\
* to $    $ in $IPlist
 IPlist=!trim $IPlist
 !if $allowtype=select and $IPlist=$empty
  allowtype=all
 !endif
 !if $allowtype=all
  allow_parm=
 !endif
 !if $allowtype=none
  allow_parm=none
 !endif
 !if $allowtype=select
  allow_parm=$IPlist 
 !endif
 !if $sharers!=$empty and $allowshare=1
  setclass=!items2words $wims_class,$sharers
 !else
  setclass=$wims_class
 !endif
 !if $allow_parm=$empty
  !sh for c in $setclass; do rm -f $wims_home/log/classes/$$c/.$sheet; done
 !else
  !sh for c in $setclass; do echo "$allow_parm" >$wims_home/log/classes/$$c/.$sheet; done
 !endif
 wims_class_log=sheet $sheet allow $allowtype $allowshare
!endif

allow=
allowlist=!record 0 of wimshome/log/classes/$wims_class/.$sheet
allowlist=!trim $allowlist
!if $allowlist=$empty
 allowtype=all
!else
 !if none iswordof $allowlist
  allowtype=none
 !else
  allowtype=select
  IPlist=$allowlist
 !endif
!endif

expmon=!char 5,6 of $expiration
expday=!char 7,8 of $expiration
expyear=!char 1,2,3,4 of $expiration

!if $job!=text
 !if $title_save$desc_save=$empty
  rec=!record $sheet of wimshome/log/classes/$wims_class/sheets/.sheets
  !distribute lines $rec into bidon,bidon,title_save,desc_save,prpage_save,,comment_save
 !endif
 title=$title_save
 desc=$desc_save
 prpage=$prpage_save
 comment=$comment_save
!else
 !if $title=$empty
  error=no_title
  !exit
 !endif
 desc=!replace $\
$ by &#13; in $desc
 title=!singlespace $title
 desc=!singlespace $desc
 title=!char 1 to $title_limit of $title
 desc=!char 1 to $desc_limit of $desc
 title=!replace internal : by &#58; in $title
 desc=!replace internal : by &#58; in $desc
 prpage=!word 1 of $prpage
 prpage=!char 1 to 100 of $prpage
 !if .. isin $prpage or / notin $prpage
  prpage=
 !endif
 !if $prpage!=$empty
  test=!defof titb in wimshome/log/classes/$wims_class/doc/$prpage.def
  !if $test=$empty
   prpage=
  !endif
 !endif
 !if $sheet_exist=yes
  !distribute lines 2\
$expiration into update_field,update_content
  !read update.sheet
  !distribute lines 3\
$title into update_field,update_content
  !read update.sheet
  !distribute lines 4\
$desc into update_field,update_content
  !read update.sheet
  !distribute lines 5\
$prpage into update_field,update_content
  !read update.sheet
  !distribute lines 6\
$comment into update_field,update_content
  !read update.sheet
 !else
  !appendfile wimshome/log/classes/$wims_class/sheets/.sheets :0\
$expiration\
$title\
$desc\
$prpage\
$comment
  sheet_exist=yes
  activetest=0
  sheet=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheets
  !writefile wimshome/log/classes/$wims_class/sheets/.sheet$sheet
 !endif
 title_save=$title
 desc_save=$desc
 prpage_save=$prpage
!endif

!if $job=activate and $[$activetest]=0
 !read writeweights
 !distribute item 1,1 into update_field,update_content
 !read update.sheet
 activetest=1
 wims_class_log=activate sheet $sheet
!endif

# delete an item
!if $job=delete and $delete>0 and $delete<=$exocnt
 !distribute items -1,$delete into update_field,exo
 !read update.exo
 delete=
 exocnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$sheet
!endif

# move up an item
!if $job=moveup and $moveup>1 and $moveup<=$exocnt
 x_=!record $moveup-1 of wimshome/log/classes/$wims_class/sheets/.sheet$sheet
 y_=!record $moveup   of wimshome/log/classes/$wims_class/sheets/.sheet$sheet
 !distribute items 0,$[$moveup-1] into update_field,exo
 update_content=$y_
 !read update.exo
 !distribute items 0,$moveup into update_field,exo
 update_content=$x_
 !read update.exo
!endif

# modify an item
!if $job=modify and $modif>0 and $modif<=$exocnt
 exo=!record $modif of wimshome/log/classes/$wims_class/sheets/.sheet$sheet
 !distribute lines $exo into D_,p_
 !bound mod_weight between 0,10 default 0
 !bound mod_require between 0,1000 default 0
 mod_title=!singlespace $mod_title
 mod_desc=!singlespace $mod_desc
 mod_comment=!singlespace $mod_comment
 mod_title=!char 1 to $etitle_limit of $mod_title
 mod_desc=!char 1 to $edesc_limit of $mod_desc
 mod_comment=!char 1 to $edesc_limit of $mod_comment
 update_content=$D_\
$p_\
$mod_require\
$mod_weight\
$mod_title\
$mod_desc\
$mod_dep\
$mod_comment
 update_field=0
 exo=$modif
 !read update.exo
 !distribute items $ into mod_title,mod_desc,mod_weight,mod_require,mod_dep,mod_comment
!endif

!if $[$activetest]=0
 erasable=yes
 scnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheets
 !for s=$sheet+1 to $scnt
  l=!record $s of wimshome/log/classes/$wims_class/sheets/.sheets
  l=!line 1 of $l
  !if $l>0
   erasable=no
  !endif
 !next s
!endif

