<!-- adm/class/exam student.phtml START -->

!! <!--There isn't any help for student yet. this will hide the "help button"-->
!set module_has_help = no

!read adm/class/classname.phtml $wims_classname\
$wims_institutionname

!read adm/title.phtml 1\
$classname\
$title

!if $wims_user=supervisor
 <div style="text-align:center">($name_presentation)</div>
!endif

!if $desc!=$empty
	<h2>$name_instructions :</h2>
	<p class="exam_instructions">$desc</p>
!endif

!if $error!=$empty
 <div class="wims_alertmsg">
 !read lang/msg.phtml.$moduclass_lang
 </div>
 !href module=home $wims_name_back2
. <br />
 !exit
!endif

!if $registered<0 or ($registered>0 and $timeleft<=0)
 !changeto score.phtml
!endif

!if $exocnt=0
 $name_empty
!else
 <h2>$name_content:</h2>
 !set activeexo=0


 <ol class="wims_exam">
   !for i in $exolist
   <li class="exo_item">
!! -------------------------- debut de la ligne correspondant a un exercice de l'examen

 !set ex=!record $i of wimshome/log/classes/$wims_class/exams/.exam$exam
 !distribute lines $ex into we,li,ti,de,op
 !set de=!trim $de

 !if $t_=yes and ($allowtype=simulation or $tryremain>0 or \
   ($tryremain=0 and $registered!=$empty))
   !if $(replied$i)>0
    <span class="exo_name">$ti </span> - <span class="exo_done">$name_done</span>
   !else
    !if $de!=$empty
     !set dt=!examdep $exam $de
    !else
     !set dt=yes
    !endif
    !if $dt=yes
     !advance activeexo
     <span class="exo_link">
     !href target=wims_exam cmd=new&worksheet=$exam.$i $ti
</span>&nbsp;
     !if autogen iswordof $op and $wims_exam_remain!=$empty
      !set ss=$(wims_session)t$(exam)t$i
      !set test=!defof w_cmd in wimshome/sessions/$ss/var
      !if $test=$empty
       !set test=$wims_ref_name?wims_window=new&+session=$(wims_session)_exam&+cmd=new&+worksheet=$exam.$i
       <img src="$test" style="height:1px; width:1px" />
      !endif
     !endif
    !else
     <span class="wims_emph">$ti</span>
     <span>
     !href cmd=reply&job=student $name_depend
.</span>
    !endif
   !endif
  !else
   <span>$ti.</span>
  !endif
!! -------fin d'affichage de la ligne correspondant a un exercice

   </li>
  !next i
 </ol>
 <br class="spacer clearall" />

<p class="exam_instructions">
 !if $t_=no
  $name_examclosed
  !href cmd=reply&job=student $wims_name_refresh
 !else
  !if $stries=1
   $name_exam1
  !else
   $name_exam2
  !endif
  !if $tryremain<0 or ($tryremain=0 and ($registered=$empty or $allowtype=simulation))
   !if $trcut=0
    $name_exam3
   !else
    $name_exam4
   !endif
   $name_exam5
   !set exhausted=yes
  !endif
  !if $tryremain>0
   $name_exam6
  !endif
  !if $registered=$empty and $exhausted!=yes
   $name_exam9
   !if $allowtype!=simulation
    $name_exam7
   !else
    $name_exam8
   !endif
  !else
   !if $allowtype!=simulation and $tryremain=0 and $exhausted!=yes
    $name_lasttry
   !endif
  !endif
  !if $exhausted!=yes
   !set wims_menu_items=!append line refresh,1,cmd=reply&job=student\
scoreexam,1,cmd=reply&job=score \
to $wims_menu_items
   !if $activeexo<1
!set wims_menu_items=!append line endexam,1,cmd=reply&job=scorereg\
to $wims_menu_items
   !endif
  !endif
 !endif
!endif
</p>

!if $t_=yes
 !if $allowtype=simulation
  <p class="simulation"><strong>$name_simulation</strong></p>
 !endif
!endif

!if $wims_user=supervisor
 !set wims_menu_items=!append line backteacher,1,cmd=resume\
 to $wims_menu_items
!endif

<!-- adm/class/exam/student.phtml END -->
!tail

!reset job

