!read adm/css.phtml
!read lang/names.phtml.$moduclass_lang
!read adm/class/classname.phtml $wims_classname\
$wims_institutionname

!set js=!record 0 of js/chooselist.js
!set wims_html_header=!append line $js to $wims_html_header
!header1
!read adm/title.phtml 1\
$classname\
$module_title\
($supervisormail)

!set wims_menu_items=!append line part_list,1,module=adm/class/userlist\
 to $wims_menu_items
 
!if $error!=$empty
  !read msg.phtml
  !set job=
!endif
!if $cmd!=reply or $error!=$empty
$name_intro
 !form reply
 <table class="wimsnoborder wimscenter">
 <tr>
  <td>$name_subject</td>
  <td><input name="subject" size="45" value="$subject"/></td>
</tr>
<tr>
<td>$name_message</td>
 <td><textarea name="msg" cols="40" rows="5">$msg</textarea></td>
 </tr></table>
 <p class="wimscenter">
<input type="submit" name="reg" value="$wims_name_send"/>
</p>
<fieldset><legend>$name_menu
</legend>
!set file=wimshome/log/classes/$wims_class/.userlist
!set cnt=!recordcnt wimshome/log/classes/$wims_class/.userlist
!for lu =1 to $cnt
  !set us_=!record $lu of $file
  !if $(us_[3]) notitemof $select_user
   !set option=!append line <option value="$(us_[3])">$(us_[1]) $(us_[2]) ($(us_[3]))</option> to $option
  !else
   !set option_select=!append line <option value="$(us_[3])">$(us_[1]) $(us_[2]) ($(us_[3]))</option> to $option_select
  !endif
!next
!if $option=$empty
  !set option=<option value=""></option>
!endif
!if $option_select=$empty
  !set option_select=<option value=""></option>
!endif

$name_expert0
&nbsp;
!set wims_ref_class=wims_button_help
!href cmd=help&special_parm=general $wims_name_help

<p>
!formradio job list all prompt $name_allparticipants
</p>
<p>
!formradio job list select prompt <b>$name_select</b>:
$name_expert1
</p>
!set wims_chooselist_select=$option
!set wims_chooselist_checked=$option_select

<div class="wimscenter">
!read js/chooselist.phtml select_user1,select_user2,select_user
</div>

<p>
!formradio job list s prompt <b>$name_loginlist</b>:
$name_expert2
</p>
<div class="wimscenter">
   <textarea name="loginlist" cols="20" rows="5">$loginlist</textarea>
</div>
!default job=$job
<p>
!formradio job list filter prompt <b>$name_filter:</b>
$name_expert3
&nbsp;
</p>
!set wims_ref_class=wims_button_help
!href cmd=help&special_parm=filter $wims_name_help

<div class="wimscenter">
<textarea name="variable" cols="20" rows="3">$variable</textarea>
</div>

</fieldset>

!formend
!goto end
!endif
!if $confirm=$empty and $reg!=
 !if $email_exists=0
!! $list_user$list_noemail
  !if $list_user$list_noemail notsametext
  $name_noemail
  ($wims_name_Login : $(list_noemail[3]))
  !else
   $name_nouser
  !endif
  <p>
  !set wims_menu_items=!append line back2,1,cmd=resume\
 to $wims_menu_items
  !goto end
 !endif
$name_warning
<div class="wimscenter">
<input name="subject" size="45" value="$subject" readonly/>
<br/>
<textarea cols="40" rows="5" readonly>
$msg
</textarea>
</div>
<pre>
$list_user
</pre>
<pre>
$list_teacher
</pre>
!if $list_noemail notsametext
$name_noemail2
<pre>
$list_noemail
</pre>
!endif
 !form reply
<div class="wimscenter">>
!set wims_ref_class=wims_button
!href cmd=reply&confirm=yes $wims_name_send
&nbsp;&nbsp;&nbsp;
!set wims_ref_class=wims_button
!href cmd=resume $wims_name_giveup
</div>

!formend

!endif
 !set wims_menu_items=!append line back2,1,cmd=resume\
 to $wims_menu_items
!endif

!if $confirm=yes
 !set cmp=0
 !set cmp_tr=0
 !set cnt=!linecnt $list_user
 !for l_ =1 to $cnt 
  !set u=!line $l_ of $list_user
  !if $(u[4])!=
   !mailto $(u[4]) -- -f $supervisormail\
   [WIMS $wims_classname] $subject\
 $msg\
 \
 
   !endif
  !reset u
 !next
 !set cnt=!linecnt $list_teacher
 !for l_ =1 to $cnt 
  !set u=!line $l_ of $list_teacher
  !if $(u[4])!=
   !mailto $(u[4]) -- -f $supervisormail\
   [WIMS $wims_classname] $subject\
 $msg\
 \
 
   !endif
  !reset u
 !next 
!endif
!if $cmd=reply and $confirm=yes
<p>
$name_participant1
<pre>$(list_user[;1..2])</pre>
!if $list_noemail notsametext
$name_participant2
<pre>
$(list_noemail[;1..2])
</pre>
!endif

!set wims_module_log=class $class: sendmail
!set date=!translate : to . in $wims_now
!appendfile wimshome/log/classes/$wims_class/.log $date $httpd_REMOTE_ADDR	$supervisormail send mail to $(list_user[;1]) $(list_teacher[;1])
 
 !reset list_user list_noemail
 
!endif
:end
!reset confirm reg abandon error

!tail
