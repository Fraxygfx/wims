!read adm/language lang/names.phtml

!! menu definition file
!if $error!=$empty
  <div class="wims_alertmsg">
    !read msg.phtml
  </div>
  !exit
!endif
$name_intro
!form reply id="classifications"

!! Tab links
<ul>
  !for t=1 to $ref_list_cnt
    !set tt=$(ref_list[$t])
    <li><a href="#$tt">$(name_$tt)</a></li>
  !next
</ul>

!for t=1 to $ref_list_cnt
  !set tt=$(ref_list[$t])
  <fieldset id="$tt" class="property_fields blockcenter">

    !set ref_$(tt)_cnt=!itemcnt $(ref_$tt[;1])
    !set ref1_$(tt)=!lines2rows $(ref_$(tt))
    !if  $(ref_$(tt)_cnt)>0
      <div class="field box">
        <p>$name_evaluation</p>
        <ul>
          !for j = 1 to $(ref_$(tt)_cnt)
            !if $(ref1_$tt[$j;1])!=$empty
              !set key=$(ref1_$(tt)[$j;1])
              !if $tt issametext commoncore
                !set key1=!lookup $key in $taxodir/commoncore.$lang

                !set link1=!replace internal . by / in $key
                !set link=$(ref_address[$t])/$(link1)
                !set key=$key1
              !endif
              !if $tt issametext unisciel
                !set link1=!replace internal : by _ in $key
                !set link=!lookup $link1 in $taxodir/unisciel.$lang
                !default link = $key
                !set key=$link
              !endif
              !if $tt issametext keywords
                !set key1=!lookup $key in wimshome/public_html/bases/sys/domain/domain.$lang
                !default key1=$key
                !set key=$key1
              !endif
            !endif
            <li>
          $key<br/>
          !set evl=!declosing $(ref1_$(tt)[$j;2])
            !set wims_ref_class=wims_formradio
            !formradio f_$(tt)_$j list -1,0,1 prompt \
              $(name_prompt[1]) ($[-$(evl[1])]),\
              $(name_prompt[2]),\
              $(name_prompt[3]) ($(evl[2])),
            </li>
          !next
        </ul>
      </div>
    !endif

    <div class="field box">
    <p>
    $(name_$(tt)_instruction)
    </p>
     !if $tt issametext keywords
        !read help/keywords.phtml radio
        <p>
         <input type="radio" name="taxon_keywords" id="empty" value=""/>
         $wims_name_erase
         </p>
        <input type="hidden" name="search_lang" value="tech" id="search_lang" />
        <label for="taxon_$tt">$name_addkeyword_label</label>
        <input name="taxonfree_$tt" id="taxonfree_$tt" class="multicomplete" placeHolder="$name_onewordmax" />
        <script type="text/javascript" src="scripts/js/interface/search_engine.js"></script>
        <div class="formHelp">$name_addkeywords</div>
    !else
       <a class="float_right wims_button_help" target="wims_external" href="$(ref_address[$t])">$wims_name_help</a>
       <p>$name_selectkeyword</p>
       !read keywords/$tt.$lang.phtml
    !endif
    </div>
  </fieldset>
!next
<div class="wimsform wimscenter">
 <input type="submit" value="$wims_name_tosave" />
</div>

!formend
