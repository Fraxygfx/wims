
n=!recordcnt conf/cat
!bound confcat between 1 and $n default 1
l=!record $confcat of conf/cat
!distribute lines $l into catname, cattit

!if $catname=appearances
 csslist=!sh cd html/css; ls *.css | sed 's/\.css$$//' | sort
 csslist=!words2items ---- -theme- $csslist
 themelist=!sh find themes -name visitor.phtml | sed 's!^themes/!!;s!/visitor.phtml$!!' | sort
 themelist=!words2items $themelist
 themelist=!listuniq default,$themelist
 iconlist=!sh cd gifs/themes/;ls | sort
!!not very clean
 iconlist=!replace internal mkindex by in $iconlist
 iconlist=!words2items default $iconlist
!endif

!if $catname=graphics
 chtest=!sh ls $wims_home/chroot/tmp/sessions/.chroot 2>/dev/null
 !if chroot isin $chtest
  cvtest=new.png
 !else
  cvtest=!sh convert gifs/new.gif ../tmp/new.png >/dev/null\
   ls ../tmp/new.png 2>/dev/null\
   rm -f ../tmp/new.png >/dev/null
 !endif
!endif

!if $catname=software
 !reset othermiss
 !distribute items tool/number/baseconv,\
	H6/algebra/decrypt,\
	adm/doc,\
	adm/doc,\
	H5/physics/chim1,\
	H3/geometry/oefsect,\
	into mod_bc,mod_fortune,mod_perl,mod_latex2html,\
		mod_chemeq,mod_units
# otherlist=tex perl latex2html bc fortune chemeq units-filter
 otherlist=tex perl latex2html chemeq units-filter
 test=!sh for bin in $otherlist\
  do\
   which $$bin\
  done
 otherlist=!words2items $otherlist
 !for other in $otherlist
  !if /$other notin $test
   othermiss=!append item $other to $othermiss
  !endif
 !next other
!endif

!readdef wimshome/src/defaults.conf
confcnt=!recordcnt conf/confdata
!for i=1 to $confcnt
  l=!record $i of conf/confdata
  n_=!word 2 of $l
  $n_=$(DF_$n_)
!next i
wims_prefix=
!readdef wimshome/log/wims.conf
wims_prefix=$wims_prefix2

!if $save!=$empty
 changed=0
 !writefile wimshome/log/wims.conf2 # This file is automatically generated by the online site maintenance tool.\
# You can still edit it by hand, but documentation is only available in the\
# online tool.\
#
 !for i=1 to $confcnt
  l=!record $i of conf/confdata
  l=!line 1 of $l
  !distribute words $l into c_,n_,w_
  v_=$($n_)
  !if $catname isitemof $c_
   v_=$(conf_$i)
   v_=!trim $v_
   v_=!singlespace $v_
   v_=!replace internal \ by $ in $v_
   !if $v_!=$($n_)
    changed=1
    $n_=$v_
   !endif
   !if $n_=css and $v_=none
    v_=
   !endif  
   !if $n_=theme_icon and $wims_theme iswordof column columnb default classic
    v_=default
   !endif
  !endif
  !if $v_!=$(DF_$n_)
   !appendfile wimshome/log/wims.conf2 $n_=$v_
  !endif
  !if $w_ !=$empty and $w_ iswordof wims_bgcolor wims_bgimg wims_ref_bgcolor wims_css wims_theme_icon
   $w_=$v_
  !endif
 !next i
 !if $changed>0
  backdir=!mexec scripts/confcopy.sh
 !endif
 !sh rm -f $wims_home/log/wims.conf2
 !restart cmd=reply
!endif

