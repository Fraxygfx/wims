
!read adm/css.phtml
!header1

!if $sheet_docpage!=$empty
 !changeto docpage.phtml
!endif
!read adm/title.phtml 1\
$wims_classname - $wims_institutionname\
test #$sh<br>$sheet_title

<div class="wims_sheet_desc">$sheet_desc</div><p>

!if $sheet_expiration!=$empty and $sheet_active=1
 !set expday=!char 7,8 of $sheet_expiration
 !set expmon=!char 5,6 of $sheet_expiration
 !set expyear=!char 1 to 4 of $sheet_expiration
 !set months=gennaio,febbraio,marzo,aprile,maggio,giugno,luglio,agosto,settembre,ottobre,novembre,dicembre
 !set expmon=!item $expmon of $months
 Devi completare questo test di autovalutazione entro il $expday $expmon $expyear.
 <p>
!endif

!if $sheet_active=2
 <b><font color=red>Warning</font></b>
 Avresti dovuto aver gi&agrave; completato questo test di autovalutazione.
 Puoi ancora lavorarci,
 ma i punteggi ottenuti non saranno più tenuti in considerazione.
 <p>
!endif

!if $n=0
 !if $sheet_active=$empty
  Questo test di autovalutazione non &egrave; ancora disponibile. 
  Contatta il docente.
 !else
  Il test di autovalutazione è vuoto! Potrebbe trattarsi di un errore da parte
  del docente.
 !endif
!else
 Contenuti del test: (la registrazione dei punteggi è
 !set scorestat=!getscorestatus sheet=$sh
 !if $scorestat=yes
  !if $wims_scorereg!=suspend
   attiva, ma puoi
   !href cmd=resume&job=suspend sospenderla
.)
!set wims_menu_items=!append line scoreclose2,1,cmd=resume&job=suspend\
to $wims_menu_items
  !else
   !set scorestat=no
   stata da te sospesa.
   !href cmd=resume&job=reopen Riattivala
.)
!set wims_menu_items=!append line scorereopen,1,cmd=resume&job=reopen\
to $wims_menu_items
  !endif
 !else
  disattivata per questo tipo di connessione.)
 !endif
 <div class="wims_sheet">
 <p><ul class="wims_sheet_list">
 !for i=1 to $n
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sh
  !distribute lines $ex into di,pa,require,we,ti,de
  !set got=!word $i of $scores
  !set mean=!word $i of $means
  !set dep=!row $i of $deps
  !set dep=!trim $dep
  <li class="wims_sheet_list"><p>$i. 
  !if $dep=$empty or $scorestat!=yes
   !href module=$di&cmd=new&worksheet=$sh&$pa $ti
, $de
   !if $require>0
    <small>hai ottenuto $[$got] punti su $[$require], con media
    $[$mean]/10.</small>
   !endif
  !else
   <font color=red><b>$ti</b></font>, $de 
   !if $require>0
    <small>(sono richiesti $[$require] punti.)</small>
   !endif
  !endif
  !if $dep!=$empty
   <small>Devi migliorare
   !if , isin $dep
    i tuoi punteggi negli esercizi $dep
   !else
    il tuo punteggio nell'esercizio $dep
   !endif
   prima di cominciare a lavorare su questo.
   </small>
  !endif
 </li>
 !next i
 </ul>
 </div>
!endif

!if $wims_user=supervisor
!set wims_menu_items=!append line sheetmanagement,1,module=adm/class/sheet&sheet=$sh\
to $wims_menu_items
!endif
!if $n>0
!set wims_menu_items=!append line printable,1,cmd=reply&job=print\
to $wims_menu_items
 !if $exolog!=$empty
 !set wims_menu_items=!append line exolog,1, module=adm/class/exolog&job=llist&logsheet=$sh \
to $wims_menu_items 
 !endif
!endif
!tail

