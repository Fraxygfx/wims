
!read adm/css.phtml
!read names.phtml
!header1

!if $sheet_docpage!=$empty
 !changeto docpage.phtml
!endif
!read adm/title.phtml 1\
$wims_classname - $wims_institutionname\
$name_title

<div class="wims_sheet_desc">$sheet_desc</div><p>

!if $sheet_expiration!=$empty and $sheet_active=1
 !set expday=!char 7,8 of $sheet_expiration
 !set expmon=!char 5,6 of $sheet_expiration
 !set expyear=!char 1 to 4 of $sheet_expiration
 !read adm/date.phtml $sheet_expiration
 !set expmon=!item $expmon of $months
 $name_student1
 <p>
!endif

!if $sheet_active=2
 <b><font color=red>$wims_name_warning</font></b>
 $name_student2
 <p>
!endif
!if $n=0
 !if $sheet_active=$empty
  $name_student3
 !else
  $name_student4
 !endif
!else
 !set scorestat=!getscorestatus sheet=$sh
 !if $scorestat=yes
  !if $wims_scorereg!=suspend
   $name_scoreopen
   !href cmd=resume&job=suspend $wims_name_scoreclose2
.
!set wims_menu_items=!append line scoreclose2,1,cmd=resume&job=suspend\
to $wims_menu_items
  !else
   !set scorestat=no
   $name_scoresuspend
   !href cmd=resume&job=reopen $wims_name_scorereopen
.
!set wims_menu_items=!append line scorereopen,1,cmd=resume&job=reopen\
to $wims_menu_items
  !endif
 !else
 $name_scoreclose
 !endif
 <div class="wims_sheet">
 <p><ul class="wims_sheet_list">
 !for i=1 to $n
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sh
  !distribute lines $ex into di,pa,require,we,ti,de
  !set got=!word $i of $scores
  !set mean=!word $i of $means
  !set dep=!row $i of $deps
  !set dep=!trim $dep
  <li class="wims_sheet_list"><p>$i. 
  !if $dep=$empty or $scorestat!=yes
   !href module=$di&cmd=new&worksheet=$sh&$pa $ti
, $de
   !if $require>0
    <small>$name_score
    $[$mean]/10.</small>
   !endif
  !else
   <font color=red><b>$ti</b></font>, $de 
   !if $require>0
    <small>($name_score2)</small>
   !endif
  !endif
  !if $dep!=$empty
   <small>
   !if , isin $dep
    $name_deps $dep
   !else
    $name_dep $dep
   !endif
   $name_dep2
   </small>
  !endif
 </li>
 !next i
 </ul>
 </div>
!endif

!if $wims_user=supervisor
!set wims_menu_items=!append line sheetmanagement,1,module=adm/class/sheet&sheet=$sh\
to $wims_menu_items
!endif
!if $n>0
!set wims_menu_items=!append line printable,1,cmd=reply&job=print\
to $wims_menu_items
 !if $exolog!=$empty
 !set wims_menu_items=!append line exolog,1, module=adm/class/exolog&job=llist&logsheet=$sh \
to $wims_menu_items 
 !endif
!endif
!tail

