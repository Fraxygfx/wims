!set sizex=65
!set types=!sort nocase lines \
auto,			default\
expression littérale,	litexp\
expression algébrique,	algexp\
expression formelle,	formal\
texte approximatif,	atext\
texte converti en minuscules,nocase\
texte exact,		case\
texte brut,		raw\
equation,		equation\
ensemble fini,		set\
matrice,		matrix\
nombre,			numeric\
nombre avec zone,	range\
nombre avec unité,	units\
fonction numérique,	function\
vecteur,		vector\
mots dans une liste,	wlist

!set types=!nonempty lines $types
!set typelist=!column 2 of $types
!set typenames=!column 1 of $types

Maintenant veuillez entrer le contenu de votre exercice 
<font color=red><b>$oef_title</b></font>.
!href cmd=reply&level=1 Retourner au menu de départ
. <p>

!form reply
<input type=hidden name=level value=3>
Vous pouvez définir des paramètres aléatoires ici&nbsp;: [
!href target=wims_mhelp cmd=help&special_parm=parameters explications
 ]<br>
!set o_parms=!reinput $oef_parms
<textarea wrap="off" rows=6 cols=$sizex name=oef_parms>$o_parms</textarea>
<p>
L'énoncé de l'exercice (format $oef_format)&nbsp;: [
!href target=wims_mhelp cmd=help&special_parm=format explications
 ]<br>
!set o_statement=!reinput $oef_statement
<textarea wrap="off" rows=6 cols=$sizex name=oef_statement>$o_statement</textarea>
<p>
!if $oef_answercnt>0
 <table border=1>
 <caption>Les réponses libres
 <tr><th>Nom<th>Bonne solution<th>Type</b><small>
 !href target=wims_mhelp &cmd=help&special_parm=reply#freestyle [$wims_name_help]
 !for i=1 to $oef_answercnt
  !default anstype$i=default
  !set p_=!reinput $(ansprompt$i)
  <tr><td> <input size=$[$promptlim-3] maxlength=$promptlim name=ansprompt$i value="$p_">
  !set p_=!reinput $(ansgood$i)
  <td> <input size=30 name=ansgood$i value="$p_">
  <td><small>
  !formselect anstype$i list $typelist prompt $typenames
 !next i
 </table> <p>
 Précision exigée des réponses numériques&nbsp;:
 !formselect oef_precision list 100,1000,10000,100000,1000000 prompt 1/100,1/1000,1/10 000,1/100 000,1/1 000 000
 <br>Les fonctions (numériques) seront comparées dans l'intervalle
 (&nbsp;<input size=6 name=oef_rangel value="$oef_rangel">,<input size=6 name=oef_ranger value="$oef_ranger">&nbsp;).
 <p>
!endif

!if $oef_condcnt>0
 <table border=1>
 <caption>Conditions d'analyse des réponses (numériques)
 <tr><th>Nom de la condition<th>Définition de la condition
 !for i=1 to $oef_condcnt
  !set p_=!reinput $(Condname$i)
  <tr><td><input size=$promptlim maxlength=$promptlim name=Condname$i value="$p_">
  !set p_=!reinput $(Condtest$i)
  <td> <input size=40 name=Condtest$i value="$p_">
 !next i
 </table> <p>
!endif

!if $oef_choicecnt>0
 <table border=1>
 <caption>Les choix multiples
 <tr><th>Nom<th>Bon choix<th>Mauvais choix
 !for i=1 to $oef_choicecnt
  !set p_=!reinput $(choiprompt$i)
  <tr><td> <input size=$promptlim maxlength=$promptlim name=choiprompt$i value="$p_">
  !set p_=!reinput $(choigood$i)
  <td> <input size=10 name=choigood$i value="$p_">
  !set p_=!reinput $(choibad$i)
  <td> <input size=30 name=choibad$i value="$p_">
 !next i
 </table> <p>
!endif

<p><center>
<input type=submit value="Enregistrer le contenu">
</center> <p>
Vous pouvez facultativement donner une indication et/ou une solution&nbsp;:
<table border=0>
<tr><td>Indication<br>(format html)
!set pp=!reinput $oef_hint
<td><textarea wrap="off" rows=3 cols=$[$sizex-10] name=oef_hint>$pp</textarea>
<tr><td>Solution<br>(format html)
!set pp=!reinput $oef_solution
<td><textarea wrap="off" rows=3 cols=$[$sizex-10] name=oef_solution>$pp</textarea>
</table>
</form>

