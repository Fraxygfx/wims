!if $wims_class=$empty or $wims_user=$empty
 error=not_class
 !exit
!endif

wims_nw=votedir Votedir userdef vote Vote job empty
wims_nr=wims_sesrandom
Votedir=classes/$wims_class/vote
votedir=wimshome/log/$Votedir
!read adm/class/userdef classes,$wims_class,$wims_user
userfile=$userdef
!read names
!bound vote between integer 1 and 64 default 0

vote_class=!defof sharing_vote in wimshome/log/classes/$wims_class/neighbors
!default vote_class=$wims_class
!default wims_superclass=$wims_class
!if $wims_superclass != $wims_class
 Votevar=!replace internal $wims_superclass/ by $ in $vote_class
 Votevar=!translate internal / to _ in $Votevar
 Vote=$(Votevar)_$vote
!else
 Vote=$vote
 Votevar=
!endif

!if $rEg!=$empty
 job=reg
 rEg=
!endif
!if $abandon!=$empty
 job=home
 abandon=
!endif
readjobs=home,read,help,vote
editjobs=creat,edit,reg,erase,preview,csv
!if $wims_user=supervisor
 !bound job within $readjobs,$editjobs default home
!else
 !bound job within $readjobs default home
!endif
!if $job=help or robot isin $session
 !exit
!endif

!read tabletheme
!for i=1 to 64
 m_parm$i=$(parm$i)
!next i

votecnt=!recordcnt $votedir/.votes
!if $votecnt<1 and $job notwordof reg creat csv
 job=home
 !changeto home.proc
!endif
!if $job=creat
 !if $votecnt>=64
  error=too_many_votes
  !exit
 !else
  vote=$[$votecnt+1]
  job=edit
 !endif
!endif

!if $vote>0 and $vote<=$votecnt
 vdata=!record $vote of $votedir/.votes
 !distribute lines $vdata into vote_status,vote_title,vote_option,vote_show
 !default vote_option=anonymous
 !default vote_status=0
 !default vote_show=1
 vote_src=!record 0 of $votedir/$vote.src
!endif

!if $vote<1
 job=home
!endif

!read $job.proc

