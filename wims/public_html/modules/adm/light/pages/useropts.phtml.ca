!!INDEX
!let module_author	=XIAO, Gang
!let module_address	=xiao@unice.fr
!let module_translator  =Manel Querol
!let module_translator_address=mquerol@xtec.cat
!let module_title	=Configuració de les preferències pel WIMS
!let module_description	=defineix les teues preferències a WIMS
!let module_version	=1.00
!let module_wims_version=0.13
!let module_language	=ca
!let module_copyright	=&copy; 1998 (<a href=COPYING>GNU GPL</a>)
!let module_category	=adm
!let module_level	=
!let module_domain	=adm
!let module_keywords	=wims
!!INDEXEND

!let texsize=!char 1 of $useropts
!let texalign=!char 2 of $useropts
!default texsize=$[$wims_texbasesize-1]
!default texalign=$talign
!let useropt2=!char 3 to -1 of $useropts
!if $useropts!=$empty and $wims_user!=$empty
 !readproc adm/class/userdef classes,$wims_class,$wims_user
 !setdef !set user_opts=$useropts in $userdef
 !setdef wims_useropts=$useropts in wimshome/$wims_sesdir/var.stat
!endif

!header

Aquesta pàgina et permet canviar les preferències de la teua sessió de treball
a WIMS.
<p>
Aquestes preferències són:
<ul>
 <li>La llengua per defecte.
  !for l=1 to $wims_language_cnt
   !let la=!word $l of $wims_site_languages
   !href cmd=resume&phtml=useropts.phtml.$la&lang=$la <img src=gifs/$la.gif border=1>$(lang_name_$la)
   &nbsp;&nbsp;
  !next l
 <p><li>La mida dels símbols matemàtics i les fórmules. Utilitza la línia
 següent per comparar la mida  dels símbols i els textos.
  <p>
  Lletres gregues $m_alpha,$m_pi. Expressions matemàtiques 
  !let ins_align=middle
  !instex $$x^3\sin(x)+{\sqrt{x^2+1}\over105}-123.45$$.
  <p> Una desigualtat 
  <center>| $m_a + $m_b | $m_le $m_varepsilon + 123.45.</center>
  <p>
  Pots
  !let ts1=!eval $texsize-1
  !let ts2=!eval $texsize+1
  !if $ts1<1
    !let ts1=1
  !endif
  !if $ts2>9
    !let ts2=9
  !endif
  !href cmd=resume&useropts=$ts1$texalign$useropt2 disminuir
  o
  !href cmd=resume&useropts=$ts2$texalign$useropt2 augmentar
  la mida dels símbols matemàtics, per ajustar-los a la mida del text.<p>
  Si l'expressió
  !insmath (x+y)/(x^2+y^2)
  està
  !if $texalign=1
   massa alta respecte a la línia de text,
   !href cmd=resume&useropts=$(texsize)0$useropt2 clica ací
   per baixar-la.
  !else
   massa baixa respecte a la línia de text,
   !href cmd=resume&useropts=$(texsize)1$useropt2 clica ací
   per pujar-la.
  !endif
</ul>

Quan tot estigui d'acord a les teues preferències, posa
<a href="$wims_ref_name?lang=$lang&useropts=$texsize$texalign$useropt2"
 title="WIMS">aquest enllaç</a>
a les teues Adreces d'interès, per mantenir aquestes preferències a les futures connexions.

:end
!tail

