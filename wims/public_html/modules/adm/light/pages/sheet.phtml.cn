!exit
!!INDEX
!let module_author  =XIAO Gang
!let module_address =xiao@unice.fr
!let module_title   =页面
!let module_description =学生用作业页
!let module_version =2.00
!let module_wims_version=2.00
!let module_language    =cn
!let module_copyright   =&copy; 1998 (<a href=COPYING>GNU GPL</a>)
!let module_category    =adm
!let module_level   =E1 E2 E3 E4 E5 E6 H1 H2 H3 H4 H5 H6 U1 U2 U3 U4 G R

!! e.g. algebra, analysis, geometry, etc.
!let module_domain  =

!! For search engines; anything you like.
!let module_keywords    =

!!INDEXEND

!if $parm5=suspend
 !set wims_scorereg=suspend
!endif
!if $parm5=reopen
 !set wims_scorereg=
!endif

!!<html><head>
!!$wims_html_header
!!</head>
!!<body $wims_htmlbody>
!header1

!set sheet=!record $parm1 of wimshome/log/classes/$wims_class/sheets/.sheets
!distribute lines $sheet into sheet_active,sheet_expiration,sheet_title,sheet_desc
!if $wims_user != supervisor and $sheet_active<1
 !distribute items $ into sheet_active,sheet_expiration,sheet_title,sheet_desc
!endif
!set requires=!getscorerequire sheet=$parm1
!set scores=!getscore sheet=$parm1
!set means=!getscoremean sheet=$parm1

<hr width=1>
<center><h3>$wims_classname ($wims_institutionname)</h3>
<hr> <p><h1>作业页 #$parm1<br>$sheet_title</h1></center> <p>
$sheet_desc<p>

!if $sheet_expiration!=$empty and $sheet_active=1
 !set expday=!char 7,8 of $sheet_expiration
 !set expmon=!char 5,6 of $sheet_expiration
 !set expyear=!char 1 to 4 of $sheet_expiration
 !set months=一月,二月,三月,四月,五月,六月,七月,八月,九月,十月,十一月,十二月
 !set expmon=!item $expmon of $months
 本作业页的有效期到 $expyear 年 $expmon $expday 日止.
 <p>
!endif

!if $sheet_active=2
 <b><font color=red>警告</font></b>
 本页已经过期. 尽管你仍可以做练习,
 但是你的成绩将不被纪录.
 <p>
!endif

!if $sheet_active=$empty
 !set n=0
!else
 !set n=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$parm1
!endif

!if $n=0
 !if $sheet_active=$empty
目前你无权阅读本页的内容. 请与你的主持教师联系.
 !else
本页是空白的! 没有为你指定任何作业. 也许这是主持教师的操作错误. 
 !endif
!else
本页指定的作业是: (得分纪录现在
 !set dd=!getscorestatus sheet=$parm1
 !if $dd=yes
  !if $wims_scorereg!=suspend
   开放，但你可以
   !href cmd=resume&parm5=suspend 暂停它
.)
  !else
   被你自己暂停.
   !href cmd=resume&parm5=reopen 重新开放
.)
  !endif
 !else
  对你关闭.)
 !endif
 <p><ul>
 !for i=1 to $n
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$parm1
  !distribute lines $ex into di,pa,require,we,ti,de
  !set got=!word $i of $scores
  !set mean=!word $i of $means
  <li><p>$i. 
  !href module=$di&cmd=new&worksheet=$parm1&$pa $ti
, $de
  !if $require>0
   <small>总共 $[$require] 分中得到了 $[$got], 平均成绩
   $[$mean]/10.</small>
  !endif
 !next i
 </ul>
!endif

!if $wims_user=supervisor
<p>
!href module=adm/class/sheet&sheet=$parm1 作业页管理
 (供主持教师使用).
!endif
<p>
!tail

