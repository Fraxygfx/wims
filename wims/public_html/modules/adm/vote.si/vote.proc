!if $vote_status!=1
 error=bad_vote
 job=home
 !changeto home.proc
!endif

!readdef $userfile

##pour la periode transitoire de changement (etablissement)
## ensuite dans aucun etablissement ne sera créé user_vote_1
####ne corrige donc pas le bogue pour les établissements actuels 
####  mais pour les nouveaux votes, mais cela semble plus propre

!if $(user_vote_$Vote)=$empty and $(user_vote_$vote)!=$empty
    user_vote_$Vote=$(user_vote_$vote)
!endif


!if $vote_option=anonymous and $(user_vote_$Vote)!=$empty
 error=already_voted
 !exit
!endif

!readdef $votedir/$vote.votes

!readdef $votedir/$vote.comments
!for t=1 to $parmcnt
  !if $vote_option=anonymous and $(type_comment$t)=yes
    tt = !char 1 to 500 of $(parm$t)
    tt = !singlespace $(parm$t)
    tt = !append item ---- $tt to $(comment_$t)
    tt = !char 1 to 12000 of $tt
    !setdef comment_$t=$tt in $votedir/$vote.comments
  !endif
!next t
voteset=
!if $(user_vote_$Vote)=$empty
 !advance vcnt
 voteset=vcnt=$vcnt
!endif
!for t=1 to $parmcnt
 !for k=0 to $(choices$t)
  vc_$k=$(vcnt_$(t)_$k)
 !next k
 !bound parm$t between integer 1 and $(choices$t) default 0
 !default vc_$(parm$t)=0
 !advance vc_$(parm$t)
 old=!item $t of $(user_vote_$Vote)
 !for v in $old
  vc_$v=$[max(0,$(vc_$v)-1)]
 !next v
 !for k=0 to $(choices$t)
  !default vc_$k=0
  !if $(vc_$k) != $(vcnt_$(t)_$k)
   voteset=!append line vcnt_$(t)_$k=$(vc_$k) to $voteset
  !endif
 !next k
!next t

!if $vote_option=anonymous
 user_vote_$Vote=yes
!else
 tt=
 !for t=1 to $parmcnt
  old=!item $t of $(user_vote_$Vote)
  !for v in $old
   vv=!replace item $wims_class/$wims_user by $ in $(votes_$(t)_$v)
   vv=!nonempty items $vv
   voteset=!append line votes_$(t)_$v=$vv to $voteset
   votes_$(t)_$v=$vv
  !next v
  tt=!append item $(parm$t) to $tt
  vv=!append item $wims_class/$wims_user to $(votes_$(t)_$(parm$t))
  voteset=!append line votes_$(t)_$(parm$t)=$vv to $voteset
 !next t
 user_vote_$Vote=$tt
!endif

!setdef !set user_vote_$Vote=$(user_vote_$Vote) in $userfile
!if $voteset!=$empty
 !setdef $voteset in $votedir/$vote.votes
!endif

ccc=!itemcnt $uvcnt
!for i=1 to $ccc
 uvc=!item $i of $uvcnt
 uvn=!item $i of $uvname
 uvv=!line $i of $uvval
 uvv=!item $(parm$uvc)+1 of $uvv
 !setdef !set user__$uvn=$uvv in $userfile
!next uv

job=read
!changeto read.proc

