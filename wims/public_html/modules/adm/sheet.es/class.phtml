<html><head>
$wims_html_header
</head><body $wims_htmlbody>
!headmenu

!if $sheet_docpage!=$empty
 !changeto docpage.phtml
!endif
<center><h3>$wims_classname, $wims_institutionname</h3>
<hr> <p><h1>Hoja de trabajo número $sh<br>$sheet_title</h1></center> <p>
$sheet_desc <p>

!if $sheet_expiration!=$empty and $sheet_active=1
 !set expday=!char 7,8 of $sheet_expiration
 !set expmon=!char 5,6 of $sheet_expiration
 !set expyear=!char 1 to 4 of $sheet_expiration
 !set months=enero,febrero,marzo,abril,mayo,junio,julio,agosto,septiembre,octubre,noviembre,diciembre
 !set expmon=!item $expmon of $months
 Puede trabajar en esta hoja hasta el $expday $expmon $expyear.
!endif

!if $sheet_active=2
 <blink><b><font color=red>Aviso</font></b></blink>
 Esta hoja ha expirado. Puede seguir 
 trabajando en ella, pero sus puntuaciones ya no serán tenidas en cuenta.
 <p>
!endif

!if $n=0
 !if $sheet_active=$empty
  Por el momento no está autorizado a consultar el contenido de esta hoja.
  Por favor contacte con su profesor.
 !else
  ¡La hoja está en blanco! No se le ha asignado nada en ella; probablemente
  se trate de un error de su profesor.
 !endif
!else
 Tareas asignadas en esta hoja (la grabación de puntuaciones está
 !set scorestat=!getscorestatus sheet=$sh
 !if $scorestat=yes
  !if $wims_scorereg!=suspend
   abierta, pero puede
   !href cmd=resume&job=suspend suspenderla
):
  !else
   !set scorestat=no
   suspendida por usted mismo.
   !href cmd=resume&job=reopen Reactivarla
):
  !endif
 !else
  cerrada para su conexión):
 !endif
 <p><ul>
 !for i=1 to $n
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sh
  !distribute lines $ex into di,pa,require,we,ti,de
  !set got=!word $i of $scores
  !set mean=!word $i of $means
  !set dep=!row $i of $deps
  !set dep=!trim $dep
  <p><li>$i.
  !if $dep=$empty or $scorestat!=yes
   !href module=$di&cmd=new&worksheet=$sh&$pa $ti
, $de
   !if $require>0
    <small>$[$got] de $[$require] puntos obtenidos, calidad
    $[$mean]/10.</small>
   !endif
  !else
   <font color=red><b>$ti</b></font>, $de 
   !if $require>0
    <small>($[$require] puntos exigidos).</small>
   !endif
  !endif
  !if $dep!=$empty
   <small>Tiene que mejorar
   !if , isin $dep
    sus puntuaciones en los ejercicios $dep
   !else
    su puntuación en el ejercicio $dep
   !endif
   antes de recibir puntuaciones en este.
   </small>
  !endif
 !next i
 </ul>
!endif

:contentend
!if $wims_user=supervisor
<p>
!href module=adm/class/sheet&sheet=$sh Página de gestión de las hojas de trabajo
 (para el profesor).&nbsp;
!endif
!href cmd=reply&job=print Versión imprimible
.
!if $exolog!=$empty
 !href module=adm/class/exolog&job=llist&logsheet=$sh Grabar los detalles de un ejercicio
.
!endif
<p>

!tail

