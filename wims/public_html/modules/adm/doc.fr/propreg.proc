
!bound copyrightr within gnu,opl,private default gnu
!if class != $doctype
 !bound docopenr within yes, no default no
!else
 !if $wims_user!=supervisor
  !goto end
 !endif
 !bound docopenr within 0,1,2 default 0
!endif
titr=!detag $titr
titr=!translate internal !$$ to $      $ in $titr
titr=!singlespace $titr
titr=!char 1 to $titlim of $titr
titr=!trim $titr
authorr=!translate internal !$$ to $      $ in $authorr
authorr=!singlespace $authorr
authorr=!char 1 to $titlim of $authorr
authorr=!trim $authorr
emailr=!translate internal !$$ to $      $ in $emailr
emailr=!word 1 of $emailr
emailr=!char 1 to $titlim of $emailr
descr=!translate internal $	\
$ to $ 		   $ in $descr
descr=!trim $descr
!if $emailr!=$empty
 etest=!translate @ to , in $emailr
 etes2=!translate @ to $ $ in $emailr
 ecnt=!itemcnt $etest
 ecnt2=!wordcnt $etes2
 !distribute items $etest into e1,e2
 !if $ecnt!=2 or $ecnt2!=2 or . notin $e2
  error=bad_email
  !exit
 !endif
!endif
dl=!words2items $dlangr
dlangr=
!for l in $dl
 l=!lower $l
 l=!text select abcdefghijklmnopqrstuvwxyz in $l
 n=!charcnt $l
 !ifval $n=2 or $l=all
  dlangr=$dlangr $l
 !endif
!next l
dlangr=!trim $dlangr
!default dlangr=all
headerr=!translate internal $	\
$ to $ 		   $ in $headerr

!if $titr=$empty
 error=no_tit
 !exit
!endif

!if $authorr=$empty
 error=no_author
 !exit
!endif

!if class != $doctype
 otest=!lower $titr $authorr
 otest=!singlespace $otest
 otest=!deaccent $otest
 otest=!text select $ $namechars in $otest
 translator_unknown=
 dictionary=bases/sys/abuse.$module_language
 otest=!exec translator $otest
 !if abuse isin $otest
  error=abuse
  !exit
 !endif
!endif

!mexec scripts/mkdocdir.sh

!writefile $docdir/$doc/.def tit=$titr\
author=$authorr\
email=$emailr\
copyright=$copyrightr\
docopen=$docopenr\
dlang=$dlangr\
header=$headerr\
desc=$descr

!read adm/docindex.proc
!read $docdir/$doc/.def

:end
block=main
!read $docdir/$doc/main.def
job=read

