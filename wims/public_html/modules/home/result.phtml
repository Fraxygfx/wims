!nocache
<a id="searchresult"> </a>
!! L is no more  a category
!!!if $s_category=L
!! !changeto result.L
!!!endif
!if $error!=$empty and  $(ER_$error)!= 
<p class="wims_alertmsg">
 $(ER_$error)
</p>
!endif

!set tt=<span class="wims_emph">$(C_$s_category)</span>

!if $gottype=search
<p class="wims_clearSearch">
  !set wims_ref_class=wims_button
  !href session=$wims_session.1&list=clear $N_clear
</p>
<p class="RE_found">
 $RE_found.
</p>
!else
 !if $s_keywords!=$empty and $Gotcnt<1
 <p class="wims_alertmsg">
  $RE_nomatch.
  </p><p>
  !if $(rel_$s_category)!=$empty
   $RE_try
   !for i in $(rel_$s_category)
    !href module=home&cmd=reply&search_category=$i&search_keywords=$s_keywords $(C_$i)
    &nbsp;
   !next i
   !!if $has_browse=yes
    $wims_name_or
    !href module=adm/browse&$browse_parm $N_browsesite
.
   !endif
  !endif
  </p>
 !endif
 !if $s_category isin ADXOTR and $gotcnt1<1 and $gotcnt>0 and $Gotcnt<1
  !if $list_start>0
   !href module=home&cmd=reply&list_start=$[$list_start-$list_batch] &lt;&lt;
   &nbsp;
   $RE_pop2.
  !else
   $RE_pop.
  !endif
  !if $list_start<$list_cc-$list_batch
   &nbsp;
   !href module=home&cmd=reply&list_start=$[$list_start+$list_batch] &gt;&gt;
  !endif
  !reset list_start
 !endif
!endif

!if $s_category isin PLS
 !changeto result.$s_category
!endif

!if $gottype=search or $gotcnt1=0
 <ul class="wims_home_result_list">
 !for i=1 to $gotcnt
  !set m_=!line $i of $gotm
  !set w_=!word $i of $gotw
  !set l_=!line $i of $gotl
  !set t_=!line $i of $gott
  !set d_=!line $i of $gotd
  !set a_=!line $i of $gota
  <li>
  !if $wims_ismanager=2
   <span class="small">$w_</span>
  !endif
  !if $l_!=$search_lang
    !set ltrans=!module translation_language $m_
    !if  $search_lang notin $ltrans
      <img src="gifs/$l_.gif" alt="$l_.gif"/>
    !endif
    !set tit_=!module title_$search_lang $m_
    !set desc_=!module description_$search_lang $m_ 
    !set t_=$tit_
    !set d_=$desc_
  !endif
  !if $t_=
   !set t_=!module title $m_ 
  !endif
 !if $d_=
  !set d_=!module description $m_ 
 !endif
  !set L=
  !if ../local/ isin ../$m_
   !set L=<sup>local</sup>
  !endif
  !if ../com/ isin ../$m_
   !set L=<sup>com</sup>
  !endif
  !if ../contrib/ isin ../$m_
   !set L=<sup>contrib</sup>
  !endif
  !href module=$m_ $t_
$L, $d_ 
 !if $wims_show_author=yes
  <span class="small">(
  !replace internal , by $ $ in $a_
)</span>
 !endif
 </li>
 !reset d_ t_
 !next i
 </ul>
!endif

!if $gotcntS > 0
  !read result.S
!endif

!if $gotcntC > 0
  !read result.C
!endif

!if $s_category isin AT and $gotcnt1>0
<hr style="width:50%"/>
 !exchange gotcnt,gotcnt1
 !exchange gottype,gottype1
 !exchange gotm,gotm1
 !set tt=<span class="wims_emph">$C_P</span>
 <p>$RE_found. </p><div>
 !read result.P
 </div>
!endif
