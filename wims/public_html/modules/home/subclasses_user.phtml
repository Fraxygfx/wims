$U_gotoclass3
!if $wims_superclass=$empty
  wims_superclass=$wims_class
!endif
!let user_supervise=!defof user_supervise in wimshome/log/classes/$wims_superclass/.users/$wims_user
!! ----------- definition des droits de connexion supervisor
<br>
!let compsup=$empty
!for cls in $user_supervise
 !let tmp=!replace internal / by , in $cls
 !let nb=!itemcnt $tmp
 !for l=2 to $nb
  !let t=!replace internal , by / in $(tmp[1 to $l])
  !let compsup=!append line $t to $compsup
 !next l
!next cls
!let compsup=!sh echo "$compsup" | sort | uniq;
!let compsup=!lines2items $compsup
!let compsup=!nospace $compsup

!let tab=0
!for cls in $compsup
 !let don=!defof class_typename,class_description,class_supervisor,class_parent in wimshome/log/classes/$cls/.def
 !let t=!positionof item $(don[1]) in level,class,program,course
 !if $t=3
  !let ta=2
 !else
  !let ta=$t
 !endif
 !if $tab!=$ta
  !if $tab>$ta
   !for k=$ta+1 to $tab
    </ul>
   !next k
  !else
   !for k=$tab+1 to $ta
    <ul class="wims_subclasses">
   !next k
  !endif
 !endif
 !let tab=$ta
 !if $t=4
  !let tmp=!defof class_typename in wimshome/log/classes/$(don[4])/.def
  !if $tmp=program
   !let t=5
  !endif
 !endif
 <li>$arr <span class="wims_classes_direct_$(don[1])"> $(CL_Name[$t]) : 
 !if $cls notitemof $user_supervise
  $(don[2])
 !else
  !href module=adm/class/classes&type=authsupervisor&class=$cls&auth_user=supervisor $(don[2])
 !endif
 ($(don[3])) </span> </li>
!next cls
!for k=1 to $tab
 </ul>
!next k



