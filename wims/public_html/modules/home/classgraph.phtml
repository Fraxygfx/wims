!set statut=!item 1 of $wims_read_parm
!set compsup=!item 2 to -1 of $wims_read_parm
!set compsup=!nospace $compsup
!let tab=0
!for cls in $compsup
 !if $wims_class/ notin $cls
   !goto encls
 !endif
 !let don=!defof class_typename,class_description,class_supervisor,class_parent in wimshome/log/classes/$cls/.def
 !let t=!positionof item $(don[1]) in level,class,program,course
 !if $t=0 or $t=$empty
    !goto encls
 !endif
 !if $t=3
  !let ta=2
 !else
  !let ta=$t
 !endif
 !if $tab!=$ta
  !if $tab>$ta
   !for k=$ta+1 to $tab
    </ul>
   !next k
  !else
   !for k=$tab+1 to $ta
    <ul class="wims_subclasses">
   !next k
  !endif
 !endif
 !let tab=$ta
 !if $t=4
  !let tmp=!defof class_typename in wimshome/log/classes/$(don[4])/.def
  !if $tmp=program
   !let t=5
  !endif
 !endif
 <li>$arr <span class="wims_classes_direct_$(don[1])"> $(CL_Name[$t]) :
 !if $cls notitemof $(user_$statut)
  $(don[2])
 !else
   !if $statut=supervise
     !href module=adm/class/classes&type=authsupervisor&class=$cls&auth_user=supervisor $(don[2])
   !else
     !href module=adm/class/classes&type=authparticipant&class=$cls&auth_user=$u_ $(don[2])
   !endif
 !endif
 ($(don[3])) </span> </li>
:encls
!next cls
!for k=1 to $tab
 </ul>
!next k
!reset compsup
