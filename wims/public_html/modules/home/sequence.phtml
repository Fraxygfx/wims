!distribute items $wims_read_parm into i,j
!default j=1
!set seq_=!record $j of wimshome/log/classes/$wims_class/seq/.sequence$i
!distribute line $seq_ into tis_,cos_,obj_,o_



!if $o_<1
  !exit
!endif
!set wlist=!record 0 of wimshome/log/classes/$wims_class/sheets/.weight
!set TI=!defof sequence_Title in wimshome/log/classes/$wims_class/seq/.def
<fieldset id="wims_seq_$i" class="wims_seq">
<legend>$TI $i
</legend>
<div class="wims_title_seq">
<span class="wims_title_item">
   $tis_
</span>
<span class="wims_comment_item">
   $cos_
</span>
</div><!--wims_title_seq-->
!set dcnt=!itemcnt $obj_
<ul class="wims_work_list">
!for i_=1 to $dcnt
   !distribute word $(obj_[$i_]) into type,num
   !if $type=c
      !set d_=!record $num of wimshome/log/classes/$wims_class/doc/.index
      !distribute lines $d_ into s_,t_,bidon,bidon,bidon,o_,bidon,bidon,de_
      !if $o_>0
      <li class="wims_doc_item">
        !href module=adm/doc&job=read&doc=$s_&block=main&seq=$i $t_
        <div class="wims_doc_desc">$de_</div>
      </li>
      !endif
   !endif
   !if $type=doc
     !set d_=!record $num of wimshome/log/classes/$wims_class/doc/.docindex
     !distribute lines $d_ into s_,t_,ti_,de_,o_,w_
     !if $o_>0
     <li class="wims_doc_item">
        !href module=$s_&$t_&seq=$i&name=$wims_name_sequence&wksheet=$w_  $ti_
       <div class="wims_doc_desc">$de_</div>
     </li>
     !endif
     !reset s_,t_,ti_,de_,o_
   !endif
   !if $type=sheet
     !set s_=!record $num of wimshome/log/classes/$wims_class/sheets/.sheets
     !distribute lines $s_ into a_,t_,ti,de
     !if $a_>=1 and $a_<=2
     <li class="wims_sheet_item">
       !href module=adm/sheet&sh=$num&seq=$i $ti
       <div class="wims_user_info">
    <span class="wims_user_sheet_expire"><!--wims_user_sheet_expire-->
      !if $a_>=2
        $U_expired
      !else
       !if showshexpire iswordof $class_option
        !read misc/expire.phtml.$lang $t_
       !endif
      !endif
    </span><!--wims_user_sheet_expire-->
      !set formula_list=!record 0 of adm/class/sheetformula
      !set scoremax=!line 1 of $sevlist
      !set sev_$num=!line $num + 1 of $sevlist
      !distribute word $(sev_$num) into w_$num,s,ss
      !bound s between integer 0 and 6 default 2
      !bound ss between integer 0 and 2 default 0
      !set f_$num=!item $s+1 of $formula_list
      !set f_$num=!replace internal I by I$ss in $(f_$num)
      !set pe=!line $num of $percents
      !distribute words $pe into p1,p2,p3,p4
      !distribute item $[$p1/100],$[$p2/10],$[$p3/100],$[$p4/100] into x0_,y_,x1_,x2_
      <span class="wims_style_score">
       !if $p1!=$empty
        !set ff_$num=!mathsubst Q=$y_ in $(f_$num)
        !for sev=0 to 2
         !set ff_$num=!mathsubst I$sev=$(x$(sev)_) in $(ff_$num)
        !next
        $(wims_name_thsheet[9]): $[ceil(10*$(ff_$num))/10]
!!       $(wims_name_thsheet[5]): $p1%,
!!       $(wims_name_thsheet[7]): $[$p2]/10,
!!       $(wims_name_thsheet[10]): $p3%,
!!       $(wims_name_thsheet[13]): $[$p4/10]/10
      !else
        &nbsp;
      !endif
      !if showshweight iswordof $class_option
       ($wims_name_coeff
        !line $i of $wlist
       )
      !endif
   </span><!--wims_user_sheet_score-->
 </div>
    <div class="wims_sheet_desc">
       !set cut=!detag $de
       !set lim=140
        !set cut=!char 1 to $lim of $cut
        !set cutcnt=!wordcnt $cut
        !set cutchar=!charcnt $cut
        !if $cutchar = $lim
          !set cutlim=$[$cutcnt-1]
        !else
          !set cutlim=$[$cutcnt]
        !endif
        !word 1 to $cutlim of $cut
        !if $cutchar=$lim
        ...
        !endif
  </div><!--wims_sheet_desc-->

  </li><!--wims_sheet_item-->
  !reset a_,t_,ti,de
    !endif
   !endif
   !if $type=vote
     !set v_=!record $num of wimshome/log/classes/$wims_class/vote/.votes
     !distribute lines $v_ into a_,t_,ti,de_
     !if $a_>=1 and $a_<=2
     <li class="wims_vote_item">
      !href module=adm/vote&job=read&vote=$num&seq=$i $t_
     <div class="wims_vote_desc">$de_</div>
     </li
     !endif
     !reset a_,t_,ti,de_
   !endif
   !if $type=exam
     !set e_=!record $num of wimshome/log/classes/$wims_class/exams/.exams
     !distribute lines $e_ into a_,ex_,du_,t_,de_
     !if $a_>=1 and $a_<=2
     <li class="wims_exam_item">
      !href module=adm/class/exam&exam=$num&seq=$i $t_
  <span class="wims_user_exam_expire">
  !if $a_>=2
   $U_expired
  !else
   !if showexexpire iswordof $class_option
    !read misc/expire.phtml.$lang $ex_
   !else
    &nbsp;
   !endif
  !endif
  </span>
     <div class="wims_exam_desc">$de_</div>
     </li>
     !endif
    !reset a_,ex_,du_,nb_,t_,de_
   !endif

   !reset s_ v_ d_ e_
 !next i_
  </ul>
 </fieldset>
